//===== Custom eAthena Script =======================================
//= Quest para conseguir Bloody Branch
//===== By: =========================================================
//
//    010100110100100101010010010010010101010101010011
//   _________      __________         ___  __ ____  ___
//   \____   / ____ \______   \ ____  |   |/ _|\   \/  /
//      /   / /    \  |    ___//    \ |     <   \     /
//     /   /_(  >_> ) |   |   (  ~,~ )|   |  \  /     \ 
//    /_____ \\____/  |___|    \____/ |__/|__ \/___/\  \
//          \/                               \/      \_/
//
//    010100110100100101010010010010010101010101010011
//          ____  ____  ____  ____  ____  ____ 
//         ||S ||||I ||||R ||||I ||||U ||||S ||
//         ||__||||__||||__||||__||||__||||__||
//         |/__\||/__\||/__\||/__\||/__\||/__\|
//
//===== Descripción: =========================================
//= 250 Dead Branch ==> 1 Bloody Branch
//===== Version =====================================================
//= 2.3
//===== Changelog ===================================================
//= 1.0 Versión Original. [Sombra]
//= 1.1 Añadidos nuevos dialogos
//= 1.2 Añadido un primero NPC iniciador en Nifleheim ciudad
//= 1.3 Añadidos Colores para los diálogos
//= 2.0 Código re-escrito
//= 2.1 Añadidas Variables de Configuración
//= 2.2 Soporte para QuestLog añadido
//= 2.3 Adaptada Quest para Low Rates
//=     Eliminado .gat del nombre de los mapas
//===================================================================

//==========================
//===NPC del Trade. FIN===
//==========================
nif_fild01,325,64,7	script	Ghost#bloody2	802,{

//= CONFIG ================================================
set .deadb,		250;	//= Número de Dead Branch necesarias 
set .reward,	14234;	//= Recompensa por Quest (ID)
set .numrew,	1;		//= Cantidad Recompensa
//= END CONFIG ============================================

	set .nombre4$, "[Ghost]";
	mes .nombre4$;
	switch (BLOODY) {
		case 0:
		case 1:
		case 2:
			mes "Bienvenido a mi mundo...";
			mes "la tierra que llaman....";
			mes "Niflheim....";
			percentheal -35,-35;
			break;
		case 3:
			mes "Ella y... Mis compañeros... ¿Te envian?";
			if(checkquest(25052) != -1) {
				completequest 25052;
			}
			next;
			mes .nombre4$;
			mes "Siendo así... Te daré una recompensa por llegar hasta aquí... Pero sólo por esta vez...";
			getitem .reward,.numrew;
			set BLOODY,4;
			next;
			mes .nombre4$;
			mes "Si ellos confían en ti... ^088A08podría enseñarte algo más^000000...";
			break;
		case 4:
			mes "Nosotros... guardamos el secreto de las... ^B40404Bloody Branch^000000...";
			mes "Si te interesa aprenderlo... tráeme..." + .deadb + "... ^8A4B08Dead Branch^000000";
			next;
			switch (select("Aquí tienes:Te las traeré luego")) {
				case 1:
					mes .nombre3$;
					if (countitem(604) >= .deadb){
						mes "Esta... es una muestra... de nuestro poder...";
						getitem 12103,1;
						delitem 604,.deadb;
					}else{
						mes "...";
						next;
						mes .nombre4$;
						mes "No trates... De engañarme";
						percentheal -50,-50;
					}
					break;
				case 2:
					mes .nombre3$;
					mes "...";
					break;
			}
			break;
	}
	close;
}

//==========================
//===Espíritu Iniciador===
//==========================
niflheim,314,166,5	script	Spirit#bloody	802,{
	set .nombre1$, "[Spirit]";
	mes .nombre1$;
	switch (BLOODY) {
		case 0:
			mes "Bienvenido a mi mundo...";
			mes "la tierra que llaman....";
			mes "Niflheim....";
			percentheal -35,-35;
			next;
			mes .nombre1$;
			mes "...";
			next;
			mes "...";
			mes ".....";
			next;
			mes "..........";
			mes "No parece que este sea tu sitio...";
			mes "¿Has venido a propósito? ¿O te has perdido?";
			next;
			switch (select("Estoy por voluntad propia:Tengo miedo, quiero volver a casa...")) {
				case 1:
					query_sql "SELECT race FROM razas WHERE char_id = "+getcharid(0),.@raza$[0];
					mes .nombre1$;
					mes "Eres un "+.@raza$[0]+" valiente... ¿Cúal es tu nombre?";
					next;
					mes .nombre1$;
					mes "Conque te llamas ^2E9AFE" + strcharinfo(0) + "^000000... ese nombre suena prometedor...";
					next;
					mes .nombre1$;
					mes "De acuerdo, te revelarán nuestros secretos... una vez que encuentres a ^8A0886Alice^000000...";
					setquest 25050;
					set BLOODY,1;
					break;
				case 2:
					mes .nombre1$;
					mes "Los humanos no deberíais estar aquí.";
					mes "Vete. Ahora.";
					break;
			}
			break;
		case 1:
			mes "Necesitas encontrar a ^8A0886Alice^000000. Busca por los campos de mi mundo.";
			break;
		case 2:
			mes "^8A0886Alice^000000 te ha debido encomendar una labor...";
			break;
		case 3:
			mes "Deberías haber hecho caso a mis compañeros... No es por aquí.";
			break;
		case 4:
			mes "No tengo nada más que enseñarte...";
			break;
	}
	close;
}
//==========================
//===Alice===
//==========================
nif_fild02,362,280,5	script	Alice#bloody	792,{
	set .nombre2$, "[Alice]";
	mes .nombre2$;
	switch (BLOODY) {
		case 0:
			mes "¿Quién... Eres?";
			break;
		case 1:
			mes "Ah... Eres ^2E9AFE" + strcharinfo(0) + "^000000";
			mes "Los ^DF0174espíritus^000000 me han hablado de ti...";
			next;
			mes .nombre2$;
			mes "Eres poderoso... Así que ellos... Te ayudarán...";
			next;
			mes .nombre2$;
			mes "Ve al final de estos campos, y hallarás un ^DF0174fantasma^000000... Él te contará... Algo útil...";
			changequest 25050,25051;
			set BLOODY,2;
			break;
		case 2:
			mes "¿Todavía estás... aquí?";
			break;
		case 3:
			mes "Tú... parece que no aprendes...";
			break;
		case 4:
			mes "Le has caído bien... a los espíritus...";
			mes "Dicen... que puedes volver... cuando quieras...";
			break;
	}
	close;
}
//==========================
//===Continuador===
//==========================
nif_fild02,52,337,7	script	Ghost#bloody1	802,{
	set .nombre3$, "[Ghost]";
	mes .nombre3$;
	switch (BLOODY) {
		case 0:
			mes "Bienvenido a mi mundo...";
			mes "la tierra que llaman....";
			mes "Niflheim....";
			percentheal -35,-35;
			break;
		case 1:
			mes "Bienvenido a mi mundo...";
			mes "la tierra que llaman....";
			mes "Niflheim....";
			percentheal -35,-35;
			break;
		case 2:
			mes "Ella... ¿Te envía?";
			next;
			mes .nombre3$;
			mes "Si ella... lo dice... la creeré...";
			mes "Nosotros... hemos puesto esperanzas en tí.";
			next;
			mes .nombre3$;
			mes "Ve... al final... ^81F781donde comienza la vida^000000... ^380B61detrás de la muerte^000000...";
			changequest 25051,25052;
			set BLOODY,3;
			break;
		case 3:
			mes "...";
			break;
		case 4:
			mes "Los espíritus... confían en ti.";
			break;

	}
	close;
}
