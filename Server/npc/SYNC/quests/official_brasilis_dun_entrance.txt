//===== eAthena Script =======================================
//= Brasilis Quests Script
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Official quests for Brasilis
//===== Additional Comments: =================================
//= 1.0 First Release
//============================================================

brasilis,187,244,4	script	Mariana#01::bradunquest	72,{
	if (bradungeon < 1) {
		mes "[Mariana]";
		mes "Ghost! We saw a ghost!";
		next;
		mes "[Fernandez]";
		mes "We snuck past the guard while he was getting his favorite food, ^0000FFCoconuts^000000. Then when we got inside we heard a lady whispering something in the bathroom.";
		next;
		mes "[Daniel]";
		mes "So I got closer and opened the stall door, but nobody was in there! It had to be a ghost!";
		next;
		mes "[Fabio]";
		mes "The voice said ^FF0000'Mother, the door won't open!'^000000";
		next;
		mes "[Mariana]";
		mes "Then it said ^FF0000'Mother, the water keeps rising!'^000000";
		next;
		mes "[Fernandez]";
		mes "I heard it say ^FF0000'Mother, the drought is here!'^000000";
		next;
		mes "[Daniel]";
		mes "No, it said ^FF0000'Mother, where did all my friends go?'^000000";
		next;
		mes "[Fabio]";
		mes "Last it said ^FF0000'Mother, where are you mother?'^000000";
		next;
		set bradungeon,1;
		setquest 2208;
		mes "[Mariana]";
		mes "You better stay out of there, if the legend of the Bathroom Blonde is true she could kill you while you're doing your business!";
		close;
	}
	else {
		mes "[Fernandez]";
		mes "That guard loves ^0000FFCoconuts^000000, but don't go sneaking past him. If the legend of the Bathroom Blonde is true she could kill you while you're doing your business!";
		close;
	}
}

brasilis,185,246,6	duplicate(bradunquest)	Fernandez#02	62
brasilis,180,249,6	duplicate(bradunquest)	Daniel#03	706
brasilis,181,250,6	duplicate(bradunquest)	Fabio#04	706

bra_in01,7,181,4	script	Musium Guard	477,{
	mes "[Musium Guard]";
	mes "Hello, are you enjoying the musium today?";
	close;
}

bra_in01,12,185,4	script	#bathroom	45,2,2,{
	end;

OnTouch:
	if ((bradungeon >= 1) && (countitem(11515) >= 1)) {
		mes "[Musium Guard]";
		mes "Is that what I think it is?";
		next;
		mes "[Musium Guard]";
		mes "I'll let you in here if you give me that ^0000FFCoconut^000000.";
		next;
		switch(select("OK.:No way.")) {
		case 1:
			delitem 11515,1;
			mes "[Musium Guard]";
			mes "Thanks! Now hurry in before someone notices.";
			close2;
			warp "bra_in01",138,177;
			end;
		case 2:
			mes "[Musium Guard]";
			mes "Jerk.";
			close;
		}
	}
	else {
		mes "[Musium Guard]";
		mes "I'm sorry, this is a restricted area.";
		close;
	}
}

bra_in01,149,184,4	script	Door	844,{
	if (bradungeon == 1) {
		mes "Do you have something to tell me?~";
		next;
		input .@line$;
		if (.@line$ == "Mother, the door won't open!") {
			mes "You must turn it, dear~";
			next;
			switch(select("Pick the lock","Push the door","Turn the key","Kick the Door")) {
			case 1:
				mes "-No response-";
				close;
			case 2:
				mes "-No response-";
				close;
			case 3:
				mes "Seven more times~";
				next;
				input .@input;
				if (.@input == 7) {
					set bradungeon,2;
					specialeffect2 EF_VENOMDUST;
					mes "Good child!~";
					close;
				}
				else {
					mes "-No response-";
					close;
				}
			case 4:
				mes "-No response-";
				close;
			}
		}
		else {
			mes "-No response-";
			close;
		}
	}
	else {
		mes "-No response-";
		close;
	}
}

bra_in01,144,187,4	script	Toilet	844,{
	if (bradungeon == 2) {
		mes "Do you have something to tell me?~";
		next;
		input .@line$;
		if (.@line$ == "Mother, the water keeps rising!") {
			mes "You must flush it, dear~";
			next;
			switch(select("Flush the toilet","Clog the toilet","Put something in the toilet","Pee in the toilet")) {
			case 1:
				mes "Three more times~";
				next;
				input .@input;
				if (.@input == 3) {
					set bradungeon,3;
					specialeffect2 EF_VENOMDUST;
					mes "Good child!~";
					close;
				}
				else {
					mes "-No response-";
					close;
				}
			case 2:
				mes "-No response-";
				close;
			case 3:
				mes "-No response-";
				close;
			case 4:
				mes "-No response-";
				close;
			}
		}
		else {
			mes "-No response-";
			close;
		}
	}
	else if (bradungeon >= 6) {
		mes "Would you like to flush the toilet?";
		next;
		switch(select("Yes.","No thanks.")) {
		case 1:
			mes "-You hop in and get flushed down the drain-";
			close2;
			warp "bra_in01",206,101;
			end;
		case 2:
			close;
		}
	}
	else {
		mes "-No response-";
		close;
	}
}

bra_in01,134,189,4	script	Faucet	844,{
	if (bradungeon == 3) {
		mes "Do you have something to tell me?~";
		next;
		input .@line$;
		if (.@line$ == "Mother, the drought is here!") {
			mes "You must open it, dear~";
			next;
			switch(select("Stare at the faucet","Open the faucet","Close the faucet","Break the faucet")) {
			case 1:
				mes "-No response-";
				close;
			case 2:
				mes "One more time~";
				next;
				input .@input;
				if (.@input == 1) {
					set bradungeon,4;
					specialeffect2 EF_VENOMDUST;
					mes "Good child!~";
					close;
				}
				else {
					mes "-No response-";
					close;
				}
			case 3:
				mes "-No response-";
				close;
			case 4:
				mes "-No response-";
				close;
			}
		}
		else {
			mes "-No response-";
			close;
		}
	}
	else {
		mes "-No response-";
		close;
	}
}

bra_in01,138,184,4	script	Carpet	844,{
	if (bradungeon == 4) {
		mes "Do you have something to tell me?~";
		next;
		input .@line$;
		if (.@line$ == "Mother, where did all my friends go?") {
			mes "You must turn it, dear~";
			next;
			switch(select("Burn the carpet","Roll up the carpet","Turn the carpet","Walk on the carpet")) {
			case 1:
				mes "-No response-";
				close;
			case 2:
				mes "-No response-";
				close;
			case 3:
				mes "Seven more times~";
				next;
				input .@input;
				if (.@input == 7) {
					set bradungeon,5;
					specialeffect2 EF_VENOMDUST;
					mes "Good child!~";
					close;
				}
				else {
					mes "-No response-";
					close;
				}
			case 4:
				mes "-No response-";
				close;
			}
		}
		else {
			mes "-No response-";
			close;
		}
	}
	else {
		mes "-No response-";
		close;
	}
}

bra_in01,151,180,4	script	Mirror#01	844,{
	if (bradungeon == 5) {
		mes "Do you have something to tell me?~";
		next;
		input .@line$;
		if (.@line$ == "Mother, where are you mother?") {
			donpcevent "Bathroom Blonde#01::OnEnable";
			mes "I'm right here~";
			next;
			switch(select("Stay and listen","Run away")) {
			case 1:
				mes "My child, do you like to gamble?~";
				next;
				set .@warp1,rand(1,6);
				set .@warp2,rand(1,6);
				if (.@warp1 == 1) {
					emotion e_dice,1;
				}
				if (.@warp1 == 2) {
					emotion e_dice2,1;
				}
				if (.@warp1 == 3) {
					emotion e_dice3,1;
				}
				if (.@warp1 == 4) {
					emotion e_dice4,1;
				}
				if (.@warp1 == 5) {
					emotion e_dice5,1;
				}
				if (.@warp1 == 6) {
					emotion e_dice6,1;
				}
				if (.@warp2 == 1) {
					donpcevent "Bathroom Blonde#01::OnEmote";
				}
				if (.@warp2 == 2) {
					donpcevent "Bathroom Blonde#01::OnEmote2";
				}
				if (.@warp2 == 3) {
					donpcevent "Bathroom Blonde#01::OnEmote3";
				}
				if (.@warp2 == 4) {
					donpcevent "Bathroom Blonde#01::OnEmote4";
				}
				if (.@warp2 == 5) {
					donpcevent "Bathroom Blonde#01::OnEmote5";
				}
				if (.@warp2 == 6) {
					donpcevent "Bathroom Blonde#01::OnEmote6";
				}
				if (.@warp1 > .@warp2) {
					set bradungeon,6;
					mes "-You rolled a ^FF0000"+.@warp1+"^000000-";
					mes "-The ghost rolled a ^FF0000"+.@warp2+"^000000-";
					next;
					mes "A lucky child you are~";
					close2;
					donpcevent "Bathroom Blonde#01::OnInit";
					warp "bra_in01",206,101;
					end;
				}
				else if (.@warp1 < .@warp2) {
					set bradungeon,1;
					mes "-You rolled a ^FF0000"+.@warp1+"^000000-";
					mes "-The ghost rolled a ^FF0000"+.@warp2+"^000000-";
					next;
					mes "How unlucky my child is~";
					close2;
					donpcevent "Bathroom Blonde#01::OnInit";
					warp "bra_in01",12,182;
					end;
				}
				else {
					donpcevent "Bathroom Blonde#01::OnInit";
					mes "A tie... Let us try again~";
					close;
				}
			case 2:
				donpcevent "Bathroom Blonde#01::OnInit";
				mes "-She disappears-";
				close;
			}
		}
		else {
			mes "-No response-";
			close;
		}
	}
	else {
		mes "-No response-";
		close;
	}
}

bra_fild01,323,136,4	script	Sewer	844,{
	if (bradungeon >= 6) {
		mes "There is a pipe with a small hole in it, would you like to jump in?";
		next;
		switch(select("Yes.","No thanks.")) {
		case 1:
			mes "-You dive in and get sucked into the small hole-";
			close2;
			warp "bra_in01",206,185;
			end;
		case 2:
			close;
		}
	}
	else {
		mes "There is a pipe with a small hole in it.";
		close;
	}
}

bra_in01,206,188,4	script	Pipe#01	844,{
	if (bradungeon == 6) {
		donpcevent "Bathroom Blonde#02::OnEnable";
		getexp 90000,0;
		set bradungeon,7;
		completequest 2208;
		mes "[Bathroom Blonde]";
		mes "Good job my child, you have found my hiding spot~";
		next;
		mes "[Bathroom Blonde]";
		mes "But this is as far as I can take you~";
		next;
		mes "[Bathroom Blonde]";
		mes "Always remember the last thing your mother tells you~";
		next;
		mes "[Bathroom Blonde]";
		mes "Beware of the Boitata~";
		next;
		donpcevent "Bathroom Blonde#02::OnInit";
		mes "-The ghost disappears into the pipe-";
		next;
	}
	mes "Open the gate?";
	next;
	switch(select("Yes.","No thanks.")) {
	case 1:
		warp "bra_dun01",88,46;
		end;
	case 2:
		close;
	}
}

bra_dun01,88,43,4	script	Pipe#02	844,{
	mes "Enter the pipe?";
	next;
	switch(select("Yes.","No thanks.")) {
	case 1:
		warp "bra_in01",206,185;
		end;
	case 2:
		close;
	}
}

bra_in01,151,182,2	script	Bathroom Blonde#01	1867,{
	end;
OnInit:
	hideonnpc "Bathroom Blonde#01";
	end;
	
OnEnable:
	hideoffnpc "Bathroom Blonde#01";
	specialeffect EF_BEGINSPELL;
	end;

OnEmote:
	emotion 58;
	end;

OnEmote2:
	emotion 59;
	end;

OnEmote3:
	emotion 60;
	end;

OnEmote4:
	emotion 61;
	end;

OnEmote5:
	emotion 62;
	end;

OnEmote6:
	emotion 63;
	end;
}

bra_in01,206,191,4	script	Bathroom Blonde#02	1867,{
	end;
OnInit:
	hideonnpc "Bathroom Blonde#02";
	end;
	
OnEnable:
	hideoffnpc "Bathroom Blonde#02";
	specialeffect EF_BEGINSPELL;
	end;
}
