//===== eAthena Script =======================================
//= Arrow Quiver Event
//===== By: ==================================================
//= Muad_Dib (Prometheus Project); L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis COnversion]
//= Turns arrows into Arrow Quivers.
// Breakdown of Subroutine "S_BuyQuiver"
// arg(0): Type of Arrow to be packaged (item ID)
// arg(1): How many of each 'getarg(0)' arrow per quiver
// arg(2): The cost of making a 'getarg(0)' quiver.
// arg(3): The quiver given by the NPC. (item ID)
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to eAthena format by Dr.Evil
//= added prize to quest - 500 Zeny
//= Fixed a few spelling errors. [Nexon]
//= 1.1 Added Holy Arrow Quivers [Playtester]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=	Removed "Holy Arrows" from the list.
//============================================================

payon_in01,5,134,5	script	Inventor Jaax	89,{
	if (checkweight(1201,1) == 0) {
		mes "[Inventor Jaax]";
		mes "Hey, estás llevando";
		mes "demasiadas cosas. ¿Por qué no dejas todas esas cosas en Kafra Storage? Podemos hablar después de que hagas eso, ¿ok?";
		close;
	}
	set .@now_weight,MaxWeight-Weight;
	if (.@now_weight < 2000) {
		mes "[Inventor Jaax]";
		mes "Hey, estás llevando";
		mes "demasiadas cosas. ¿Por qué no dejas todas esas cosas en Kafra Storage? Podemos hablar después de que hagas eso, ¿ok?";
		close;
	}
	mes "[Inventor Jaax]";
	mes "Mi nombre es Jaxx.";
	mes "Sin vanagloriarme, puedo";
	mes "decir que tal vez soy el";
	mes "^663300mejor inventor de nuestra era^000000.";
	next;
	mes "[Inventor Jaxx]";
	mes "Ahora, he";
	mes "creado algo";
	mes "realmente extraordinario. Lo llamo ¡¡^663300Magic Quiver^000000 !! Esto será recordado en la historia como ¡¡la";
	mes "revolución en las flechas!!";
	Emotion e_no1;
	next;
	mes "[Inventor Jaax]";
	mes "He estudiado la magia y los quivers por años, trabajando día y noche hasta que por fin encontré como condensar flechas con magia! Con Magic Quivers, podrás llevar más flechas, ¡pero con menos peso!";
	next;
	mes "[Inventor Jaax]";
	mes "¿Te gustaría usar uno de mis arrow quivers? ¡No tengo ninguna duda que alguien como tu pueda apreciar mi ingenio!";
	next;
	switch(select("Quiver:Iron Arrow Quiver:Steel Arrow Quiver:Oridecon Arrow Quiver:Fire Arrow Quiver:Silver Arrow Quiver:Wind Arrow Quiver:Stone Arrow Quiver:Crystal Arrow Quiver:Shadow Arrow Quiver:Immaterial Arrow Quiver:Rusty Arrow Quiver")) {
	case 1: callsub S_BuyQuiver,1750,500,500,12004;
	case 2: callsub S_BuyQuiver,1770,500,500,12005;
	case 3: callsub S_BuyQuiver,1753,500,500,12006;
	case 4: callsub S_BuyQuiver,1765,500,500,12007;
	case 5: callsub S_BuyQuiver,1752,500,500,12008;
	case 6: callsub S_BuyQuiver,1751,500,500,12009;
	case 7: callsub S_BuyQuiver,1755,500,500,12010;
	case 8: callsub S_BuyQuiver,1756,500,500,12011;
	case 9: callsub S_BuyQuiver,1754,500,500,12012;
	case 10: callsub S_BuyQuiver,1767,500,500,12013;
	case 11: callsub S_BuyQuiver,1757,500,500,12014;
	case 12: callsub S_BuyQuiver,1762,500,500,12015;
	}
	//This doesn't seem to be accessable...
	//mes "[Inventor Jaax]";
	//mes "Is there";
	//mes "anything you want?";
	//close;

S_BuyQuiver:
	if (countitem(getarg(0)) > 499) {
		mes "[Inventor Jaax]";
		mes "¡¡Excelente!!";
		mes "¿Estás llevando algunas Flechas contigo? Te daré un quiver que puede llevarlas "+getarg(1)+" por ti"+getitemname(getarg(0))+"por solamente ^FF3131"+getarg(2)+" Zeny^000000.";
		next;
		switch(select("Store as many Arrows in quivers as possible:Purchase 1 quiver:Cancel")) {
		case 1:
			set .@arrows,countitem(getarg(0));
			set .@quiver,.@arrows / getarg(1);
			set .@arrows_used,.@quiver * getarg(1);
			set .@arrow_zeny01,.@quiver * getarg(2);
			mes "El número de ";
			mes "Flechas: ^3131FF"+.@arrows+" ^000000";
			mes "El número máximo";
			mes "de Quivers que se";
			mes "pueden comprar: ^3131FF"+.@quiver+" ^000000";
			mes "Zeny requerido: ^3131FF"+.@arrow_zeny01+" Zeny^000000";
			next;
			mes "[Inventor Jaax]";
			mes "¿Te gustaría comprar";
			mes "tantos Quivers como";
			mes "se puedan hacer con la";
			mes "cantidad de flechas que llevas?";
			next;
			if (select("Yes:Cancel") == 1) {
				if (.@arrow_zeny01 < Zeny) {
					mes "[Inventor Jaax]";
					mes "¡Ahí tienes!";
					mes "Solo recuerda, ^FF0000 que no podrás usar el Quiver cuando tu peso sea del 90% o más de tu límite máximo de peso.^000000.";
					set zeny,zeny-.@arrow_zeny01;
					delitem getarg(0),.@arrows_used; //Arrow
					getitem getarg(3),.@quiver; //Quiver
					next;
					mes "[Inventor Jaax]";
					mes "Así que...";
					mes "Sólo realiza un verificación";
					mes "de cuanto estás";
					mes "cargando de vez";
					mes "en cuando y vas";
					mes "a estar bien.";
					close;
				}
				else {
					mes "[Inventor Jaax]";
					mes "Lo lamento, pero no tienes suficiente Zeny. No puedo regalarte esto después de años de trabajo";
					mes "para desarrollar esta tecnología revolucionaria.";
					close;
				}
			}
			mes "[Inventor Jaax]";
			mes "¿Has cambiado de idea?";
			mes "Cuando la gloria de ser propietario";
			mes "de un quiver esta tan cerca";
			close;
		case 2:
			if (zeny > getarg(2)) {
				mes "[Inventor Jaax]";
				mes "¡Ahí tienes!";
				mes "Solo recuerda, ^FF0000 que no podrás usar el Quiver cuando tu peso sea del 90% o más de tu límite máximo de peso.^000000.";
				set zeny,zeny-getarg(2);
				delitem getarg(0),getarg(1); //Arrow
				getitem getarg(3),1; //Quiver
				next;
				mes "[Inventor Jaax]";
				mes "Así que...";
				mes "Sólo realiza un chequeo";
				mes "de cuanto estas";
				mes "cargando de vez";
				mes "en cuando y vas";
				mes "estar bien.";
				close;
			}
			else {
				mes "[Inventor Jaax]";
				mes "¿Ni siquiera";
				mes "tienes "+getarg(2)+" Zeny? ";
				mes "Lo siento mucho. No tenía";
				mes "ni idea de que eras tan";
				mes "^333333Pobre^000000.";
				close;
			}
		case 3:
			mes "[Inventor Jaax]";
			mes "¿Qué...?";
			mes "¡¿No te das cuenta que esta invención puede cambiar para siempre la forma en que se llevan las Flechas?! ¡El futuro es ahora!";
			close;
		}
	}
	else {
		mes "[Inventor Jaax]";
		mes "Puedes llevar un maximo de 500 Flechas dentro de este quiver. Se hizó utilizando mi método secreto,";
		mes "así que el peso total de las Flechas en el Quiver es menor que cargando las flechas solas.";
		next;
		mes "[Inventor Jaax]";
		mes "¡Es un milagro de la ciencia! Uno que puedes experimentar por ti mismo si me traes al menos 500 Flechas y 500 Zeny por cada Quiver.";
		close;
	}
}
