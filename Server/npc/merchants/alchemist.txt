//===== eAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= eAthena Team
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Selling Alchemist Materials and Manuals
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//= Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//============================================================ 

alde_alche,24,188,3	script	Guild Dealer	740,{
	if (checkweight(1201,1) == 0) {
		mes "- ¡Un minuto! -";
		mes "- Estas cargando -";
		mes "- demasiados objetos. -";
		mes "- Por favor, regresa -";
		mes "- hasta que te deshagas de algunos de ellos. -";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Bienvenido a la";
	mes "Union de Alchemist.";
	mes "¿En qué puedo ayudarte hoy?";
	next;
	switch(select("Purchase materials.:Purchase a production manual.:Quit.")) {
	case 1:
		mes "[Gever Al Sharp]";
		mes "¿Qué quisieras?";
		next;
		if (select("Medicine Bowl - 8 Zeny:Cancel.") == 1) {
			mes "[Gever Al Sharp]";
			mes "¿Cuántos quieres?";
			mes "Escribe '0' si quieres cancelar.";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "El trato fue cancelado.";
					mes "Regresa después.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "El número debe ser";
					mes "menor que 2000.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				mes "No tienes";
				mes "suficiente zeny.";
				mes "Checa cuanto zeny";
				mes "tienes primero.";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "Parece que no puedes";
				mes "cargar con todo.";
				mes "Por favor, checa el espacio";
				mes "en tu inventario.";
				close;
			}
			set zeny,zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[Gever Al Sharp]";
			mes "Gracias.";
			mes "Vuelve cuando quieras...";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "Bueno,";
		mes "regresa cuando";
		mes "necesites comprar";
		mes "materiales relacionados a la";
		mes "Alquimia, ¿está bien?";
		close;
	case 2:
		mes "[Gever Al Sharp]";
		mes "¿Qué necesitas?";
		mes "Los manuales generalmente cuestan 100,000 zeny. Pero hay un par de manuales especiales que costarían más.";
		next;
		switch(select("Potion Creation Guide:Alcohol Creation Guide:Bottle Grenade Creation Guide:Acid Bottle Creation Guide:Plant Bottle Creation Guide:Marine Sphere Bottle Creation Guide:Glistening Coat Creation Guide:Condensed Potion Creation Guide:Cancel Deal.")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "Bueno...";
			mes "Regresa cuando";
			mes "quieras comprar algún";
			mes "manual de producción.";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "Entonces,";
		mes "que tengas un buen día.";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[Gever Al Sharp]";
		mes "No tienes";
		mes "el suficiente zeny.";
		mes "Checa cuanto zeny";
		mes "tienes antes.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes ""+getitemname(getarg(0))+"?";
	mes "Seran "+getarg(1)+" zeny.";
	next;
	if (select("Purchase.:Quit.") == 1) {
		set zeny,zeny-getarg(1);
		getitem getarg(0),1;
		mes "[Gever Al Sharp]";
		mes "Gracias por";
		mes "tu patrocinio.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Regresa";
	mes "cuando quieras.";
	close;
}
