//===== eAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 2.0
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2	Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1	Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you loose some weight. -";
		close;
	}
	mes "[Seiyablem]";
	mes "Soy el ingeniero especialista en hacerle Slots a las armas y armaduras normales.";
	mes "El precio, los materiales necesarios y la posibilidad de éxito depende del item específico con el que trabajo.";
	next;
	mes "[Seiyablem]";
	mes "Poner Slots puede parecer simple, pero es más complicado de lo que parece.";
	mes "Si estás interesado en mis servicios, házmelo saber.";
	next;
	switch(select("Add Slot to Weapon:Add Slot to Armor:Slot Attempt Info:Cancel"))
	{
		case 1:
			mes "[Seiyablem]";
			mes "¿Quieres ponerle un Slot a un arma?";
			mes "Yo clasifiqué las armas de acuerdo a la dificultad de ponerle un Slot, Clase C las más fáciles y Clase S las más difíciles.";
			mes "¿De cuál clase te gustaría intentar?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Clase C, ¿eh?";
					mes "Ahora, yo puedo trabajar solo con ciertos equipos de esa clase, así que por favor escoge uno de los siguientes items, así podré tratar de ponerle un Slot.";
					next;
					switch(select("Trident:Rope:Violin"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes "¿Clase B?";
					mes "Ok, esta es la categoría de equipo regular.";
					mes "Por favor, elije el arma a la que te gustaría intentar ponerle un Slot.";
					next;
					switch(select("Chain:Gladius:Gakkung Bow:Pike:Haedonggum:Lute:Wire:Waghnakh:Arbalest Bow"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Ooh, Clase A.";
					mes "¡Este es territorio de alto riesgo!";
					mes "De acuerdo, ¿a qué arma quieres ponerle un Slot?";
					next;
					switch(select("Hunter Bow:Survivor's Rod(INT):Zweihander:Flamberge:Infiltrator:Ballista:Stunner:Berserk:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes "Whoa, whoa, whoa...";
					mes "¿Clase S? Ok...";
					mes "Sería un milagro si pudiera lograrlo.";
					mes "Ok... ¿A cuál arma intentaremos ponerle un Slot?";
					next;
					switch(select("Gungnir:Poison Knife:Ice Pick:Sucsamad:Ginnungagap:Cutlas:Crescent Scythe:Survivor's Rod(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes "¿Dijisté armadura?";
			mes "Claro, no hay problema.";
			mes "Las armaduras se dividen en 4 clases diferentes, dependiendo de la dificultad del trabajo.";
			mes "Clase C las más fáciles y Clase S las más difíciles.";
			mes "¿De cuál clase te gustaría probar?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Armadura clase C, ¿eh?...";
					mes "Suena razonable.";
					mes "¿A cuál armadura te gustaría ponerle un Slot?";
					next;
					switch(select("Mantle:Coat:Circlet:Biretta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "Has escogido la clase regular, Clase B";
					mes "Ok, ¿qué tipo de armadura tienes?";
					next;
					switch(select("Mirror Shield:Chain Mail:Saint Robe:Silk Robe:Boots:Shoes:Muffler:Guard:Buckler:Shield:Bongun Hat"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Ooh, Clase A.";
					mes "¡Este es territorio de alto riesgo!";
					mes "Ok... ¿En cuál armadura te gustaría poner un Slot?";
					next;
					switch(select("Gemmed Sallet:Bucket Hat:Memory Book:Tights:Legion Plate Armor:Full Plate:Thief Clothes:Greaves:Coif:Manteau:Helm:Ninja Suit:Orc Helm:Ancient Cape:Monk Hat:Golden Gear:Brooch:Munak Hat"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "Um... ¿Clase S?";
					mes "¿Estás seguro de eso?";
					mes "Ok, dime cual es la armadura en la quieres que ponga un Slot y te diré que items necesito.";
					next;
					switch(select("Majestic Goat:Spiky Band:Bone Helm:Corsair:Crown:Tiara:Sphinx Hat:Robe of Cast:Earring:Ring:Bow Thimble"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "Bueno, realmente no he refinado mi arte de Poner Slots.";
			mes "Es tan complicado que te estaría mintiendo si te dijera que conozco cada factor que afecta el proceso.";
			mes "No obstante, noto algunas tendencias...";
			next;
			mes "[Seiyablem]";
			mes "En algunas ciudades, la posibilidad de poner un Slot es más alta en equipos con menos refinación.";
			mes "En otras ciudades, es lo contrario.";
			mes "¿No es algo extraño?";
			next;
			mes "[Seiyablem]";
			mes "Oh, there's also something really important that you should know.";
			mes "Cuando me pidas poner un Slot a algo, asegúrate que no tengas varios items iguales en tu inventario.";
			next;
			mes "[Seiyablem]";
			mes "Un ejemplo: si tienes un Manteau +7 y un Manteau normal en tu inventario, yo aleatoriamente escogeré uno de ellos para poner el Slot.";
			mes "Sólo recuerda ser cuidadoso...";
			next;
			mes "[Seiyablem]";
			mes "Otra vez, ^FF0000sólo lleva contigo uno de los items a los que quieres que le ponga un slot^000000.";
			mes "Todo equipo con el mismo nombre debes de dejarlo en el Kafra Storage, ¿entendiste?";
			close;
		case 4:
			mes "[Seiyablem]";
			mes "Tranquilo, aventurero.";
			mes "Si en algún momento quisieras ponerle un Slot a algún equipo, regresa y házmelo saber.";
			mes "Nos vemos~";
			close;
	}
}

function	script	Func_Socket	{

	mes "[Seiyablem]";
	mes "¿Quieres ponerle un Slot a " + getitemname(getarg(0)) + "?";
	mes "Ok, por favor tráeme ^FF0000" + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + "^000000, " + (getarg(7,0) != 0 && getarg(8,0) != 0 ? ("^FF0000" + getarg(8,0) + " " + getitemname(getarg(7,0)) + (getarg(8,0) > 1 && getarg(7,0) != 999 ? "s":"") + "^000000 ") : "") + "and my ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 zeny^000000 como pago por el servicio.";
	mes "Ah, no olvides traer eso. " + getitemname(getarg(0)) + "!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(getarg(0),5)&2) // EQP_HAND_R = 2, it's a weapon
	{
		mes "Puedo intentar poner un Slot ahora, si tienes los items requeridos y el zeny.";
		mes "Sin embargo, debes saber que hay la posibilidad de que pueda fallar.";
		mes "Por lo tanto, necesito darte una advertencia justa...";
		next;
		mes "[Seiyablem]";
		mes "Si este intento de poner un Slot falla, entonces el ^FF0000Arma^000000, ^FF0000 y las cartas que tenga serán destruidas^000000.";
	}
	else // armor
	{
		mes "Si tienes los items necesarios, mi zeny por el servicio y la armadura, entonces podemos proceder con el intento de poner el Slot.";
		mes "Pero primero, debo advertirte sobre el riesgo.";
		next;
		mes "[Seiyablem]";
		mes "Si el intento de poner un Slot falla, entonces la ^FF0000Armadura^000000, ^FF0000las refinaciones^000000 ^FF0000y las cartas que tenga serán destruidas^000000.";
	}
	mes "¿Todavía quieres intentar poner un Slot?";
	next;
	switch(select("Attempt Slot Addition:Cancel"))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "Ok, ¡empecemos con el trabajo!";
				mes "Mejor reza para tener buenos resultados.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "Bien, parece ser exitoso.";
					mes "Se ve muy bien hecho. ¡Felicidades!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "¡Nos vemos, amigo!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "¡Wah! ...Lo siento tanto, el proceso falló.";
					mes "Sin embargo, yo soy completamente inocente.";
					mes "Es tu suerte y el destino de dios, ¿ok?";
					mes "No te decepciones y prueba la próxima ves.";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					next;
					mes "[Seiyablem]";
					mes "¡Te deseo buena suerte para la próxima!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "Me gustaría poder poner el Slot, pero te faltan algunas cosas.";
				mes "¿Estás seguro que tienes el equipo, los materiales y el zeny?";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "Necesitas tiempo para pensarlo, ¿huh?";
			mes "Ok, yo entiendo.";
			mes "Solo recuerda que la vida no es divertida si siempre la juegas a lo seguro~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes "Estoy pesando en hacerme un Ingeniero poniendo Slots.";
	mes "Ponerle Slots a los equipos es un nuevo campo y sería genial si pudiera trabajar en tan lucrativa carrera.";
	next;
	mes "[Young Man]";
	mes "Por lo que puedo entender, hay un elemento enorme de suerte implicado en poder poner un Slot con éxito.";
	mes "A veces fallas, pero si tienes mucha suerte podes poner dos Slots.";
	mes "Loco. ¿Huh?";
	next;
	mes "[Young Man]";
	mes "Pero no te emociones.";
	mes "Solo algunos equipos pueden aguantar la adición de dos Slots";
	mes "Hay muchas cosas más que tengo que aprender antes de poder ser por lo menos un aprendiz...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	97
payon,143,143,7	duplicate(SocketMan)	Young Man#pay	97
morocc,60,42,3	duplicate(SocketMan)	Young Man#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	97
