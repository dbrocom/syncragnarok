//===== eAthena Script ======================================= 
//= Inn Npcs
//===== By: ================================================== 
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: ===================================== 
//= 3.0a
//===== Compatible With: ===================================== 
//= eAthena 1.0+
//===== Description: ========================================= 
//= Inn Npcs, Guardar and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to Guardar at
//= arg3, arg4 - x and y cord. to Guardar at
//= @cost - cost of renting a room
//===== Additional Comments: ================================= 
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=	  and Guardar points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Salir dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as Guardar point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 3.0 Added Brasilis inn receptionist. [L0ne_W0lf]
//= 3.0a Updated dialog for Brasilis inn. [L0ne_W0lf] 
//============================================================ 


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	Inn Employee#Sammy	53,{

	callfunc "F_InnMaid","[Employee Sammy]","Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	Inn Employee#Ahlma	53,{

	callfunc "F_InnMaid","[Employee Ahlma]","Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	Inn Employee#Jennie	53,{

	callfunc "F_InnMaid","[Employee Jennie]","'Fisherman Inn'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Inn Employee#Cena	53,{

	callfunc "F_InnMaid","[Employee Cena]","'Ifrit,' the only Inn in the city of Geffen.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	Inn Employee#Ahee	53,{

	callfunc "F_InnMaid","[Employee Ahee]","Payon Inn","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	Inn Employee#Hasna	53,{

	callfunc "F_InnMaid","[Employee Hasna]","Morroc Inn","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	Inn Employee#Manar	53,{

	callfunc "F_InnMaid","[Employee Manar]","Morroc Inn","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Inn Maid#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","Al De Baran Inn","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Inn Keeper#Annie	931,{

	callfunc "F_InnMaid","[Annie]","Rachel Inn","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Hotel Employee#01	86,{
	mes "[Hotel Employee]";
	mes "Bienvenido a";
	mes "al Royal Dragón,";
	mes "donde puedes encontrar las más";
	mes "finas habitaciones";
	mes "y el mejor servicio.";
	next;
	switch(select("Guardar:Descansar - 4,000 zeny:Salir")) {
	case 1:
		savepoint "lhz_in02",209,275;
		mes "[Hotel Employee]";
		mes "Gracias, tu";
		mes "punto de aparición ha";
		mes "sido guardado en";
		mes "el Royal Dragón.";
		close;
	case 2:
		if (!callfunc ("DelCoin",4000,1)) {
			mes "[Hotel Employee]";
			mes "Lo siento, pero necesitas 4,000 zeny para poder reposar aquí.";
			close;
		}
		mes "[Hotel Employee]";
		mes "Gracias~";
		mes "Espero que disfrutes";
		mes "tu estadía en el";
		mes "Royal Dragon.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		sc_start SC_BLESSING,240000,10;
		skilleffect AL_BLESSING,0;
		end;
	case 3:
		mes "[Hotel Employee]";
		mes "Gracias y que tengas un buen día.";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Inn Maid#Receptionist	53,{

	callfunc "F_InnMaid","[Receptionist]","Hugel Inn","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Inn Master#Receptionist	709,{
	mes "[Inn Master]";
	mes "Buen día~";
	mes "Bienvenido al";
	mes "más cómodo";
	mes "inn aquí en Veins~";
	next;
	switch(select("Guardar:Descansar -> 3500 zeny:Salir")) {
	case 1:
		mes "[Inn Master]";
		mes "Tu Punto de Reaparición";
		mes "ha sido guardado en Veins.";
		mes "Disfruta tu estadía en el pueblo~";
		savepoint "ve_in",157,209;
		close;
	case 2:
		mes "[Inn Master]";
		if (!callfunc ("DelCoin",3500,1)) {
			mes "Lo siento, pero necesitas 3,500 zeny para poder reposar aquí.";
			close;
		}
		mes "Disfruta tu estadía~";
		close2;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[Inn Master]";
		mes "por favor vuelve otra vez.";
		close;
	}
}

//======================== Brasilis =================================
bra_in01,27,24,3	script	Hotel Keeper#bra1	478,{ 
	mes "[Hotel Keeper]";
	mes "Welcome to the beautiful Brasilis Hotel.";
	next;
	switch(select("Guardar:Descansar - 3,500 zeny")) {
	case 1:
		mes "[Hotel Keeper]"; 
		mes "Do you want to save here at the Brasilis Hotel?";
		next;
		switch(select("No thank you.:Absolutely.")) {
		case 1:
			mes "[Hotel Keeper]"; 
			mes "Ok then, enjoy your stay.";
			close;
		case 2:
			mes "[Hotel Keeper]";
			mes "Your respawn has been saved here at the hotel. I hope that you enjoy your stay here in Brasilis.";
			savepoint "bra_in01",144,69;
			close;
		}
	case 2:
		if (!callfunc ("DelCoin",3500,1)) {
			mes "Lo siento, pero necesitas 3,500 zeny para poder descansar aquí una noche.";
			close;
		}
		else {
			mes "[Hotel Keeper]";
			mes "I will show you a great room."; 
			close2;
			percentheal 100,100;
			warp "bra_in01",144,69;
			end;
		}
	}
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	mes getarg(0);
	mes "Bienvenido a " + getarg(1) + ".";
	mes "¿En qué puedo ayudarte?";
	next;
	switch(select("Guardar:Descansar -> 3500 zeny:Salir")) {
	case 1:
		mes getarg(0);
		mes "Tu Punto de Reaparición se ha establecido en esta posada.";
		mes "Gracias, y vuelve cuando quieras.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	case 2:
		mes getarg(0);
		if (!callfunc ("DelCoin",3500,1)) {
			mes "Lo siento, pero el coste es de 3,500 zeny.";
			mes "Asegúrate de tener el dinero la próxima vez, ¿vale?";
			close;
		}
		mes "Muy bien.";
		mes "Espero que disfrutes del descanso~";
		close2;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
