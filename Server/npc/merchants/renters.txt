//===== eAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//============================================================ 

// PecoPeco Breeder (for Knights)-------------------------------------------
prontera,55,350,5	script	Peco Peco Breeder#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[Peco Peco Breeder]";
	if (BaseJob == Job_Knight) {
		mes "Bienvenido.";
		mes "honorable Knight, ";
		mes "¿Te gustaría alquilar";
		mes "un Peco Peco? El costo del";
		mes "alquiler es "+.@price+" zeny. ";
		next;
		switch(select("Rent Peco Peco:Cancel")) {
		case 1:
			if (zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "No tienes";
				mes "suficiente zeny.";
				mes "¿Te encuentras...";
				mes "en bancarrota?";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "Lo siento, pero no";
				mes "eres adecuado para este";
				mes "servicio. Por favor aprende";
				mes "la skill Peco Peco Ride primero.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "Ya estás";
				mes "montado en un";
				mes "Peco Peco. ";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Peco Peco Breeder]";
			mes "Ya veo.";
			mes "Bueno entonces,";
			mes "que tengas un buen día.";
			close;
		}
	} else {
		mes "Lo siento, pero estos";
		mes "Peco Pecos sólo están";
		mes "disponibles para Knights";
		mes "y Lord Knights.";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)-------------------------------------------
prontera,232,318,3	script	Peco Peco Breeder#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[PecoPeco Breeder]";
	if (BaseJob == Job_Crusader) {
		if(Upper != 1 ) mes "Bienvenido, Crusader.";
		else mes "Bienvenido, Paladin.";
		mes "Tenemos preparado un";
		mes "Peco Peco especial";
		mes "para ti. Alquilar uno";
		mes "le costará "+.@price+" zeny. ";
		next;
		switch(select("Rent a PecoPeco:Quit")) {
		case 1:
			if (zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "No tienes";
				mes "suficiente zeny.";
				mes "Si quieres un";
				mes "Peco Peco por favor";
				mes "trae "+.@price+" zeny... ";
				close;
			}

			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "Primero debes aprender";
				mes "a montar un PecoPeco antes";
				mes "que yo pueda alquilarte uno.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "Ya estás";
				mes "montado en un Peco Peco.";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[PecoPeco Breeder]";
			mes "Nos vemos.";
			close;
		}
	} else {
		mes "¿Qué puedo hacer por ti?";
		mes "Por favor sepa que";
		mes "este servicio de alquiler de Peco Peco";
		mes "es estrictamente para";
		mes "Crusaders y Paladins.";
		close;
	}
}

// Falcon Master--------------------------------------------------------------
hu_in01,381,304,5	script	Falcon Breeder#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[Falcon Breeder]";
	if (BaseJob == Job_Hunter) {
		mes "¿Necesitas un Halcón?";
		mes "Puedes alquilar tu propia";
		mes "ave de presa leal por";
		mes "un costo de "+.@price+"zeny~";
		next;
		switch(select("Rent Falcon:Cancel")) {
		case 1:
			if (zeny < .@price) {
				mes "[Falcon Breeder]";
				mes "¿Qué es esto?";
				mes "¡¿No tienes";
				mes "suficiente Zeny?!";
				mes "Mejor que empieces";
				mes "a cazar dinero";
				mes "en lugar de monstruos~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Falcon Breeder]";
				mes "Gosh.~";
				mes "¡Ve a aprender como";
				mes "controlar un Halcón";
				mes "primero! No puedo darte uno";
				mes "si no puedes";
				mes "controlarlo, tu sabes.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Falcon Breeder]";
				mes "Um... ";
				mes "Ya tienes";
				mes "un Halcón. Está justo";
				mes "ahí, ¿no puedes verlo?";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Falcon Breeder]";
			mes "E-espera, ¿A dónde";
			mes "vas? Estos Halcones";
			mes "son de primera clase,";
			mes "¡Te lo garantizo! Vamos~";
			close;
		}
	} else {
		mes "¡Joven tontol!";
		mes "Los Halcones solo pueden";
		mes "ser usados por Hunters";
		mes "y Snipers, ¿capish?";
		mes "...Heh heh, ¿celoso?";
		close;
	}
}
