//===== eAthena Script =======================================
//= War of Emperium Managers for N Guild Castles
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena 0.1+; RO Episode 4+
//===== Description: =========================================
//=
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, etc.
//===== Additional Comments: =================================
//= 1.0 Based off existing guild scripts.  Do not know if it is accurate.[kobra_k88]
//= 1.1 Added modified guild manager function. [L0ne_W0lf]
//= 	Removed guardian summoning.
//= 1.2 Corrected function name. (bugreport:1728) [L0ne_W0lf]
//============================================================

//==============================================
function	script	F_GldManager	{

	set @GID, GetCastleData(getarg(1),1);
	mes "[ "+getarg(0)+" ]";
	if (@GID == 0){
		mes "Estoy esperando a mi maestro. ¡Valiente aventurero, sigue tu destino!";
		return;
	}
	if (getcharid(2) != @GID){
		mes "Estoy aquí para seguir ^5533FF" + getguildmaster(@GID) + "^000000's Comandante. ¡Hey! ¡Tú ni siquiera eres parte del gremio!";
		mes "¿Dónde están los guardianes? ¡Destruyan estos intrusos!";
		return;
	}
	if (strcharinfo(0) != getguildmaster(@GID)){
		mes "Usted no ^5533FF" + getguildmaster(@GID) + "^000000! Estoy aquí para seguir ^5533FF" + getguildmaster(@GID) + "^000000's Mando único";
		return 0;
	}

	mes "Bienvenido Maestro ^5533FF" + getguildmaster(@GID) + "^000000  ¡Voy a ayudarle en todo lo que pueda!";
	next;

	menu "Kafra personal Empleo / despido",M_Kaf, "Entrar al Treasure Room",M_Treas, "Cancelar",M_End;

	//===========================
	M_Kaf:
	//======
		mes "[ "+getarg(0)+" ]";
		if (GetCastleData(getarg(1),9) == 1) goto L_Dismiss;
		if (getgdskilllv(@GID,10001) == 0){
			mes "Maestro, usted no tiene un contrato con la Empresa del Personal de Kafra.";
			mes "Con el fin de contratar a un Kafra, primero debe aprender la habilidad de la Guild ^5533FFContract With Kafra^000000.";
			return;
		}

		L_Hire:
			mes "¿Le gustaría emplear los servicios de un Kafra? Usted necesitará ^5533FF10,000 Zeny^000000 Para hacerlo ... ";
			next;
			menu "Emplear Kafra.",-,"Cancelar",sM_KafEnd;

				mes "[ "+getarg(0)+" ]";
				if (Zeny < 10000){
					mes "Maestro, usted no tiene suficiente dinero para emplear a un Kafra. El empleo ha sido cancelado.";
					return;
				}
				set Zeny,Zeny-10000;
				enablenpc "Kafra Staff#"+getarg(4);
				SetCastleData getarg(1),9,1;
				mes "Ustedes han creado un contrato con la Empresa del Personal de Kafra.";
				next;
				cutin "kafra_01",2;
				mes "[ Kafra Staff ]";
				mes "¿Algo que hacer? ¡Estoy a su disposición para proporcionarle un servicio útil! Lo haré lo mejor que pueda para servirle a usted.";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				//mes "Your employment contract lasts ^5533FF1 month^000000.  After this term is over you will have to create a new contract.";
				mes "Creo que el Personal de Kafra beneficiarán a los miembros de nuestro gremio.";
				return;

			sM_KafEnd:
				mes "[ "+getarg(0)+" ]";
				mes "Maestro como desee. Pero sugiero que debe obtener una Kafra lo antes posible!";
				return;

		L_Dismiss:
			mes "¿Le gustaría despedir la actual Kafra?";
			next;
			menu "Despedir",-,"Cancelar",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[ Kafra Staff ]";
				mes "¿He hecho algo mal? Si lo hice, ¿por favor me perdone?";
				next;
				menu "Despedir",-,"Cancelar",ssM_KafEnd2;

					mes "[ Kafra Staff ]";
					mes "Es lamentable que no voy a ser capaz de servir a su gremio más ....";
					next;
					disablenpc "Kafra Staff#"+getarg(4);
					SetCastleData getarg(1),9,0;
					cutin "kafra_01",255;
					mes "[ "+getarg(0)+" ]";
					mes "El Kafra ha sido despedida. Pero ... ¡Realmente deberíamos tener una Kafra lo antes posible!";
					return;
				ssM_KafEnd2:
					mes "[ Kafra Staff ]";
					mes "Gracias maestro, haré lo mejor! ^ ^.";
					cutin "kafra_01",255;
					return;
			sM_KafEnd2:
				mes "[ "+getarg(0)+" ]";
				mes "Maestro, creo que hay que mantener la actual Kafra, porque ella ha intentando dar su mejor esfuerzo para servirnos";
				return;

	//=========================
	M_Treas:
	//========
		mes "[ "+getarg(0)+" ]";
		mes "¿Para ir a nuestra Sala de Tesoros? Sólo usted, el Guild Master, se le permite la entrada a esta sala.";
		next;
		menu "Entrar al Treasure room.",-,"Cancelar",sM_TresEnd;

			mes "[ "+getarg(0)+" ]";
			mes "Por favor, siga conmigo a través del pasaje secreto.";
			mes "Usted debe tirar abajo en el interruptor secreto, a fin de salir.";
			next;
			warp getarg(1),getarg(2),getarg(3);
			return;
		sM_TresEnd:
			mes "[ "+getarg(0)+" ]";
			mes "Las mercancías se producen todos los días.";
			mes "Usted debe conseguirlo cada vez que puede, ya que podría desaparecer si se toman en el momento equivocado.";
			return;


	//==========================
	M_End:
	//=======
		mes "[ "+getarg(0)+" ]";
		mes "Como lo desee, maestro.";
		return;
}

// Castle 1 ==================================================================================
nguild_alde,218,175,0	script	Frolo	55,{
	callfunc "F_GldManager","Frolo","nguild_alde",119,223,"N01";
	close;
}


// Castle 2 ==================================================================================
nguild_gef,40,48,5	script	Leiber	55,{
	callfunc "F_GldManager","Leiber","nguild_gef",155,112,"N02";
	close;
}


// Castle 3 ==================================================================================
nguild_pay,120,58,4	script	Dundar	55,{
	callfunc "F_GldManager","Dundar","nguild_pay",290,7,"N03";
	close;
}


// Castle 4 ==================================================================================
nguild_prt,112,181,0	script	Thefton	55,{
	callfunc "F_GldManager","Thefton","nguild_prt",15,209,"N04";
	close;
}
