//===== eAthena Script ======================================= 
//= Renewal Acolyte Job Change
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Job Change to Acolyte Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added back the npcs used for the priest quest. 
//============================================================ 
prt_church,184,41,4	script	Cleric#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[Padre Mareusis]";
				mes "Hmmm, noto que has sufrido experiencias en una vida pasada.";
				mes "Has debido aprender muchas cosas antes de entrar al Valhalla.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Padre Mareusis]";
					mes "Desafortunadamente, no creo que estés preparado para convertirte en Acólito todavía. Por favor termina primero todo tu aprendizaje elemental.";
					next;
					mes "[Padre Mareusis]";
					mes "Mientras, estaré esperando a que estés preparado.";
					mes "Que los Dioses estén contigo.";
					close;
				}
				mes "[Padre Mareusis]";
				mes "Te doy la bienvenida otra vez desde tu estancia en el Valhalla,";
				mes "y te deseo mucha suerte en tu nuevo viaje.";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[Padre Mareusis]";
				mes "Ahora, aventúrate a encontrar aquellos que necesiten tu ayuda. Que los Dioses guíen tus pasos.";
				close;
			}else{
				mes "[Padre Mareusis]";
				mes "Aventúrate a encontrar aquellos que necesiten tu ayuda. Que los Dioses guíen tus pasos.";
				close;
			}
		}else{
			mes "[Padre Mareusis]";
			mes "Percibo que has vivido muchas experiencias en una vida pasada. Debes haber aprendido muchas cosas antes de llegar al Valhalla.";
			next;
			mes "[Padre Mareusis]";
			mes "Sin embargo, lamento decir que no estás capacitado para ser Acolyte. Recuerda quién fuiste en tu vida pasada, y encuentra tu camino.";
			close;
		}
	}
	mes "[Padre Mareusis]";
	mes "¿Qué es lo que buscas?";
	next;
	switch(select("Convertirme en acolyte.:¿Cuáles son los requerimientos para ser acolyte?.:Me he equivocado, disculpe.")) {
	case 1:
		if(Class != Job_Novice && Class != Job_Baby) {
			mes "[Padre Mareusis]";
			if(Class == Job_Acolyte || Class== Job_Baby_Acolyte) {
				mes "¿Te encuentras bien? Puedo asegurarte que ya eres un acolyte. ¿O acaso querías gastarme una broma?";
			}else{
				mes "Lo siento, pero parece que ya tienes otra profesión, ¿me equivoco?";
			}
			close;
		}else{
			mes "[Padre Mareusis]";
			mes "¿De verdad deseas convertirte en un sirviente de los Dioses?";
			mes "Veamos si estás preparado para ello o no... Hmm...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Padre Mareusis]";
				mes "!Por los Dioses! ¡¿Todavía no has completado el entrenamiento básico?! ¡Tienes un largo camino por delante! ¡Vuelve cuando hayas aumentado tu Nivel de Job!";
				close;
			}
			mes "[Padre Mareusis]";
			mes "Hmm... tienes suficiente entrenamiento...";
			mes "Eso está bien. Entonces puedo darte la calificación para convertirse en Acolyte.";
			next;
			mes "[Padre Mareusis]";
			mes "Recuerda ser siempre agradecido con los Dioses; ellos son quienes nos protegen todo el tiempo. En época de dificultad, enfréntate a tus problemas con una férrea fé.";
			if (Class == Job_Novice){ callfunc "Job_Change",Job_Acolyte; }
			else if (Class == Job_Baby) { callfunc "Job_Change",Job_Baby_Acolyte; }
			getitem 1545,1;	// N_Mace
			next;
			mes "[Padre Mareusis]";
			mes "Por último, mi más sincera enhorabuena por tu perserverancia.";
			close;
		}
	case 2:
		mes "[Padre Mareusis]";
		mes "¿Deseas convertirte en Acolyto?";
		mes "Entonces, deberás cumplir los siguientes requisitos ya que son prácticas dadas por los Dioses.";
		next;
		mes "[Padre Mareusis]";
		mes "Lo primero, deberás alcanzar el Nivel de Job 9 de Novice y aprender todas las Basic Skills.";
		mes "Tienes que considerarlo como tu primer entrenamiento. Es algo básico que todos hemos tenido que hacer alguna vez.";
		next;
		mes "[Padre Mareusis]";
		mes "Cuando creas que cumples todos los requisitos, vuelve a verme. Así tendrás una profesion divina y podrás difundir la voluntad de los Dioses.";
		close;
	case 3:
		close;
	}
}

// NPCs para Quest Priest

prt_fild03,365,255,2	script	Ascetic#aco	89,{
	mes "[Father Rubalkabara]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "Please take care. They should know that you've met me by the time you arrive at the Prontera Sanctuary.";
			next;
			mes "[Father Rubalkabara]";
			mes "I've sent a carrier pigeon with a message. I hope it will arrive there safely...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "Oh...? You must be the one who aspires to become an Acolyte. I've already received news from the Sanctuary that you might be coming.";
				next;
				mes "[Father Rubalkabara]";
				mes "Now, your name was " + strcharinfo(0) + ", right? Excellent, thank you for visiting me.";
				next;
				mes "[Father Rubalkabara]";
				mes "I believe you've been told much about Acolytes from Friar Mareusis. Plus, there's plenty of helpful people in the Prontera Sanctuary.";
				next;
				mes "[Father Rubalkabara]";
				mes "I guess there's really no need for me to teach you much. Besides, I'm sure your someone from your generation may have trouble listening to an old man like me. Hahaha~";
				next;
				mes "[Father Rubalkabara]";
				mes "Still, lessons may come from the places you'd least expect. God loves to teach his children in strange ways. You'll see.";
				next;
				mes "[Father Rubalkabara]";
				mes "Well, I'll send the message telling them that you've come to visit me. So, you may now return to the Prontera Sanctuary.";
				next;
				mes "[Father Rubalkabara]";
				mes "Farewell.";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "Oh...";
				mes "Are you one of the";
				mes "Acolyte applicants...?";
				mes "Let's see...";
				next;
				mes "[Father Rubalkabara]";
				mes "Your name is " + strcharinfo(0) + "?";
				mes "I don't think your name";
				mes "is on my list. Hmmm...";
				next;
				mes "[Father Rubalkabara]";
				mes "Why don't you go back to the Prontera Sanctuary and check again?";
				close;
			}
		}
		else {
			mes "Huh? What brings you here? This is a very dangerous place for a Novice like yourself!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Greetings.";
			next;
			mes "[Father Rubalkabara]";
			mes "Welcome to the Deep. Feel free to sit and contemplate God's message with me. This place is beautiful, even if danger accompanies its sense of serenity...";
			close;
		}
		else {
			mes "Oh ho...";
			mes "Have you come into the Deep here for training? Or are you just a Wanderer?";
			next;
			mes "[Father Rubalkabara]";
			mes "Whoever you are, please take care of yourself. The monsters in here are shockingly strong, contrary to their cute appearance.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Ascetic#2aco	95,{
	mes "[Mother Mathilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "I will send a carrier pigeon to the Prontera Sanctuary. When you return, the Priest there should already have received my message.";
			next;
			mes "[Mother Mathilda]";
			mes "I will pray to God, and hope that you become an Acolyte soon.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "Ah, you must be one of the Acolyte applicants. I sincerely welcome you.";
				next;
				mes "[Mother Mathilda]";
				mes "What is your name? " + strcharinfo(0) + "? Let's see... Ah, you're on my list.";
				next;
				mes "[Mother Mathilda]";
				mes "I will send a message to the Sanctuary confirming that you, " + strcharinfo(0) + " visited me and completed your penance.";
				next;
				mes "[Mother Mathilda]";
				mes "Please return to the Prontera Sanctuary and speak to the Priest in charge.";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "Ah...!";
				mes "You must be one";
				mes "of the Acolyte applicants.";
				mes "I sincerely welcome you.";
				next;
				mes "[Mother Mathilda]";
				mes "Now, what is your name?";
				mes "" + strcharinfo(0) + "? Let's see...";
				next;
				mes "[Mother Mathilda]";
				mes "Hmm...";
				mes "It seems your name";
				mes "is not on my list...";
				next;
				mes "[Mother Mathilda]";
				mes "Perhaps you should return to the Prontera Sanctuary and check the destination for your penance trial once again.";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "Hello there~";
			next;
			mes "[Mother Mathilda]";
			mes "How is your practice coming along? I certainly hope you're enjoying living in the grace of God.";
			close;
		}
		else {
			mes "May God";
			mes "be with you...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Ascetic#3aco	98,{
	mes "[Father Yosuke]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "What?";
			next;
			mes "[Father Yosuke]";
			mes "Have you any more business with me?! You don't! Go back to the Sanctuary now!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "Hey.";
				mes "Whatever you are,";
				mes "you look like an";
				mes "Acolyte applicant.";
				mes "Right?";
				next;
				mes "[Father Yosuke]";
				mes "Not bad, not bad. You withstood the penance trial pretty well.";
				mes "So what's your name?";
				next;
				mes "[Father Yosuke]";
				mes "" + strcharinfo(0) + ", huh?";
				next;
				mes "[Father Yosuke]";
				mes "Okay. I'll send a message to the Sanctuary that you, " + strcharinfo(0) + ", came to visit me.";
				next;
				mes "[Father Yosuke]";
				mes "Now go back to the Santuary and finish becoming an Acolyte, kid.";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "Hey.";
				mes "You look like an Acolyte Applicant. Am I right?";
				next;
				mes "[Father Yosuke]";
				mes "Not bad at all, you've made it all the way here from Prontera. So what's your name, kid?";
				next;
				mes "[Father Yosuke]";
				mes "" + strcharinfo(0) + ", huh? Why isn't your name on my list?";
				next;
				mes "[Father Yosuke]";
				mes "You probably made a mistake. Go back to the Santuary, and check with the Bishop.";
				close;
			}
		}
		else {
			mes "You...";
			mes "Novice.";
			mes "There something";
			mes "you wanna tell me?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "Hey...";
			next;
			mes "[Father Yosuke]";
			mes "If you like, come sit here with me and meditate the great truths. God's majesty is truly inspiring...";
			close;
		}
		else {
			mes "Do you have anything to say? Because unfortunately for you,";
			mes "I don't any replies.";
			close;
		}
	}
}

