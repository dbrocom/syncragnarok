//===== eAthena Script ======================================= 
//= Soul Linker Job Quest
//===== By: ================================================== 
//= Celestria & Samuray22
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job quest for Soul Linker class.
//= [Vars Used]
//= SOUL_Q = Job Change Phase. (Max 4)
//===== Additional Comments: ================================= 
//= 1.2 Rescripted to Aegis 10.3 standards. [Samuray22]
//= 1.3 Slight updates and fixes to the script. [L0ne_W0lf]
//=	Removes the use of labels, and corrects other errors.
//=	Removed the GM-use only NPC from the sec_in map. 
//=	No longer used a global var, uses an NPC var instead.
//= 1.3a Deleted Empty Color Tag. (bugreport:1572) [Samuray22]
//= 1.4 Replaced effect numerics with constants. [L0ne_W0lf]
//= 1.5 Added Quest Log commands. [Kisuka]
//============================================================ 

morocc_in,174,30,6	script	Kid#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Que tengas la mejor suerte en tus";
		mes "viajes. Al ir dominando";
		mes "más skills de Soul Linker,";
		mes "serás capaz de obtener";
		mes "más poder de los espiritus";
		mes "para proveérselos y ayudar a tus aliados...";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[Kid]";
		if (Sex == 1) {
			mes "¿Qué no eres un guerrero";
			mes "del sol? Conozco";
		}
		else {
			mes "¿Qué no eres un guerrero";
			mes "de la luna? Conozco";
		}
		mes "tus hábitos. Después de todo,";
		mes "los principios de nuestras";
		mes "artes están basadas en el";
		mes "Taekwon Do, ¿No?";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[Kid]";
		mes "¿Mm? No tengo nada que";
		mes "ofrecerte. Pero si sabes";
		mes "de algunos practicantes";
		mes "experimentados en el Taekwon Do,";
		mes "ellos podrían beneficiarse";
		mes "de lo que yo sé.";
		close;
	}
	if (JobLevel < 40) {
		mes "[Kid]";
		mes "Así que estudias";
		mes "Taekwon Do. Eso es bueno,";
		mes "es muy bueno. Sólo sigue";
		mes "mejorando esas habilidades y";
		mes "concéntrate en tu entrenamiento.";
		close;
	}
	else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[Kid]";
			mes "...";
			mes "Oye, tú.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "¿Me llamaste?";
			next;
			mes "[Kid]";
			mes "Si, te llame.";
			mes "No me hagas";
			mes "subir la voz y";
			mes "ven para acá.";
			next;
			if (select("You're awfully rude for a kid!:Ignore him.") == 1) {
				mes "[Kid]";
				mes "¡Tienes suerte de que";
				mes "esté tomando interés";
				mes "en ti! Puedo parecer";
				mes "un niño, pero, ¡Tengo más de";
				mes "trescientos años!";
				emotion e_pif;
				next;
				mes "[Kid]";
				mes "Ahora escucha...";
				mes "Sé que tú eres un";
				mes "discípulo del Taekwon Do.";
				mes "Es un arte respetable, pero";
				mes "tengo una propuesta para";
				mes "ti si deseas escucharla.";
				emotion e_heh;
				next;
				mes "[Kid]";
				mes "Te veo y puedo";
				mes "decir que eres muy";
				mes "devoto a la espiritualidad. Tienes";
				mes "mucho potencial que no lo quiero";
				mes "ver desperdiciado. ¿Por qué no";
				mes "te conviertes en un ''Soul-Linker''?";
				next;
				if (select("Ha! Silly little boy~:Soul Linker?") == 1) {
					mes "[Kid]";
					mes "Tú... ¿Tú n-no";
					mes "me crees? Estoy hablando";
					mes "muy en serio. ¿Puedes";
					mes "olvidar el hecho de que me vea";
					mes "como un niño pequeño sólo por un";
					mes "minuto? *Psh* ...Jóvenes.";
					close;
				}
				mes "[Kid]";
				mes "Los Soul Linkers se comunican";
				mes "con los espíritus de los guerreros caídos";
				mes "que aún desean pelear en el";
				mes "mundo de los vivos. Ahora, estos";
				mes "espíritus guerreros no pueden pelear por";
				mes "si mismos en nuestro mundo.";
				next;
				mes "[Kid]";
				mes "Sin embargo, ya que tienes";
				mes "una inclinación hacia la espiritualidad, estos";
				mes "espíritus son atraídos por ti.";
				mes "Con suficiente entrenamiento, puedes";
				mes "temporalmente confiar y brindar el poder de estos espíritus a tus aliados.";
				next;
				mes "[Kid]";
				mes "Ahora, no puedes brindarte a ti mismo";
				mes "los poderes espirituales de estos guerreros. También,";
				mes "dependiendo de tus habilidades como";
				mes "un Soul Linker, puedes sólo";
				mes "apoyar a ciertas clases con aumento de poder.";
				next;
				mes "[Kid]";
				mes "Tendrás que entrar";
				mes "en un mundo diferente";
				mes "para hacerte Soul Linker,";
				mes "pero sé que será posible";
				mes "para ti. Así que, ¿Qué dices?";
				next;
				if (select("No. At least, not now...:Alright. What do I have to do?") == 1) {
					mes "[Kid]";
					mes "Ah, bueno. Entonces,";
					mes "si alguna vez decides";
					mes "convertirte en Soul Linker,";
					mes "por favor vuelve";
					mes "y habla conmigo en cualquier momento.";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[Kid]";
				mes "¿Así que quieres convertirte";
				mes "en Soul Linker? ¡Genial!";
				mes "Bien, primero necesito";
				mes "que me traigas un par de cosas.";
				mes "No te preocupes, te explicaré";
				mes "por qué las necesitas luego.";
				next;
				mes "[Kid]";
				mes "Ahora tráeme";
				mes "^0000FF1 3 Carat Diamond^000000,";
				mes "^0000FF1 Immortal Heart^000000 y";
				mes "^0000FF1 Witherless Rose^000000.";
				mes "Y trata de no hacerme";
				mes "esperar mucho, ¿Está bien?";
				close;
			}
			mes "[Kid]";
			mes "¿Huh...?";
			mes "Espera, ¿A dónde";
			mes "vas? Yo...";
			mes "¡Yo te estoy hablando!";
			close;
		}
		else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[Kid]";
				mes "Volviste, ¿eh?";
				mes "¿Me trajiste los";
				mes "^0000FF1 3 Carat Diamond^000000,";
				mes "^0000FF1 Immortal Heart^000000 y";
				mes "^0000FF1 Witherless Rose^000000.";
				mes "que te lo pedí?";
				next;
				if (select("There you are.:No, not yet...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; //Crystal_Jewel__
						delitem 929,1; //Immortal_Heart
						delitem 748,1; //Witherless_Rose
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[Kid]";
						mes "Genial, ya veo que has";
						mes "traído todo. Pero";
						mes "antes de comenzar, permíteme";
						mes "presentarme. Mi nombre";
						mes "es Maia, y he estado viva por más de trescientos años.";
						next;
						mes "[Maia]";
						mes "Sin dar muchos";
						mes "detalles, he sido divinamente encomendada a la misión de encontrar";
						mes "y reclutar más Soul Linkers. Esa es parte de la razón por la cual";
						mes "no he, tú sabes, cruzado.";
						next;
						mes "[Maia]";
						mes "De todas maneras, todavía necesito terminar";
						mes "los preparativos con los materiales";
						mes "que acabas de traer, así que,";
						mes "¿Podrías volver en un rato?";
						mes "Entonces, hablaremos una vez más.";
						close;
					}
					mes "[Kid]";
					mes "¿Mm...?";
					mes "Hey. Olvidaste";
					mes "unas cosas. Ahora";
					mes "vuelve y trae";
					mes "todo lo que te pedí,";
					mes "¿Comprendido?";
					emotion e_pif;
					next;
					mes "[Kid]";
					mes "Sé que recién te dije";
					mes "lo que necesitamos, pero voy";
					mes "a recordártelo de nuevo:";
					mes "^0000FF1 3 Carat Diamond^000000,";
					mes "^0000FF1 Immortal Heart^000000 y";
					mes "^0000FF1 Witherless Rose^000000.";
					close;
				}
				mes "[Kid]";
				mes "Mm. Eso está bien.";
				mes "Aunque tengo todo";
				mes "el tiempo del mundo,";
				mes "no me gusta";
				mes "esperar mucho.";
				close;
			}
			set SOUL_Q,0;
			mes "[Kid]";
			mes "¿Te has convertido en un guerrero";
			mes "del Sol, la Luna y";
			mes "las Estrellas? No tenía";
			mes "idea de que tuvieras ese potencial.";
			mes "Supongo que no te puedo culpar...";
			close;
		}
		else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[Maia]";
				mes "Aún tienes algunos";
				mes "Skill Points sin distribuir.";
				mes "Úsalos para aprender algunas";
				mes "skills de Taekwon Do y entonces";
				mes "vuelve cuando estés listo.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Justo ahora, alguien más";
				mes "está completando la ceremonia";
				mes "para convertirse en Soul Linker.";
				mes "¿Podrías esperar por favor hasta";
				mes "que termine? Luego, cuando esté";
				mes "disponible, te atenderé.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Genial, he terminado";
			mes "los preparativos. Ahora";
			mes "procederemos con la";
			mes "ceremonia para convertirte";
			mes "en un Soul Linker.";
			mes "Cierra los ojos...";
			close2;
			warp "job_soul",30,30;
			end;
		}
		else if (SOUL_Q > 2) {
			mes "[Maia]";
			mes "¿Estás listo para";
			mes "explorar las profundidades";
			mes "de tu mente de nuevo?";
			next;
			if (select("No:Yes") == 1) {
				mes "[Maia]";
				mes "Bien entonces, vuelve";
				mes "conmigo cuando";
				mes "creas que estés listo.";
				mes "Hasta entonces, estaré";
				mes "esperando aquí.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Justo ahora, alguien más";
				mes "está completando la ceremonia";
				mes "para convertirse en Soul Linker.";
				mes "¿Podrías esperar por favor hasta";
				mes "que termine? Luego, cuando esté";
				mes "disponible, te atenderé.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Ok, ahora, cierra";
			mes "los ojos y relájate.";
			mes "Volveremos a entrar en";
			mes "las profundidades de tu mente.";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	Maia#link2::SLTester	-1,3,3,{
OnTouch:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			mes "[Maia]";
			mes "¿Hm? ¿Cómo llegaste";
			mes "aquí? No estás calificado";
			mes "para esta ceremonia todavía. Ven, te llevaré de vuelta a Morroc...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[Maia]";
			mes "¿Reconoces este";
			mes "lugar? Justo ahora, estamos";
			mes "dentro de tu mente. Los espíritus";
			mes "de los guerreros que han muerto";
			mes "rondan aquí, esperando";
			mes "que convoques sus poderes.";
			next;
			mes "[Maia]";
			mes "Por ahora, sólo hay";
			mes "unos pocos de ellos aquí, pero si";
			mes "continúas entrenando,";
			mes "serás capaz de llamar";
			mes "más espíritus como Soul Linker.";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[Maia]";
			mes "Sólo podemos permanecer";
			mes "en tu mente por 3 minutos.";
			mes "Te sugiero que hables";
			mes "con los espiritus mientras aún";
			mes "tienes oportunidad.";
			close;
		}
		else if (SOUL_Q == 3) {
			mes "[Maia]";
			mes "Escucha lo";
			mes "ellos intentan decir.";
			mes "Aún hay una razón por la cual";
			mes "ellos no se han ido";
			mes "al otro mundo.";
			close;
		}
		else if (SOUL_Q == 4) {
			mes "[Maia]";
			mes "Creo que ya estás";
			mes "listo para convertirte";
			mes "en Soul Linker. Sin embargo,";
			mes "puedes continuar";
			mes "hablando con los espíritus";
			mes "si es lo que deseas.";
			next;
			if (select("Converse more with the spirits:Become a Soul Linker") == 1) {
				mes "[Maia]";
				mes "Muy bien. Intenta apresurarte";
				mes "porque sólo nos podemos quedar";
				mes "en tu mente por un tiempo";
				mes "limitado. Aunque, podemos";
				mes "volver a entrar a tu mente";
				mes "si hablas conmigo después...";
				close;
			}
			mes "[Maia]";
			mes "Comencemos con la";
			mes "ceremonia. Estos items serán";
			mes "usados para proporcionarte";
			mes "la habilidad de tomar los poderes";
			mes "de los guerreros caídos y brindárselos";
			mes "a tus amigos en la batalla.";
			next;
			mes "[Maia]";
			mes "Esta Witherless Rose se";
			mes "marchitará en vez de ti...";
			specialeffect EF_MAPPILLAR2,AREA,"Maia#link2";
			next;
			mes "[Maia]";
			mes "Esta Witherless Rose se";
			mes "marchitará en vez de ti...";
			mes "Este Immortal Heart cesará";
			mes "de bombear sangre, en lugar del tuyo. ";
			next;
			mes "[Maia]";
			mes "Esta Witherless Rose se";
			mes "marchitará en vez de ti...";
			mes "Este Immortal Heart cesará";
			mes "de bombear sangre, en vez del tuyo. Este Diamond se hará polvo,";
			mes "en lugar de tu cuerpo mortal.";
			next;
			mes "[Maia]";
			mes "Los muertos que deseen";
			mes "seguir peleando...";
			mes "¡Pelearan por ti! Usa tus";
			mes "poderes como Soul Linker";
			mes "sabiamente y por motivos justos.";
			next;
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";		// clears all job variables for the current player
			set SOUL_Q,0;
			mes "[Maia]";
			mes "Te deseo la mejor suerte";
			mes "en tu nueva vida. Rodéate";
			mes "de aliados, y los";
			mes "espíritus podrán protegerte";
			mes "y ayudarte a pelear en tus batallas. Es todo por ahora, amigo.";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
		mes "[Maia]";
		mes "¿Hmm...?";
		mes "El tiempo para que";
		mes "estés aquí aún no";
		mes "ha llegado. Volvamos";
		mes "a Morroc...";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "¡El tiempo para que te";
		mes "aventures por el amplio mundo";
		mes "ha llegado! Definitivamente se necesitarán";
		mes "más Soul Linkers en la constante lucha contra el mal...";
	}
	else {
		mes "[Maia]";
		mes "Es extraño...";
		mes "No se supone que";
		mes "debas estar aquí. Permíteme guiarte";
		mes "de vuelta a Morroc...";
	}
	close2;
	warp "morocc",157,47;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	Maia#link6	716

job_soul,30,35,6	script	Monk Spirit#link4	827,{
	if (SOUL_Q == 2) {
		mes "[Monk Spirit]";
		mes "¿Quien soy yo...?";
		mes "Creo... Creo";
		mes "que sería mejor";
		mes "si hablaras con Maia primero...";
		mes "Quién y qué soy yo requiere";
		mes "una complicada explicación...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Monk Spirit]";
		mes "En vida, mis compañeros hicieron";
		mes "sus mejores esfuerzos para asegurarse";
		mes "de que yo lograra todo lo";
		mes "posible como un Monk. Pero...";
		mes "Pero aún así nunca estaba completamente";
		mes "satisfecho con mis habilidades.";
		next;
		mes "[Monk Spirit]";
		mes "En muerte, me arrepentí,";
		mes "ya que nunca tuve la oportunidad de transmitir";
		mes "mis habilidades a futuros Monks.";
		mes "Brindarle mis poderes a otros ";
		mes "es la único que me queda";
		mes "para lograrlo.";
		next;
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		mes "[Monk Spirit]";
		mes "Te lo imploro...";
		mes "Necesito que me ayudes";
		mes "a liberar el";
		mes "verdadero potencial de los";
		mes "Monks de hoy en día.";
		close;
	}
	mes "[Monk Spirit]";
	mes "...";
	close;
}

job_soul,30,25,7	script	Sage Spirit#link5	754,{
	if (SOUL_Q == 2) {
		mes "[Sage Spirit]";
		mes "Habla con Maia.";
		mes "Me temo que pueda";
		mes "confundirte si Maia";
		mes "no te explica primero";
		mes "tu situación actual...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Sage Spirit]";
		mes "Mi búsqueda del conocimiento";
		mes "me otorgó increíbles poderes:";
		mes "en vida, pude haber destruido";
		mes "lo que quisiera. Pocos Sages";
		mes "podían siquiera alcanzar mi nivel...";
		next;
		mes "[Sage Spirit]";
		mes "Morí, pero nunca pude";
		mes "pasar al otro mundo.";
		mes "Aún quiero usar mis habilidades. Quiero usar mis conocimientos";
		mes "para hacer lo que me plazca,";
		mes "y destruir lo que me plazca.";
		next;
		mes "[Sage Spirit]";
		mes "Es suficiente si logro";
		mes "prestarle mis poderes a un Sage";
		mes "que merezca recibirlos.";
		mes "Pero para hacerlo, requeriré";
		mes "de tu ayuda. Te lo suplico,";
		mes "permíteme convertirme en tu espíritu aliado.";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Sage Spirit]";
		mes "Creo que eres";
		mes "el único que";
		mes "tiene posibilidad de traer";
		mes "descanso a mi pobre alma...";
		close;
	}
	mes "[Sage Spirit]";
	mes "...";
	close;
}

job_soul,25,30,5	script	Alchemist Spirit#link7	744,{
	if (SOUL_Q == 2) {
		mes "[Alchemist Spirit]";
		mes "¡Oh! Realmente quiero";
		mes "hablar contigo, pero lo que";
		mes "tengo que decir no tendrá";
		mes "mucho sentido a menos que";
		mes "hables con Maia primero. Pero sí,";
		mes "en verdad necesito tu ayuda.";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Alchemist Spirit]";
		mes "Sin exagerar, yo era";
		mes "el Alchemist más rápido de mi";
		mes "época. De hecho, podría incluso ser";
		mes "el Alchemist más rápido de todos los tiempos.";
		mes "Pero después me volví arrogante y";
		mes "me suicidé en un accidente.";
		next;
		mes "[Alchemist Spirit]";
		mes "Pero la muerte no se llevó";
		mes "mis habilidades. De hecho, he incluso";
		mes "mejorado mi habilidad desde que he";
		mes "muerto. No puedo ir";
		mes "al otro mundo hasta que haya";
		mes "transmitido mis técnicas...";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Alchemist Spirit]";
		mes "No tengo poderes como un espíritu,";
		mes "pero con tu ayuda, puedo";
		mes "influenciar a los Alchemists de";
		mes "hoy y ayudarlos a mejorar";
		mes "sus habilidades. Te lo ruego,";
		mes "por favor concédeme esta oportunidad...";
		close;
	}
	mes "[Alchemist Spirit]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	111,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (getmapusers("job_soul") == 0) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	stopnpctimer;
}

//============================================================ 
// Old changelog
//============================================================ 
//= A temp Soul Linker Job Changer based on the kRO quest.
//= Quest info from RagnaInfo. Sprites from kRO screenshots
//= 1.0	Optimized and updated [Lupus]
//= 1.1 Fixed NPC names according to iRO [Lupus]
//============================================================ 
