//===== eAthena Script ======================================= 
//= Lighthalzen Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena Revision 3000+
//===== Description: ========================================= 
//= Lighthalzen Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 Cloned from einbroch.txt [Justin84]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Fixed syntax errors for duplicate [KarLaeda]
//= 1.3 Removed use of goto. [L0ne_W0lf]
//============================================================


lhz_airport,126,43,4	script	Airport Staff#1::AirportL	90,{
	mes "[Airport Staff]";
	mes "Bienvenido al Aeropuerto.";
	mes "¿En qué puedo ayudarle?";
	next;
	if (select("Board the Airship:Cancel") == 1) {
		mes "[Airport Staff]";
		mes "La tarifa de abordaje";
		mes "es 1.200 zeny, pero si tiene";
		mes "un Free Ticket for Airship,"; 
		mes "no tendra que pagar la tarifa. ¿Desea";
		mes "abordar la Aeronave?";
		next;
		if (select("Yes:No") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "lhz_airport",148,51;
				close;
			}
			if(zeny >= 1200) {
				set Zeny,zeny-1200;
				warp "lhz_airport",148,51;
				close;
			}
			mes "[Airport Staff]";
			mes "No tiene suficiente zeny";
			close;
		}
	}
	mes "[Airport Staff]";
	mes "Gracias y";
	mes "que tenga un buen día.";
	close;
}

lhz_airport,143,43,4	duplicate(AirportL)	Airport Staff#2	90
lhz_airport,156,43,4	duplicate(AirportL)	Airport Staff#3	90

lhz_airport,126,51,4	script	Airport Staff#4::AirportL2	90,{
	mes "[Airport Staff]";
	mes "Bienvenido.";
	mes "Por favor, siga por";
	mes "aquí para abordar la Aeronave.";
	next;
	if (select("Exit to Main Terminal:Cancel") == 1) {
		mes "[Airport Staff]";
		mes "Sí abandona la";
		mes "Terminal Principal, tendrá que";
		mes "pagar nuevamente la tarifa ";
		mes "para abordar ";
		mes "la Aeronave. ¿Está seguro que";
		mes "desea salir?";
		next;
		if (select("Yes:No") == 1) {
			warp "lhz_airport",142,40;
			close;
		}
	}
	mes "[Airport Staff]";
	mes "De acuerdo, muchas gracias";
	mes "por su preferencia";
	mes "y espero que tenga";
	mes "un viaje placentero.";
	close;
}

lhz_airport,143,51,4	duplicate(AirportL2)	Airport Staff#5	90
lhz_airport,156,51,4	duplicate(AirportL2)	Airport Staff#6	90
