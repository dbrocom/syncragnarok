//===== eAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= eAthena Dev Team
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN 3422+(Requires jA Script System)
//===== Description: ========================================= 
//= The Airship System used in the official servers, however this
//= one is still about 25% custom and is missing some npcs.
//===== Additional Comments: ================================= 
//= 0.1 Added first version, it might be a little buggy [MasterOfMuppets]
//= 0.1a Fixed the Airport Staff#Iz which was [MasterOfMuppets]
//= 	 warping you to the same location as Airport Staff#Ein
//= 0.2 Implemented some more airship NPCs, more are to come.
//= 0.3 Implemented some more NPCs [MasterOfMuppets]
//= 0.4 Implemented two mini games scripted by Dj-Yhn [MasterOfMuppets]
//= 0.5 Removed Duplicates [Silent]
//= 0.5a Fixed a typo [Evera] 
//= 0.5b Fixed appledice bugs [Lupus]
//= 0.6 Added Rachel to the international route [Playtester]
//= 0.7 Implemented Few Missings NPCs like (Pilot, Aanos, etc...).
//=	Corrected Dialogs from some NPCs.
//=	Added sistem to work with the "How Does the Airship Work" Quest [Samuray22]
//= 0.8 Moved Gambling NPC Kaci since it's part of a quest. [SinSloth]
//= 0.8b Fixed a Little Bug with the "How Does the Airship Work" Quest. [Samuray22]
//= 0.9 Fixed a incorrect variable changed form "mob_inv" to "mobinv". [Samuray22]
//= 0.9b Fixed a typo and bad variable use in the Typing Challenge [ultramage]
//= 1.0 Fixed bad NPC header data to comply with rev. 11603. [L0ne_W0lf]
//= 1.1 Touch-ups, and changed references of "Yuno" to "Juno",  [L0ne_W0lf]
//= 1.1a Small fix to comparison check. [Paradox924X]
//= 1.1b Small fix to zeny check. [Kisuka]
//= 1.2 Updated some NPCs based on official files. [L0ne_W0lf]
//============================================================ 

//============================================================ 
//= The Airship System (Juno -> Hugel -> Einbroch -> Lighthalzen -> repeat)
//============================================================ 

airplane,243,74,4	script	#AirshipWarp-1	45,2,2,{
OnTouch:
	if($@airplanelocation == 1)warp "einbroch",90,275;
	if($@airplanelocation == 2)warp "yuno",85,265;
	if($@airplanelocation == 3)warp "lighthalzen",302,75;
	if($@airplanelocation == 4)warp "hugel",182,150;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane,243,29,4	script	#AirshipWarp-2	45,2,2,{
OnTouch:
	if($@airplanelocation == 1)warp "einbroch",90,275;
	if($@airplanelocation == 2)warp "yuno",85,265;
	if($@airplanelocation == 3)warp "lighthalzen",302,75;
	if($@airplanelocation == 4)warp "hugel",182,150;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane,1,1,0	script	EinYuno_Airship	-1,{
OnInit:
while(1)
{
	initnpctimer;
	setnpctimer 0;
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","El Dirigible está despegando. Nuestro próximo destino es Hugel.",bc_map,0xBA55D3;
	end;
OnTimer15000:
	mapannounce "airplane","Nos estamos dirigiendo hacia Hugel.",bc_map,0xBA55D3;
	end;
OnTimer30000:
	mapannounce "airplane","Llegaremos a Hugel en breves momentos.",bc_map,0xBA55D3;
	end;
OnTimer45000:
	set $@airplanelocation,4;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";	
	mapannounce "airplane","Bienvenidos a Hugel. Tengan un buen viaje.",bc_map,0xBA55D3;
	end;
OnTimer55000:
	mapannounce "airplane","En estos momentos nos encontramos en Hugel. Último aviso para el despegue del Dirigible.",bc_map,0xBA55D3;
	end;
OnTimer65000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","El Dirigible está despegando. Nuestro próximo destino es Einbroch.",bc_map,0x00FF00;
	end;
OnTimer80000:
	mapannounce "airplane","Nos estamos dirigiendo hacia Einbroch.",bc_map,0x00FF00;
	end;
OnTimer95000:
	mapannounce "airplane","Llegaremos a Einbroch en breves momentos.",bc_map,0x00FF00;
	end;
OnTimer110000:
	set $@airplanelocation,1;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bienvenidos a Einbroch. Tengan un buen viaje.",bc_map,0x00FF00;
	end;
OnTimer120000:
	mapannounce "airplane","En estos momentos nos encontramos en Einbroch. Último aviso para el despegue del Dirigible.",bc_map,0x00FF00;
	end;
OnTimer130000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","El Dirigible está despegando. Nuestro próximo destino es Lighthalzen.",bc_map,0xFF8600;
	end;
OnTimer145000:
	mapannounce "airplane","Nos estamos dirigiendo hacia Lighthalzen.",bc_map,0xFF8600;
	end;
OnTimer160000:
	mapannounce "airplane","Llegaremos a Lighthalzen en breves momentos.",bc_map,0xFF8600;
	end;
OnTimer175000:
	set $@airplanelocation,3;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bienvenidos a Lighthalzen. Tengan un buen viaje.",bc_map,0xFF8600;
	end;
OnTimer185000:
	mapannounce "airplane","En estos momentos nos encontramos en Lighthalzen. Último aviso para el despegue del Dirigible.",bc_map,0xFF8600;
	end;
OnTimer195000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","El Dirigible está despegando. Nuestro próximo destino es Yuno.",bc_map,0x70DBDB;
	end;
OnTimer210000:
	mapannounce "airplane","Nos estamos dirigiendo hacia Yuno.",bc_map,0x70DBDB;
	end;
OnTimer225000:
	mapannounce "airplane","Llegaremos a Yuno en breves momentos.",bc_map,0x70DBDB;
	end;
OnTimer240000:
	set $@airplanelocation,2;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bienvenidos a Yuno. Tengan un buen viaje.",bc_map,0x70DBDB;
	end;
OnTimer250000:
	mapannounce "airplane","En estos momentos nos encontramos en Yuno. Último aviso para el despegue del Dirigible.",bc_map,0x70DBDB;
	end;
OnTimer260000:
	stopnpctimer;
}
}

//C4644E

//============================================================ 
//= Some normal NPCS (airplane)
//============================================================ 

airplane,240,64,5	script	Exit#01::Exit2	857,{
	end;
}

airplane,247,64,5	duplicate(Exit2)	Exit#02	857

airplane,240,39,1	duplicate(Exit2)	Exit#03	857

airplane,247,39,1	duplicate(Exit2)	Exit#04	857

airplane,100,69,2	script	Airship Crew#01::Airship Crew	852,{
	mes "[Personal del Dirigible]";
	mes "Cuando hayamos aterrizado en tu destino, dirígete a la salida por estas escaleras.";
	mes "Gracias por confiar en nuestro servicio.";
	close;
}

airplane,250,58,2	script	Airship Staff#airplane	67,{
	if (hg_ma1 == 3) {
		mes "[Personal del Dirigible]";
		mes "Bienvenid"+(Sex?"o":"a")+" al Dirigible.";
		mes "¿En qué puedo ayudarte?";
		next;
		switch(select("¿Hay algún pasajero a bordo llamado Thierry?")) {
			case 1:
				break;
		}
		mes "[Personal del Dirigible]";
		mes "Lo siento, pero creo que no tenemos a ningún pasajero con ese nombre.";
		close;
	}
	mes "[Personal del Dirigible]";
	mes "Bienvenid"+(Sex?"o":"a")+" al Dirigible.";
	mes "¿En qué puedo ayudarte?";
	next;
	switch(select("Usando el Dirigible:El Puente:Servicios:Nada, gracias")) {
	case 1:
		mes "[Personal del Dirigible]";
		mes "Cuando veas un anuncio informando de que hemos llegado a tu destino, utiliza una de las salidas que se encuentran en la cubierta del Dirigible.";
		next;
		mes "[Personal del Dirigible]";
		mes "Si se te ha olvidado bajarte, o no te ha dado tiempo, no te preocupes. El Dirigible está continuamente sobrevolando la ruta que realiza.";
		break;
	case 2:
		mes "[Personal del Dirigible]";
		mes "El Puente se encuentra en la popa de la cubierta.";
		mes "Allí podrás conocer al capitán y al piloto del Dirigible.";
		break;
	case 3:
		mes "[Personal del Dirigible]";
		mes "El Dirigible disponde de varios Minijuegos para el entretenimiento de nuestros pasajeros.";
		mes "Te aconsejamos probar suerte en ellos~";
		break;
	case 4:
		mes "[Personal del Dirigible]";
		mes "Vale, Esperamos que disfrutes del vuelo.";
		mes "Gracias y que tengas un buen día.";
		break;
	}
	close;
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Ah, hola, aventurer"+(Sex?"o":"a")+".";
	mes "Estoy realizando una peregrinación, ofreciendo plegarias por el bien del continente de Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Eh, este Dirigible va hacia Lutie, ¿no?";
	mes "Llevo esperando mucho rato, pero aun no han anunciado esa parada.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Oh wooow~";
	mes "¡El cielo parece tan bonito y diferente desde aquí!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	if (hg_ma1 == 3) {
		mes "[Piloto]";
		mes "Me encantaría poder beber una buena cerveza fresca.";
		mes "¡Es la meta de mi vida! ¡Beber me da energía!";
		mes "¡No soy nadie sin la bebida!";
		next;
		mes "[Piloto]";
		mes "Lo malo es que conducir ebrio no es bueno.";
		mes "¡El no poder beber aquí me da aun más ganas!";
		emotion e_sob;
		next;
		switch(select("¿Hay algún pasajero a bordo llamado Thierry?")) {
		case 1:
			break;
		}
		mes "[Piloto]";
		mes "Me encanta este uniforme, pero es demasiado gordo para llevarlo en el Dirigible.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Menos mal que nadie puede entrar en este compartimento. El capitán es en realidad un reno.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "¿¡Qué!? ¡Quien eres tú!";
		next;
		mes "- ...Parece que el piloto no te escuchó entrar. -";
		close;
	}
	set .@pilot,rand(1,4);
	if (.@pilot == 1) {
		mes "[Piloto]";
		mes "Hace taaaanto que no tomo una rica cerveza fria. ¡Este trabajo me obliga a estar tan despierto como me sea posible!";
		next;
		mes "[Piloto]";
		mes "Ya no me acuerdo de la última vez que tuve unas vacaciones de verdad, o un día libre.";
		mes "Sí, relajado en casa con un buen pedo, unas patatas y viendo algo en la tele.";
		emotion e_sob;
	}
	else if (.@pilot == 2) {
		mes "[Piloto]";
		mes "Tí"+(Sex?"o":"a")+", el tiempo está perfecto hoy.";
		mes "Qué el cielo esté despejado hace el vuelo más seguro y sin complicaciones.";
	}
	else if (.@pilot == 3) {
		mes "[Piloto]";
		mes "Sabes, nuestro capitán es un hombre honrado.";
		mes "Él y su hermano son muy conocidos en la industria del transporte.";
		mes "¿Como habrán conseguido unos renos ser tan buenos capitanes?";
		next;
		mes "[Piloto]";
		mes "No se lo digas a nadie, pero creo que Santa tuvo algo que ver, ¡ése tío tiene que ser un genio!";
	}
	else {
		mes "[Piloto]";
		mes "Sabes que este Dirigible es nuevo, ¿no?";
		mes "Se hizo en muy poco tiempo.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Lo gracioso es que cuando aun era un proyecto, ya nos estaban enseñando para trabajar aquí.";
		mes "¡¿A que es una locura?!";
	}
	close;
}

//============================================================ 
//= The Airship System (Izlude -> Juno -> Rachel -> Repeat)
//============================================================ 

airplane_01,243,74,4	script	#AirshipWarp-3	45,2,2,{
OnTouch:
	if($@airplanelocation2 == 1)warp "izlude",202,56;
	if($@airplanelocation2 == 2)warp "yuno",20,265;
	if($@airplanelocation2 == 3)warp "ra_fild12",292,204;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane_01,243,29,4	script	#AirshipWarp-4	45,2,2,{
OnTouch:
	if($@airplanelocation2 == 1)warp "izlude",202,56;
	if($@airplanelocation2 == 2)warp "yuno",20,265;
	if($@airplanelocation2 == 3)warp "ra_fild12",292,204;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane_01,1,1,0	script	YunoIzl_Airship	-1,{
OnInit:
while(1)
{
	disablenpc "Airship#airplane02";
	initnpctimer;
	setnpctimer 0;
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","El Dirigible está despegando. Nuestro próximo destino es Izlude.",bc_map,0x00FF00;
	end;
OnTimer15000:
	set $@mobinv, $@mobinv+1;
	set $@mobrand, rand(1,3);
	if($@mobinv >= 9 && $@mobrand == 3) {
		stopnpctimer;
		enablenpc "Airship#airplane02";
		donpcevent "Airship#airplane02::OnInvasion";
	}
	mapannounce "airplane_01","Nos estamos dirigiendo hacia Izlude.",bc_map,0x00FF00;
	end;
OnTimer30000:
	mapannounce "airplane_01","Llegaremos a Izlude en breves momentos.",bc_map,0x00FF00;
	end;
OnTimer45000:
	set $@airplanelocation2,1;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";	
	mapannounce "airplane_01","Bienvenidos a Izlude. Tengan un buen viaje.",bc_map,0x00FF00;
	end;
OnTimer55000:
	mapannounce "airplane_01","En estos momentos nos encontramos en Izlude. Último aviso para el despegue del Dirigible.",bc_map,0x00FF00;
	end;
OnTimer65000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","El Dirigible está despegando. Nuestro próximo destino es Yuno.",bc_map,0x70DBDB;
	end;
OnTimer80000:
	mapannounce "airplane_01","Nos estamos dirigiendo hacia Yuno.",bc_map,0x70DBDB;
	end;
OnTimer95000:
	mapannounce "airplane_01","Llegaremos a Yuno en breves momentos.",bc_map,0x70DBDB;
	end;
OnTimer110000:
	set $@airplanelocation2,2;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bienvenidos a Yuno. Tengan un buen viaje.",bc_map,0x70DBDB;
	end;
OnTimer120000:
	mapannounce "airplane_01","En estos momentos nos encontramos en Yuno. Último aviso para el despegue del Dirigible.",bc_map,0x70DBDB;
	end;
OnTimer130000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","El Dirigible está despegando. Nuestro próximo destino es Rachel.",bc_map,0xFF8600;
	end;
OnTimer145000:
	mapannounce "airplane_01","Nos estamos dirigiendo hacia Rachel.",bc_map,0xFF8600;
	end;
OnTimer160000:
	mapannounce "airplane_01","Llegaremos a Rachel en breves momentos.",bc_map,0xFF8600;
	end;
OnTimer175000:
	set $@airplanelocation2,3;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bienvenidos a Rachel. Tengan un buen viaje.",bc_map,0xFF8600;
	end;
OnTimer185000:
	mapannounce "airplane_01","En estos momentos nos encontramos en Rachel. Último aviso para el despegue del Dirigible.",bc_map,0xFF8600;
	end;
OnTimer195000:
 	stopnpctimer;
}
OnReturn:
	killmonsterall "airplane_01";
	set $@mobinv, 0;
	mapannounce "airplane_01","La amenaza de monstruos ha sido eliminada. El Dirigible vuelve a su normalidad.",bc_map,0x00FF00;
	startnpctimer;
	end;
}

//============================================================ 
//= Some normal NPCS (airplane_01)
//============================================================ 

airplane_01,240,64,5	duplicate(Exit2)	Exit#05	857

airplane_01,247,64,5	duplicate(Exit2)	Exit#06	857

airplane_01,240,39,1	duplicate(Exit2)	Exit#07	857

airplane_01,247,39,1	duplicate(Exit2)	Exit#08	857

airplane_01,100,69,2	duplicate(Airship Crew)	Airship Crew#02	852

airplane_01,250,58,2	script	Airship Staff#airplane01	67,{
	mes "[Personal del Dirigible]";
		mes "Bienvenid"+(Sex?"o":"a")+" al Dirigible.";
		mes "¿En qué puedo ayudarte?";
	next;
	switch(select("Usando el Dirigible:El Puente:Servicios:Nada, gracias")) {
	case 1:
		mes "[Personal del Dirigible]";
		mes "Cuando veas un anuncio informando de que hemos llegado a tu destino, utiliza una de las salidas que se encuentran en la cubierta del Dirigible.";
		next;
		mes "[Personal del Dirigible]";
		mes "Si se te ha olvidado bajarte, o no te ha dado tiempo, no te preocupes. El Dirigible está continuamente sobrevolando la ruta que realiza.";
		break;
	case 2:
		mes "[Personal del Dirigible]";
		mes "El Puente se encuentra en la popa de la cubierta.";
		mes "Allí podrás conocer al capitán y al piloto del Dirigible.";
		break;
	case 3:
		mes "[Personal del Dirigible]";
		mes "El Dirigible disponde de varios Minijuegos para el entretenimiento de nuestros pasajeros.";
		mes "Te aconsejamos probar suerte en ellos~";
		break;
	case 4:
		mes "[Personal del Dirigible]";
		mes "Vale, Esperamos que disfrutes del vuelo.";
		mes "Gracias y que tengas un buen día.";
		break;
	}
	close;
}


airplane_01,50,66,5	script	Apple Merchant#airplane	86,{
	if (checkweight(1201,1) == 0) {
		mes "- ¡¡ Un momento !! -";
		mes "- Llevas mucho peso encima. -";
		mes "- Habla conmigo cuando liberes un poco de carga. -";
		close;
	}
	mes "[Meltz]";
	mes "Bienvenid"+(Sex?"o":"a")+" a la tienda de Meltz.";
	mes "Aquí puedes comprar Manzanas o exprimirlas para hacer Zumo.";
	next;
	switch(select("Comprar Manzanas:Hacer Zumo de Manzana:No quiero nada")) {
	case 1:
		mes "[Meltz]";
		mes "Dime la cantidad de Manzanas que quieres comprar.";
		mes "Cada una cuesta 15 zeny y puedes comprar como mucho 500 de cada vez.";
		mes "Escribe '0' para cancelar tu pedido.";
		next;
		while(1) {
			input .@input,0,501;
			set .@pay,.@input*15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Gracias por parar por mi tienda.";
				mes "Vuelve cuando quieras tomar Manzanas.";
				mes "¡Hasta luego!";
				close;
			}
			else if (.@input < 0 && .@input > 500) {
				mes "[Meltz]";
				mes "Has puesto un número mayor de 500.";
				mes "Escríbelo otra vez.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "^FF0000" + .@input + "^000000 Manzanas te costarán ^FF0000" + .@pay + "^000000 zeny.";
				mes "¿Estás seguro?";
				next;
				if (select("Sí:No") == 1)
					break;
				else {
					mes "[Meltz]";
					mes "Gracias por parar por mi tienda.";
					mes "Vuelve cuando quieras tomar más Manzanas.";
					mes "¡Hasta luego!";
					close;
				}
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Lo siento, pero no llevas suficiente dinero encima.";
			mes "Revisa tus fondos o compra menos Manzanas.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Mmm, no tienes suficiente sitio para cargar con tantas Manzanas.";
			mes "Libera algo de espacio en tu inventario.";
			close;
		}
		else {
			set zeny,zeny-.@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Gracias por parar por mi tienda.";
			mes "Vuelve cuando quieras tomar más Manzanas.";
			mes "¡Hasta luego!";
			close;
		}
		break;
	case 2:
		mes "[Meltz]";
		mes "Vale, necesito ^FF00003 Manzanas y una 1 Empty Bottle^000000 para hacerte un Zumo de Manzana.";
		mes "¿Quieres continuar?";
		next;
		switch(select("Sí:No")) {
		case 1:
			if ((countitem(512) < 3) || (countitem(713) < 1)) {
				mes "[Meltz]";
				mes "Lo siento, pero no tienes los materiales que te pedí. Recuerda que son 3 Manzanas y 1 Empty Bottle.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Gracias. Espera un momento.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Grind* *Grind*";
				mes "*¡Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Meltz]";
				mes "Aquí tienes~";
				mes "Vuelve cuando quieras.";
				close;
			}
			break;
		case 2:
			mes "[Meltz]";
			mes "Gracias por parar por mi tienda.";
			mes "Vuelve cuando quieras tomar más Manzanas.";
			mes "¡Hasta luego!";
			close;
		}
		break;
	case 3:
		mes "[Meltz]";
		mes "Gracias por parar por mi tienda.";
		mes "Vuelve cuando quieras tomar más Manzanas.";
		mes "¡Hasta luego!";
		break;
	}
	close;
}

airplane_01,32,61,4	script	Nils#ein	49,{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables
loopback:
	mes "[Nils]";
	mes "Bienvenid"+(Sex?"o":"a")+" al ^FF0000Desafío de Escritura RO^000000.";
	mes "¿Quiere echar una partida?";
	next;
	menu "^FF0000Jugar^000000",s_Play,"Información",s_Info,"Record",s_Record,"No",-;

	mes "[Nils]";
	mes "Ven a participar cuando quieras.";
	mes "Estaré por aquí.";
	close;

s_Play:
	mes "[Nils]";
	mes "Bien, ¡tenemos un nuevo participante!";
	mes "Escribe el siguiente texto tan rápido como puedas sin cometer ningún fallo!";
	mes "¡Empezemos~!";
	next;
	//below arrays are for simplified entering of new lines ;P

	//first lines array
	setarray $@textstringsa$[0],
	"^00FFFFthkelfkskeldmsiejdlsle^0000FFhfndkelsheidl",
	"^993366hfjdkeldjsieldjs^663366hfjdjeiskdlefvbd",
	"^00FFFFCoboman no chikara-yumei na",
	"^993366belief love luck grimace sweat rush",
	"^00FFFFcallipygian salacious lascivious",
	"^663366uNflAPPaBLe LoVaBLe SeCreTs AnD",
	"^00CCFFburrdingdingdilidingdingphoohudaamb",
	"^FF33CCbarapaphurarlandreamduranbatuhi^990066wooi",
	"^FF33CCLiGhTsPeEd RiGhT SPEed leFT TURn",
	"^00CCFFI'm the King of all Weirdos! Now",
	"^00CCFFBy the power of^000000",
	"^00CCFF...silence. quiet benevolence...";

	//second lines array
	setarray $@textstringsb$[0],"skemd",
	"",
	"chikara-dalookii na chikara da ze!",
	"folktale rodimus optimus bumblebee",
	"licentious prurient concupiscent",
	"BoWLiNg aGaINST tHe KarMA of YoUtH",
	"andoora^0000FFbambarambambamburanbamding",
	"kabamturubamdingding",
	"RiGhT BuRn OrIGInAL GaNgSteR SmACk",
	"you know of my true power. Obey~!",
	"p-po-poi-po-poi-poin-poing",
	"soul mate... wonder. enigma...";
	//for the few 3-liners... =X

	setarray $@textstringsc$[0],"",
	"",
	"COBO ON!",
	"",
	"",
	"",
	"",
	"",
	"",
	"",
	"GOD-POING. I NEVER LOSE!",
	"cloud.";

	//comparisation strings
	setarray $@compstring$[0],
	"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
	"hfjdkeldjsieldjshfjdjeiskdlefvbd",
	"Coboman no chikara-yumei na chikara-dalookii na chikara da ze! COBO ON!",
	"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
	"callipygian salacious lascivious licentious prurient concupiscent",
	"uNflAPPaBLe LoVaBLe SeCreTs AnD BoWLiNg aGaINST tHe KarMA of YoUtH",
	"burrdindingdilidingdingphoohudaambandoorabambarambambamburanbamding",
	"barapaphurarlandreamduranbatuhiwooikabamturubamdingding",
	"LiGhTsPeEd RiGhT SPEed leFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
	"I'm the King of all Weirdos! Now you know of my true power. Obey~!",
	"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
	"...silence. quiet benevolence... soul mate... wonder. enigma... cloud";

	//lengths
	setarray $@lengths[0],20,32,73,73,65,66,67,55,67,66,67,69;

	//	initnpctimer;
	// pick 2 distinct phrases and initialize related variables
	set @index,rand(1,getarraysize($@textstringsa$))-1;
	set @string1a$,$@textstringsa$[@index];
	set @string1b$,$@textstringsb$[@index];
	set @string1c$,$@textstringsc$[@index];
	set @typingstring1$,$@compstring$[@index];

	set @index2,rand(1,getarraysize($@textstringsa$))-1;
	while (@index2 == @index) {
		set @index2,rand(1,getarraysize($@textstringsa$))-1;
	}
	set @string2a$,$@textstringsa$[@index2];
	set @string2b$,$@textstringsb$[@index2];
	set @string2c$,$@textstringsc$[@index2];
	set @typingstring2$,$@compstring$[@index2];

	set @typingkeys,$@lengths[@index]+$@lengths[@index2];
	set @start,gettime(3)*60*60+gettime(2)*60+gettime(1);

	mes "[Nils]";
	mes @string1a$;
	if(@string1b$ != "")mes @string1b$;
	if(@string1c$ != "")mes @string1c$;
	input @comparisonvar$[1];
	next;
	mes "^000000[Nils]";
	mes @string2a$;
	if(@string2b$ != "")mes @string2b$;
	if(@string2c$ != "")mes @string2c$;
	input @comparisonvar$[2];
	next;
	if(@comparisonvar$[1] != @typingstring1$ || @comparisonvar$[2] != @typingstring2$) goto s_Fail;
	set @time,(gettime(3)*60*60+gettime(2)*60+gettime(1))-@start; //seconds taken
	set @mypoints,(@typingkeys * 100) / @time;
	mes "[Nils]";
	mes "Tu velocidad de escritura ha sido de^FF0000" + @time + " segundos^000000 y has conseguido ^0000FF" + @mypoints + "^000000 puntos.";
	next;
	if(@mypoints <= $TypingRecord)goto loopback;
	mes "[Nils]";
	mes "¡Felicidades! Has hecho un nuevo record.";
	mes "A partir de ahora ésta es la marca a batir.";
	set $TypingRecord,@mypoints;
	set $TypingRecord$,strcharinfo(0);
	close;

s_Fail:
	mes "^000000[Nils]";
	mes "Lo siento pero no has escrito todos los caracteres correctamente.";
	mes "No te desanimes, recuerda que la práctica hace al campeón.";
	close;

s_Info:
	mes "[Nils]";
	mes "El Desafío de Escritura RO es un juego en el que tienes que escribir un texto tan rápido como te sea posible.";
	mes "Si haces un record quedará registrado hasta que alguien consiga superarlo.";
	next;
	mes "[Nils]";
	mes "Simplemente hay que escribir todo el texto indicado en una sola linea.";
	mes "Pero cuando termines no pulses la tecla Intro, haz click en el 'OK'.";
	close;

s_Record:
	mes "[Nils]";
	mes "El record lo consiguió ^0000FF" + $TypingRecord$ + "^000000 con ^0000FF" + $TypingRecord + "^000000 puntos.";
	mes "Intenta superarlo si eres capaz.";
	close;

}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	set .@pilot, rand(1,4);
	if (.@pilot == 1) {
		mes "[Piloto]";
		mes "Longitud, 131 grados este.";
		mes "Latitud, 37 grados norte.";
		mes "Seguimos ése rumbo, mi capitán.";
	} else if(.@pilot == 2) {
		mes "[Piloto]";
		mes "Parece que el día está nublado. Siempre se hace difícil navegar cuando el cielo no está despejado.";
		mes "Seguramente tendremos que guiarnos por el radar.";
	} else if(.@pilot == 3) {
		mes "[Piloto]";
		mes "El capitán es un buen hombre. No conozco a ninguna persona más apta para dirigir esta nave que él.";
		mes "Aunque es bastante estricto";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000¡Eh...!^000000";
		mes "^ff0000¡Menos hablar y más pilotar!^000000";
		next;
		mes "[Piloto]";
		mes "¡Enterado, señor!";
		mes "(¿Ves lo que te dije?)";
	} else {
		mes "[Piloto]";
		mes "Me encanta este uniforme, pero es demasiado gordo para llevarlo en el Dirigible.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Menos mal que nadie puede entrar en este compartimento. El capitán es en realidad un reno.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "¡Oh...! ¡Hola, tú!";
		mes "¡¿Di-di-disfrutando del vuelo?!";
	}
	close;
}



airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "¡Qué raro!";
	mes "Fui a visitar al capitán del Dirigible y me encontré con un reno.";
	mes "¡Un momento! ¿No será que...?";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "Como me suponía, las comidas a bordo son como mucho de tres tenedores.";
	mes "*Harrrumph* Me debería haber pagado un chef personal para disfrutar de un solomillo de verdad.";
	close;
}

airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Hola, me llamo Clarice~";
	mes "¿Te apuestas unas Manzanas jugando a los Dados conmigo?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

airplane_01,71,31,2	script	Swordsman Shimizu#01	106,{
	mes "[Swordsman Shimizu]";
	mes "Al fin, después de cinco años esperando...";
	mes "¡Me vengaré!";
	next;
	mes "[Swordsman Shimizu]";
	mes "Solamente...";
	mes "Tengo que asegurarme de no rendirme.";
	mes "Pero pronto, muy pronto, ¡la venganza será mía!";
	close;
}

//============================================================ 
//= The Izlude Airship Staff
//============================================================

izlude,201,54,3	script	Airship Staff#izl	91,{
	mes "[Personal del Dirigible]";
	mes "Bienvenid"+(Sex?"o":"a")+" al Dirigible Internacional.";
	mes "¿En qué te puedo ayudar?";
	next;
	if (select("Quiero subir al Dirigible:Nada, gracias") == 1) {
		mes "[Personal del Dirigible]";
		mes "Embarcar en el Dirigible cuesta 1,200 zeny, aunque si tienes un Free Ticket for Flyship te saldrá gratis.";
		mes "¿Quieres subir?";
		next;
		if (select("Sí:No") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airplane_01",224,64;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airplane_01",224,64;
				close;
			}
			mes "[Personal del Dirigible]";
			mes "No tienes suficiente dinero.";
			close;
		}
	}
	mes "[Personal del Dirigible]";
	mes "Gracias y ten un buen día.";
	close;
}

//============================================================ 
//= The Hugel Airship Staff
//============================================================

hugel,182,150,3	script	Airship Staff#hu	91,{
	mes "[Personal del Dirigible]";
	mes "Bienvenid"+(Sex?"o":"a")+" al Dirigible Internacional.";
	mes "¿En qué te puedo ayudar?";
	next;
	if (select("Quiero subir al Dirigible:Nada") == 1) {
		mes "[Personal del Dirigible]";
		mes "Embarcar en el Dirigible cuesta 1,200 zeny, aunque si tienes un Free Ticket for Flyship te saldrá gratis.";
		mes "¿Quieres subir?";
		next;
		if (select("Sí:No") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airplane",224,64;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airplane",224,64;
				close;
			}
			mes "[Personal del Dirigible]";
			mes "No tienes suficiente dinero.";
			close;
		}
	}
	mes "[Personal del Dirigible]";
	mes "Gracias y ten un buen día.";
	close;
}

//============================================================ 
//= The Rachel Airship Staff
//============================================================

ra_fild12,295,208,3	script	Airship Staff#ra	45,1,1,{
OnTouch:
	mes "Para subir al Dirigible son 1,200 zeny o un Free Ticket for Flyship.";
	mes "¿Quieres subir";
	next;
	if (select("Sí:No") == 1) {
		if (countitem(7311) > 0) {
			delitem 7311,1;
			warp "airplane_01",224,64;
			close;
		}
		if (Zeny >= 1200) {
			set Zeny, Zeny - 1200;
			warp "airplane_01",224,64;
			close;
		}
		mes "No tienes suficiente dinero.";
		close;
	}
	mes "Gracias y ten un buen día.";
	close;
}

//============================================================ 
//= Juno Airport NPCs
//============================================================

y_airport,144,63,4	script	Airport Staff#Ein	91,{
	mes "[Personal del Dirigible]";
	mes "¡Buenos días!";
	mes "¿Quieres ir a ^FF0000Einbroch^000000,^FF0000Hugel^000000 o ^FF0000Lighthalzen^000000?";
	next;
	if (select("Sí:No") == 1) {
		mes "[Personal del Dirigible]";
		mes "Gracias y ten un buen día";
		close2;
		warp "yuno",57,240;
		end;
	}
	mes "[Personal del Dirigible]";
	mes "Gracias y ten un buen día";
	close;
}

y_airport,141,63,4	script	Airport Staff#Izl	91,{
	mes "[Personal del Dirigible]";
	mes "¡Buenos días!";
	mes "¿Quieres ir a ^FF0000Izlude^000000 o ^FF0000Rachel^000000?";
	next;
	if (select("Sí:No") == 1) {
		mes "[Personal del Dirigible]";
	mes "Gracias y ten un buen día";
		close2;
		warp "yuno",50,240;
		end;
	}
	mes "[Personal del Dirigible]";
	mes "Gracias y ten un buen día";
	close;
}

yuno,14,262,5	script	Airship Staff#yuno01	91,{
	mes "[Personal del Dirigible]";
	mes "Bienvenid"+(Sex?"o":"a")+" al Aeropuerto de Yuno.";
	mes "Entra por la puerta de aquí al lado para viajar hacia Izlude en el Reino de Rune-Midgard, y hacia Rachel en la República de Arunafeltz.";
	next;
	mes "[Personal del Dirigible]";
	mes "Si por el contrario tu destino es Yuno, por favor baja las escaleras y habla con Arrival Staff para que te saque de la zona de embarque.";
	mes "Gracias y disfruta de tu viaje.";
	close;
}

yuno,88,263,3	script	Airship Staff#yuno02	91,{
	mes "[Personal del Dirigible]";
	mes "Bienvenid"+(Sex?"o":"a")+" al Aeropuerto de Yuno.";
	mes "Entra por la puerta de aquí al lado para viajar en el Dirigible hacia Einbroch, Lighthalzen y Hugel en la República Schwaltzvalt.";
	next;
	mes "[Personal del Dirigible]";
	mes "Si por el contrario tu destino es Yuno, por favor baja las escaleras y habla con Arrival Staff para que te saque de la zona de embarque.";
	mes "Gracias y disfruta de tu viaje.";
	close;
}

//============================================================ 
//= Apple Gambling Function
//============================================================

function	script	applegamble	{

	switch(select("Jugar a los Dados:Enséñame las reglas:No")){
	case 3:
		mes "["+getarg(0)+"]";
		mes "Cuando quieras jugar ven a verme, ¿vale?";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "Las reglas son bastante simples.";
		mes "Primero tienes que hacer la apuesta, que puede ser de hasta 50 Manzanas.";
		mes "Solo aceptamos Manzanas por cuestiones legales.";
		next;
		mes "["+getarg(0)+"]";
		mes "Pero escucha, si llevas dinero de sobra en tu bolsillo, acércate hasta el Apple Merchant de aquí al lado y compra tantas como necesites.";
		next;
		mes "["+getarg(0)+"]";
		mes "Luego de hacer las apuestas, Tanto tú como yo tiramos dos dados de seis caras.";
		mes "El objetivo es conseguir el número más alto haciendo la suma de ambos.";
		next;
		mes "["+getarg(0)+"]";
		mes "Una vez hecho esto tenemos la posibilidad de tirar un tercer dado. Este dado es opcional, te puedes plantar con lo que has sacado en los dos primeros.";
		mes "Pero te puedes arriesgar y tirarlo a ver si sacas un número mayor, siempre y cuando la suma de los tres no sea mayor a 12.";
		mes "Porque si consigues más de 12, pierdes. El que obtenga el mayor número gana el juego.";
		next;
		mes "["+getarg(0)+"]";
		mes "El ganador recibirá el doble de Manzanas que apostó. Si empatamos te devolveré la apuesta que hiciste.";
		mes "Es justo, ¿no?";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "Ah, ¿así que quieres jugar conmigo? ¡Bien!";
	mes "¿Cuantas Manzanas quieres apostar? Si quieres salir escribe '0'.";
	next;
L_Input:
	input @amount;
	if(@amount == 0) {
		mes "["+getarg(0)+"]";
		mes "¿Te quieres echar atrás? Bueno, lo entiendo.";
		mes "Espero que podamos jugar más adelante.";
		close;
	}
	if(@amount > 50) set @amount,50;
	mes "["+getarg(0)+"]";
	mes "La apuesta son ^FF0000"+@amount+"^000000 Manzanas.";
	mes "¿De acuerdo?";
	next;
	if(select("Sí:No")==2){
		mes "["+getarg(0)+"]";
		mes "Mmm, ¿te has equivocado?";
		mes "Bueno, escribe otra vez tu apuesta.";
		next;
		goto L_Input;
	}
	if(countitem(512)<@amount){
		//more apples then in inventory
		//-Improvised-
		mes "["+getarg(0)+"]";
		mes "Ooh...";
		mes "No llevas tantas Manzanas contigo...";
		close;
	}
	delitem 512,@amount;
	mes "["+getarg(0)+"]";
	mes "¡Bien!";
	mes "¡Comenzemos! Yo tiraré primero~";
	next;
	mes "^0000FF*Agitando los dados*";
	set @table1,rand(1,6);
	set @table2,rand(1,6);
	set @tablesub,@table1+@table2;
	next;
	mes "Tengo un "+@table1+" y un "+@table2+", "+@tablesub+" en total.";
	mes "Tira tú ahora.";
	mes strcharinfo(0)+".";
	next;
	menu "Tirar",-;

	mes "^0000FF*Agitando los dados*";
	set @player1,rand(1,6);
	set @player2,rand(1,6);
	set @playersub,@player1+@player2;
	next;
	mes "["+getarg(0)+"]";
	mes strcharinfo(0)+", conseguiste un "+@player1+" y un "+@player2+", sumando un total de ^FF0000"+@playersub+"^000000.";
	next;
	mes "["+getarg(0)+"]";
	if(@playersub == @tablesub) {
		mes "Bueno, bueno, bueno, los dos hemos conseguido "+@playersub+". ¿Quieres tirar el tercer dado, "+strcharinfo(0)+"?";
	} else if(@playersub > @tablesub) {
		mes "Con mi ^0000FF"+@tablesub+"^000000 y tu ^FF0000"+@playersub+"^000000, ahora mismo llevas tú la ventaja.";
		mes "¿Quieres tirar el tercer dado? Recuerda que perderás si te pasas de 12.";
	} else if(@tablesub > @playersub) {
		mes "Con mi ^0000FF"+@tablesub+"^000000 y tu ^FF0000"+@playersub+"^000000, ahora mismo la ventaja es mía.";
		mes "¿Quieres tirar el tercer dado? Recuerda que perderás si te pasas de 12.";
	}
	next;
	if(select("Tirarlo:No tirarlo")==2){
		mes "["+getarg(0)+"]";
		mes "No sigues, ¿eh?";
		if(@tablesub == @playersub) {
			mes "Reza por que saque más de 12 y me pase, ¡porque si no te ganaré!";
			mes "Allá va...";
			next;
			goto L_Table3;
		} else if(@tablesub < @playersub) {
			next;
			mes "["+getarg(0)+"]";
			mes "Bueno, se ve que no quieres arriesgarte. Voy a tirarlo yo a ver que pasa.";
			next;
			goto L_Table3;
		} else if(@tablesub > @playersub) {
		//you have a lower sub total then table, and do not roll 3th -Improvised-
			mes "["+getarg(0)+"]";
			mes "No sigues, ¿eh?";
			mes "Vale, yo me planto con lo que tengo ya.";
			next;
			mes "["+getarg(0)+"]";
			mes "Eso significa que con mi ^0000FF"+@tablesub+"^000000 venzo a tu ^FF0000"+@playersub+"^000000.";
			mes "Lo siento pero has perdido, "+strcharinfo(0)+". Suerte para la próxima.";
		}
		close;
	}
	mes "^0000FF*Agitando los dados*";
	set @player3,rand(1,6);
	set @playersub,@playersub+@player3;
	if(@playersub > 12) {
	//player bust --Improvised--
		next;
		mes "["+getarg(0)+"]";
		mes "Vaya..., ^FF000"+@player3+"^000000, con eso haces un total de ^FF0000"+@playersub+"^000000.";
		mes "Te has pasado de 12, "+strcharinfo(0)+", perdiste.";
		close;
	}
	next;
	mes "["+getarg(0)+"]";
	mes "¡Eh! Sacaste un";
	if(@playersub >= @tablesub) {
		mes "^FF0000"+@player3+"^000000, eso te da un total de ^FF0000"+@playersub+"^000000.";
		mes "Como no tire el tercero, ¡perderé!";
		mes "A ver si tengo suerte...";
		next;
		goto L_Table3;
	} else if(@playersub < @tablesub) {
		//player's sub together with 3th die is still too low
		//--Improvised--
		mes "^FF0000"+@player3+"^000000, eso te da un total de ^FF0000"+@playersub+"^000000.";
		mes "Aun así no es suficiente para vencer mi ^0000FF"+@tablesub+"^000000. Lo siento "+strcharinfo(0)+", has perdido.";
		close;
	}
L_Table3:
	mes "^0000FF*Agitando los dados*";
	set @table3,rand(1,6);
	set @tablesub,@tablesub+@table3;
	next;
	if(@tablesub > 12) {
		mes "["+getarg(0)+"]";
		mes "¡Eh? He sacado un ^0000FF"+@table3+"^000000, haciendo un total de ^0000FF"+@tablesub+"^000000.";
		mes "Me duele tener que decir esto, pero me arriesgué y perdí.";
		mes "¡Toma tu victoria antes de que me ponga a llorar, "+strcharinfo(0)+"~!";
		close2;
		getitem 512,@amount*2;
		end;
	}
	mes "["+getarg(0)+"]";
	if(@tablesub < @playersub) {
		mes "He sacado un "+@table3+", haciendo un total de ^0000FF"+@tablesub+"^000000. Aun así estoy por debajo de tu ^FF0000"+@playersub+"^000000.";
		mes "Se ve que no puedo competir contigo, "+strcharinfo(0)+"...";
		close2;
		getitem 512,@amount*2;
		end;
	} else if(@tablesub > @playersub) {
	mes "He sacado un ^0000FF"+@table3+"^000000, haciendo un total de ^0000FF"+@tablesub+"^000000. He superado tu ^FF0000"+@playersub+"^000000.";
	mes "Lo siento "+strcharinfo(0)+", inténtalo la próxima vez.";
	close;
	} else if(@tablesub == @playersub) {
		//Result = tie, --Improvised--
		mes "He sacado un ^0000FF"+@table3+"^000000, haciendo un total de ^0000FF"+@tablesub+"^000000.";
		mes "Al final hemos empatado, jajaja.";
		close2;
		getitem 512,@amount;
		end;
	}
}
