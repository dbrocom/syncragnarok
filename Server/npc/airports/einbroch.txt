//===== eAthena Script ======================================= 
//= Einbroch Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena Revision 3000+
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= Converted by Dr.Evil, 1.0b more typos and credits [Lupus]
//= 1.1 Fixed wrong check, added extra condition [Justin84]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Fixed syntax errors for duplicate [KarLaeda]
//= 1.4 Removed use of goto. [L0ne_W0lf]
//============================================================


airport,126,43,4	script	Airport Staff#01::AirportE	90,{
	mes "[Airport Staff]";
	mes "Bienvenido al Aeropuerto.";
	mes "¿Cómo le puedo ayudar?";
	next;
	if (select("Board the Airship:Cancel") == 1) {
		mes "[Airport Staff]";
		mes "La tarifa de abordaje";
		mes "es 1,200 zeny, pero si tiene";
		mes "un Boleto Gratis Para Dirigible,"; 
		mes "no tendrá que pagar la tarifa. ¿Desea";
		mes "abordar la Aeronave?";
		next;
		if (select("Yes:No") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airport",148,51;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				close;
			}
			mes "[Airport Staff]";
			mes "No tiene suficiente zeny.";
			close;
		}
	}
	mes "[Airport Staff]";
	mes "Gracias y que";
	mes "tenga un buen día.";
	close;
}

airport,143,43,4	duplicate(AirportE)	Airport Staff#02	90
airport,156,43,4	duplicate(AirportE)	Airport Staff#03	90

airport,126,51,4	script	Airport Staff#04::AirportE2	90,{
	mes "[Airport Staff]";
	mes "Bienvenido.";
	mes "Por favor, diríjase por";
	mes "este camino para abordar el avión.";
	next;
	if (select("Exit to Main Terminal:Cancel") == 1) {
		mes "[Airport Staff]";
		mes "Si abandona la";
		mes "Terminal Principal,";
		mes "tendrá que pagar la tarifa";
		mes "nuevamente para abordar";
		mes "el Dirigible. ¿Está seguro";
		mes "de que desea salir?";
		next;
		if (select("Yes:No") == 1) {
			warp "airport",142,40;
			close;
		}
	}
	mes "[Airport Staff]";
	mes "Perfecto, muchas gracias";
	mes "por su preferencia";
	mes "y espero que tenga";
	mes "un vuelo placentero.";
	close;
}

airport,143,51,4	duplicate(AirportE2)	Airport Staff#05	90
airport,156,51,4	duplicate(AirportE2)	Airport Staff#06	90

einbroch,94,267,4	script	Airship Staff#07	91,{
	mes "[Airship Staff]";
	mes "Bienvenido al";
	mes "Aeropuerto de Einbroch";
	mes "Por favor use esta puerta para";
	mes "abordar la Aeronave con escalas en";
	mes "Lighthalzen y Yuno en";
	mes "la República de Schwarzwald.";
	next;
	mes "[Airship Staff]";
	mes "No obstante, si Einbroch es";
	mes "su destino final,";
	mes "por favor baje por las escaleras";
	mes "y pídale al Arrival Staff que lo guíe";
	mes "al terminal principal. Gracias,";
	mes "y que disfrute su viaje.";
	close;
}
