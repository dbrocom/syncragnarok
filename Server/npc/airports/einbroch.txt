//===== eAthena Script ======================================= 
//= Einbroch Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena Revision 3000+
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= Converted by Dr.Evil, 1.0b more typos and credits [Lupus]
//= 1.1 Fixed wrong check, added extra condition [Justin84]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Fixed syntax errors for duplicate [KarLaeda]
//= 1.4 Removed use of goto. [L0ne_W0lf]
//============================================================


airport,126,43,4	script	Airport Staff#01::AirportE	90,{
	mes "[Personal del Aeropuerto]";
	mes "Bienvenid"+(Sex?"o":"a")+" al Aeropuerto.";
	mes "¿En qué puedo ayudarte?";
	next;
	if (select("Quiero subir al Dirigible:Nada, gracias") == 1) {
		mes "[Personal del Aeropuerto]";
		mes "Embarcar en el Dirigible cuesta 1,200 zeny, aunque si tienes un Free Ticket for Flyship te saldrá gratis.";
		mes "¿Quieres subir?";
		next;
		if (select("Sí:No") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airport",148,51;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				close;
			}
			mes "[Personal del Aeropuerto]";
			mes "No tienes suficiente dinero.";
			close;
		}
	}
	mes "[Personal del Aeropuerto]";
	mes "Gracias y ten un buen día.";
	close;
}

airport,143,43,4	duplicate(AirportE)	Airport Staff#02	90
airport,156,43,4	duplicate(AirportE)	Airport Staff#03	90

airport,126,51,4	script	Airport Staff#04::AirportE2	90,{
	mes "[Personal del Aeropuerto]";
	mes "Bienvenid"+(Sex?"o":"a")+"~";
	mes "Por favor, sal por el centro de esta sala para subir al Dirigible.";
	next;
	if (select("Salir de aquí:Cancelar") == 1) {
		mes "[Personal del Aeropuerto]";
		mes "Si sales de la zona de embarque, tendrás que pagar el peaje otra vez para poder subir al Dirigible.";
		mes "¿Estás segur"+(Sex?"o":"a")+" de que quieres salir?";
		next;
		if (select("Sí:No") == 1) {
			warp "airport",142,40;
			close;
		}
	}
	mes "[Personal del Aeropuerto]";
	mes "Gracias por tu patrocinio y esperamos que tengas un agradable vuelo~";
	close;
}

airport,143,51,4	duplicate(AirportE2)	Airport Staff#05	90
airport,156,51,4	duplicate(AirportE2)	Airport Staff#06	90

einbroch,94,267,4	script	Airship Staff#07	91,{
	mes "[Personal del Dirigible]";
	mes "Bienvenid"+(Sex?"o":"a")+" al Aeropuerto de Einbroch.";
	mes "Entra por la puerta de aquí al lado para viajar en el Dirigible hacia Lighthalzen y Yuno en la República Schwaltzvalt.";
	next;
	mes "[Personal del Dirigible]";
	mes "Si por el contrario tu destino es Einbroch, por favor baja las escaleras y habla con Arrival Staff para que te saque de la zona de embarque.";
	mes "Gracias y disfruta de tu viaje.";
	close;
}
