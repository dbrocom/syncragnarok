//===== eAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= DZeroX
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= Town-specific Alberta NPCs
//===== Additional Comments: ================================= 
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//============================================================

alberta,97,51,0	script	Fabian	84,{
	mes "[Fabian]";
	mes "Colega... cuando viajas por todo el mundo, escuchas cosas realmente increibles.";
	next;
	mes "[Fabian]";
	mes "Una vez me hablaron de unas cartas que contienen el poder de los monstruos. Dicen que si alguien consigue una, es capaz de utilizar el poder de ese monstruo.";
	next;
	mes "[Fabian]";
	mes "Supongo que será algún tipo de estafa o algo así, para hacer que consigas todas las cartas. Porque la verdad, ¡¿como puede una carta llevar dentro el poder de un monstruo?!";
	next;
	mes "[Fabian]";
	mes "En serio...";
	close;
}

alberta,53,39,0	script	Steiner	100,{
	mes "[Steiner]";
	mes "¡Oh...! ";
	mes "¡Bienvenido a Alberta,";
	mes "joven aventurero!";
	next;
	mes "[Steiner]";
	mes "Perdóname si parezco distraido. Estoy concentrado, planeando algo. Verás, he escuchado que hay una tienda en Geffen que vende armaduras resistentes a la magia.";
	next;
	mes "[Steiner]";
	mes "Si comprara una gran cantidad de ellas, y las vendiera por aquí a un precio más alto...";
	close;
}

alberta,20,183,0	script	Chad	49,{
	mes "[Chad]";
	mes "La gente dice que el arma legendaria Gungnir nunca falla contra un objetivo. Me pregunto si será verdad...";
	next;
	mes "[Chad]";
	mes "También se dice que los niños los traen las cigueñas, que las chicas solo buscan hombres que se comportan como idiotas, ¡y que Santa Claus existe! Pero solo en Lutie.";
	next;
	mes "[Chad]";
	mes "Me pregunto...";
	mes "Si es posible ";
	mes "que alguna de esas cosas";
	mes "sea cierta...";
	close;
}

alberta,131,139,2	script	Drunken Old Man	54,{
	mes "[Deagle]";
	mes "^666666*Hiccup*^000000";
	mes "¿Qu-qué estás";
	mes "mirando? ¡¡Piérdete!!!";
	next;	
	switch(select("Say nothing.","Leave him alone.")) {
		case 1:
			mes "[Deagle]";
			mes "Jajajaja ^666666*hiccup*^000000... Tienes pinta de ser valiente. Puede que parezca un cobarde ahora, pero hace años fui marinero del 'Going Mary.'";
			next;
			switch(select("Never heard of it.","Really? No kidding!")) {
				case 1:
					mes "[Deagle]";
					mes "¡¿Nunca escuchaste hablar de él?! Todo el mundo conoce el famoso barco pirata 'Going Mary!' ^666666*Hiccup~*^000000";
					next;
					mes "[Deagle]";
					mes "Ah~ Viejos tiempos. Sí... si no hubieramos caido en aquella TORMENTA...^666666*hiccup*^000000";
					next;
					mes "[Deagle]";
					mes "AH~ Capitán. Echo mucho de menos a nuestro capitán... Ningún enemigo sobrevivió a su espada.";
					close;
				case 2:
					mes "[Deagle]";
					mes "¡Es cierto! ¡NADIE se metía con la tripulación del 'Going Mary!' ¡Y nadie podía vencer a nuestro capitán en una pelea de espadas!";
					next;
					mes "[Deagle]";
					mes "¡¡CAPITÁN~!!! ^666666*HICCUP~*^000000 El movería su espada de aquella forma, y entonces...";
					next;
					mes "[Deagle]";
					mes "¡Cuando el cabrón del capitán luchaba, cualquier compañero que estuviera cerca de él se rodeaba de llamas!";
					next;
					mes "[Deagle]";
					mes "Colega, esa espada debía tener algún tipo de poder misterioso, ¡O era el capitán que era así de bueno...!";
					next;
					mes "[Deagle]";
					mes "Buf~~ ^666666*Sollozo* *Sollozo...*^000000 ¡Dios, los echo de menos a todos! ¡Estoy deprimido! Por favor, vete ya.";
					close;
			}
		case 2:
			mes "[Deagle]";
			mes "¡Eso es!";
			mes "VETE~";
			close;
	}
}

alberta,58,80,2	script	Shakir	99,{
	mes "[Shakir]";
	if (rand(2)) {
		mes "Los Merchants tenemos nuestra propia skill de negociación para vender items. Esta skill nos permite obtener mas dinero que otra gente vendiéndolos.";
		next;
		mes "[Shakir]";
		mes "Es más complicado que ir por ahí diciendo simplemente '¡Tendrás que darme más dinero!'. Necesitas tener carisma, ¡Y ser un maestro de la retórica!";
		next;
		mes "[Shakir]";
		mes "Conseguimos hasta un 24 % mas de zeny con esta increible skill. ¡¡Entrena mucho para conseguirla!!";
	} else {
		mes "Los Merchants podemos ";
		mes "abrir tiendas en";
		mes "cualquier parte para hacer negocios.";
		next;
		mes "[Shakir]";
		mes "Con la skill Discount, compramos items a muy bajo precio en las tiendas de los pueblos y los cargamos en los cart que alquilamos.";
		next;
		mes "[Shakir]";
		mes "Luego viajamos a donde haga falta, ¡Y los vendemos para obtener beneficio!";
		next;
		mes "[Shakir]";
		mes "De esa forma, los negocios salen más rentables.";
	}
	close;
}

alberta,62,156,2	script	Sonya	102,{
	mes "[Sonya]";
	switch(rand(3)){
	case 0:
		mes "Hey, you know, this one time I was walking through the forest and I saw this little green stem moving around.";
		next;
		mes "[Sonya]";
		mes "I went to see what it was and when I went to touch it. The stem actually slapped my hand!";
		next;
		mes "[Sonya]";
		mes "It startled me, so I jumped back a bit and then I realized it wasn't a stem, but a very small animal.";
		next;
		mes "[Sonya]";
		mes "I was lucky I didn't upset it. Even the smallest animal can be dangerous if angered.";
		close;
	case 1:
		mes "You know those lazy looking bears that live in the forest on the way to Payon?";
		next;
		mes "[Sonya]";
		mes "Just for fun, I threw a rock at it and all of sudden it rushed at me! I was sooooo scared, I started to run away, then BAM!!!";
		next;
		mes "[Sonya]";
		mes "It ran into a low tree branch and knocked itself out! I swear, I'll never provoke an animal for fun again!";
		close;
	case 2:
		mes "I once saw a pack of wolves take on one of those huge, lazy bears!";
		next;
		mes "[Sonya]";
		mes "Wolves are much more cooperative than they may seem. If one of them is attacked, then any nearby wolves will run to help.";
		next;
		mes "[Sonya]";
		mes "I'd think twice if you ever want to fight one when others of its kind are around. Be careful: don't get ganged up on!";
		close;
	}
}

alberta,93,174,2	script	Grandmother Alma	103,{
	mes "[Grandmother Alma]";
	mes "Hace algún tiempo,";
	mes "un barco abandonado";
	mes "llegó a la deriva";
	mes "al puerto de Alberta.";
	next;
	mes "[Grandmother Alma]";
	mes "Hoping to save any survivors, some of the townspeople ventured into the ship. However, they all ran out terrified, saying that corpses were walking around inside the ship.";
	next;
	mes "[Grandmother Alma]";
	mes "The ship was also packed with dangerous marine organisms, and they couldn't get inside, even if they wanted to.";
	next;
	mes "[Grandmother Alma]";
	mes "We couldn't do anything about that ominous looking ship, and just left it as it was. Nowadays, exploration teams try to enter that ship and wipe out its monsters.";
	next;
	mes "[Grandmother Alma]";
	mes "So it might be a good experience for a young person like yourself to be a recruit. But, it's still not worth risking your life if you're not strong enough.";
	close;
}

alberta,189,151,5	script	Fisk	100,{
	mes "[Fisk]";
	mes "Hola marinero,";
	mes "¿a dónde quieres";
	mes "ir?";
	next;
	switch(select("Sunken Ship -> 250 zeny.","Izlude Marina -> 500 zeny.","Never mind.")) {
		case 1:
			if (Zeny < 250) {
				mes "[Fisk]";
				mes "Hey now, don't try to cheat me! I said 250 zeny!";
				close;
			}
			set Zeny,Zeny - 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[Fisk]";
				mes "Ain't no way yer getting there without the 500 zeny first!";
				close;
			}
			set Zeny,Zeny - 500;
			warp "izlude",176,182;
			end;
		case 3:
			mes "[Fisk]";
			mes "Está bien...";
			mes "No te gusta el mar, ¿eh?.";
			close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	100,{
	mes "[Fisk]";
	mes "So you wanna head back to the mainland in Alberta, eh?";
	next;
	if(select("Yes please.","I changed my mind.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	86,{
	mes "[Paul]";
	mes "Buen día~";
	mes "¿Te gustaría";
	mes "unirte al";
	mes "equipo de exploradores";
	mes "de Sunken Ship?";
	next;
	mes "[Paul]";
	mes "Oh! Before you join, I must warn you. If you're not that strong, you may not want to go.";
	next;
	mes "[Paul]";
	mes "Así que,";
	mes "¿quieres anotarte?";
	mes "El pago por";
	mes "la admisión es sólo";
	mes "200 Zeny. ";
	next;
	switch(select("Sign me up!","Uh, no thanks.")) {
		case 1:
			if (Zeny < 200) {
				mes "[Paul]";
				mes "It seems you don't have the money, my friend. But please come back when you're able to pay.";
				close;
			} else {
				set Zeny,Zeny - 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[Paul]";
			mes "De acuerdo, bueno...";
			mes "Estaré por aquí";
			mes "si cambias";
			mes "de parecer.";
			close;
	}
}

alberta,190,173,4	script	Phelix	85,{
	set .@weight,MaxWeight-Weight;
	mes "[Phelix]";
	if ((.@weight) < 10000) {
		mes "¡¡Espera un momento!!";
		mes "¡Has traído muchas cosas!";
		mes "¡No puedes aceptar más cosas!";
		mes "Por favor, reduce la cantidad de cosas que cargas,";
		mes "luego ven a verme otra vez.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "¿Qué demonios haces aquí?";
		mes "There is nothing you can get for free on this ship, if you want somethin', work for it!!";
		next;
		mes "[Phelix]";
		mes "Hmm, so why don't you bring me 10 jellopies and I will give 1 potion. How's that sound?";
		mes "Or if that's too hard for your pansy ass, 3 jellopies for 1 Carrot.";
		next;
		mes "[Phelix]";
		mes "If you're interested in my offer, get me the stuff I mentioned.";
		set @event_zelopy,1;
		close;
	} else {
		mes "Hmm.. you want to exchange jellopies for Red Potions or some Carrots eh? Well.. which one?";
		next;
		switch(select("Red Potions please.","Carrots please.")) {
		case 1:
			mes "[Phelix]";
			mes "Está bien...";
			mes "Déjame ver";
			mes "qué tienes...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 10) {
				mes "Hey! Weren't you listening?. I said 10 jellopies for 1 Red Potion.. are ya deaf?";
				close;
			} else {
				set .@max,countitem(909)/10;
				mes "Hmmm, nada mal...";
				mes "¿Cuántas pociones";
				mes "quieres obtener?";
				next;
				switch(select("As many as I can, please.","I want this many.","Never mind, I like my jellopy.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Red_Potion
					break;
				case 2:
					mes "[Phelix]";
					mes "I'm not giving you more then 100 at a time so don't bother, OK? If you don't want any, just say '0'.";
					mes "Right now, the most you can get is " + gap + " but remember, 100 at most, you want to break my back?.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount <= 0) {
						mes "Muy agradecido, regresa cuando gustes.";
						close;
					}
					if (.@amount > 100) {
						mes "Hey, what'd I say? 100 at a time at most, you're trying to kill me aren't you!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Hmm, it looks like you don't have enough. Go get more jellopies if you want anything else from me.";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Red_Potion
					break;
				case 3:
					mes "[Phelix]";
					mes "No hay problema,";
					mes "nos vemos la próxima vez.";
					close;
				}
				mes "[Phelix]";
				mes "There you go! As I promised. Don't go suckin' them all down at once.";
				close;
			}
		case 2:
			mes "[Phelix]";
			mes "Muy bien, déjame ver lo que tienes...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 3) {
				mes "Hmm, look pansy ass, I said 3 jellopies for 1 Carrot.. got it?";
				close;
			} else {
				set .@max,countitem(909)/3;
				mes "No seas tan incrédulo...";
				mes "¿Cuántos quieres?";
				next;
				switch(select("As many as I can get, please","I want this many.","Never mind, I like my jellopy.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Carrot
					break;
				case 2:
					mes "[Phelix]";
					mes "Right I'm not giving you more then 100 at a time so don't bother, okay? If you don't want any, just say '0'.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount == 0) {
						mes "Muy bien entonces, nos vemos la próxima vez.";
						close;
					}
					if (.@amount > 100) {
						mes "Hey pansy ass, I said 100 at most, no more then that! I'm not going to break my back for the likes of you!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Seems you don't have enough. Go get some more if you want anything else.";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Carrot
					break;
				case 3:
					mes "[Phelix]";
					mes "Nos vemos después.";
					close;
				}
				mes "[Phelix]";
				mes "There you go~! As I promised. Try not to stuff yer face.";
				close;
			}
		}
	}
}
