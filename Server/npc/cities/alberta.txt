//===== eAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= DZeroX
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= Town-specific Alberta NPCs
//===== Additional Comments: ================================= 
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//============================================================

alberta,97,51,0	script	Fabian	84,{
	mes "[Fabian]";
	mes "Colega... cuando viajas por todo el mundo, escuchas cosas realmente increibles.";
	next;
	mes "[Fabian]";
	mes "Una vez me hablaron de unas cartas que contienen el poder de los monstruos. Dicen que si alguien consigue una, es capaz de utilizar el poder de ese monstruo.";
	next;
	mes "[Fabian]";
	mes "Supongo que será algún tipo de estafa o algo así, para hacer que consigas todas las cartas. Porque la verdad, ¡¿como puede una carta llevar dentro el poder de un monstruo?!";
	next;
	mes "[Fabian]";
	mes "En serio...";
	close;
}

alberta,53,39,0	script	Steiner	100,{
	mes "[Steiner]";
	mes "¡Oh...! ";
	mes "¡Bienvenid"+(Sex?"o":"a")+" a Alberta, joven aventurer"+(Sex?"o":"a")+"!";
	next;
	mes "[Steiner]";
	mes "Perdóname si parezco distraido. Estoy concentrado, planeando algo. Verás, he escuchado que hay una tienda en Geffen que vende armaduras resistentes a la magia.";
	next;
	mes "[Steiner]";
	mes "Si comprara una gran cantidad de ellas, y las vendiera por aquí a un precio más alto...";
	close;
}

alberta,20,183,0	script	Chad	49,{
	mes "[Chad]";
	mes "La gente dice que el arma legendaria Gungnir nunca falla contra un objetivo. Me pregunto si será verdad...";
	next;
	mes "[Chad]";
	mes "También se dice que los niños los traen las cigueñas, que las chicas solo buscan hombres que se comportan como idiotas, ¡y que Santa Claus existe! Pero solo en Lutie.";
	next;
	mes "[Chad]";
	mes "Me pregunto... Si es posible que alguna de esas cosas sea cierta...";
	close;
}

alberta,131,139,2	script	Drunken Old Man	54,{
	mes "[Deagle]";
	mes "^666666*Hiccup*^000000";
	mes "¿Qu-qué estás mirando? ¡¡Piérdete!!!";
	next;	
	switch(select("No decir nada.","Dejarlo solo.")) {
		case 1:
			mes "[Deagle]";
			mes "Jajajaja ^666666*hiccup*^000000... Tienes pinta de ser valiente. Puede que ahora te pueda parecer un cobarde, pero hace años fui marinero del 'Going Mary'.";
			next;
			switch(select("No conozco ese barco.","¿En serio? ¡Estás de broma!")) {
				case 1:
					mes "[Deagle]";
					mes "¡¿Nunca escuchaste hablar de él?! Todo el mundo conoce el famoso barco pirata 'Going Mary'! ^666666*Hiccup~*^000000";
					next;
					mes "[Deagle]";
					mes "Ah~ Viejos tiempos. Sí... si no hubieramos caido en aquella TORMENTA...^666666*hiccup*^000000";
					next;
					mes "[Deagle]";
					mes "AH~ Capitán. Echo mucho de menos a nuestro capitán... Ningún enemigo sobrevivió a su espada.";
					close;
				case 2:
					mes "[Deagle]";
					mes "¡Es cierto! ¡NADIE se metía con la tripulación del 'Going Mary'! ¡Y nadie podía vencer a nuestro capitán en una pelea de espadas!";
					next;
					mes "[Deagle]";
					mes "¡¡¡CAPITÁN~!!! ^666666*HICCUP~*^000000 El movería su espada de aquella forma, y entonces...";
					next;
					mes "[Deagle]";
					mes "¡Cuando el cabrón del capitán luchaba, cualquier compañero que estuviera cerca de él se rodeaba de llamas!";
					next;
					mes "[Deagle]";
					mes "Colega, esa espada debía tener algún tipo de poder misterioso, ¡o era el capitán, que era así de bueno...!";
					next;
					mes "[Deagle]";
					mes "Buf~~ ^666666*Sob* *Sob...*^000000 ¡Dios, los echo de menos a todos! ¡Estoy deprimido! Por favor, vete ya.";
					close;
			}
		case 2:
			mes "[Deagle]";
			mes "¡Eso es!";
			mes "VETE~";
			close;
	}
}

alberta,58,80,2	script	Shakir	99,{
	mes "[Shakir]";
	if (rand(2)) {
		mes "Los Merchants tenemos nuestra propia skill de negociación para vender items. Esta skill nos permite obtener más dinero que otra gente vendiéndolos.";
		next;
		mes "[Shakir]";
		mes "Es más complicado que ir por ahí diciendo simplemente '¡Tendrás que darme más dinero!'. Necesitas tener carisma, ¡y ser un maestro de la retórica!";
		next;
		mes "[Shakir]";
		mes "Conseguimos hasta un 24 % mas de zeny con esta increible skill. ¡¡Entrena mucho para conseguirla!!";
	} else {
		mes "Los Merchants podemos abrir tiendas en cualquier parte para hacer negocios.";
		next;
		mes "[Shakir]";
		mes "Con la skill Discount, compramos items a muy bajo precio en las tiendas de los pueblos y los cargamos en los cart que alquilamos.";
		next;
		mes "[Shakir]";
		mes "Luego viajamos a donde haga falta, ¡Y los vendemos para obtener beneficio!";
		next;
		mes "[Shakir]";
		mes "De esa forma, los negocios salen más rentables.";
	}
	close;
}

alberta,62,156,2	script	Sonya	102,{
	mes "[Sonya]";
	switch(rand(3)){
	case 0:
		mes "Sabes, una vez mientras paseaba por el bosque vi una pequeña raiz que se movía.";
		next;
		mes "[Sonya]";
		mes "Me acerqué a ver que era y cuando la toqué, ¡me pegó en la mano!";
		next;
		mes "[Sonya]";
		mes "Me asustó tanto que pegué un salto hacia atrás, y entonces me di cuenta de que no era una raiz, sino un animal pequeño.";
		next;
		mes "[Sonya]";
		mes "Fue una suerte que no se enfadara. Hasta el animal más diminuto puede ser peligroso si lo molestas.";
		close;
	case 1:
		mes "¿Sabes cuales son esos osos perezosos que viven en el bosque cerca de Payon?";
		next;
		mes "[Sonya]";
		mes "Una vez, para divertirme, le tiré a uno una roca, ¡y se lanzó contra mi! Estaba asustadííííísima, empecé a correr y ¡¡¡PAM!!!";
		next;
		mes "[Sonya]";
		mes "¡Se golpeó en la cabeza con la rama de un arbol! Te lo juro, ¡nunca más provocaré a un animal por diversión!";
		close;
	case 2:
		mes "¡Una vez vi a un grupo de lobos llevarse por delante a uno de esos osos perezosos!";
		next;
		mes "[Sonya]";
		mes "Los lobos se ayudan más entre ellos de lo que parece. Si le atacas a uno, los que se encuentren cerca irán en su ayuda.";
		next;
		mes "[Sonya]";
		mes "Yo me lo pensaría dos veces si quieres enfrentarte a uno habiendo otros en las cercanías. Ten cuidado: ¡son una manada!";
		close;
	}
}

alberta,93,174,2	script	Grandmother Alma	103,{
	mes "[Abuela Alma]";
	mes "Hace algún tiempo, un barco abandonado llegó navegando a la deriva al puerto de Alberta.";
	next;
	mes "[Abuela Alma]";
	mes "Esperando encontrar supervivientes, algunos aldeanos se adentraron en él. Sin embargo, todos escaparon aterrorizados, diciendo que había cadáveres caminando por su interior.";
	next;
	mes "[Abuela Alma]";
	mes "El barco estaba lleno de peligrosos organismos subacuáticos, y no se podía llegar al final por mucho que lo intentarán.";
	next;
	mes "[Abuela Alma]";
	mes "En ese momento, no pudimos hacer nada más que observar aquel extraño barco, y dejarlo como estaba. Hoy día, los equipos de exploración entran para limpiar la zona de monstruos.";
	next;
	mes "[Abuela Alma]";
	mes "Podría ser una buena experiencia para una persona joven como tú echarle un vistazo. Pero ten cuidado, no tiene sentido arriesgar tu vida si no eres lo suficientemente fuerte.";
	close;
}

alberta,189,151,5	script	Fisk	100,{
	mes "[Fisk]";
	mes "Hola mariner"+(Sex?"o":"a")+", ¿A dónde quieres ir?";
	next;
	switch(select("Sunken Ship -> 250 zeny.","Izlude Marina -> 500 zeny.","A ningún lugar.")) {
		case 1:
			if (Zeny < 250) {
				mes "[Fisk]";
				mes "Eh, ¡No me engañes! ¡He dicho 250 zeny!";
				close;
			}
			set Zeny,Zeny - 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[Fisk]";
				mes "¡No tienes forma de llegar allí sin darme primero los 500 zeny!";
				close;
			}
			set Zeny,Zeny - 500;
			warp "izlude",176,182;
			end;
		case 3:
			mes "[Fisk]";
			mes "Está bien...";
			mes "No te gusta el mar, ¿eh?.";
			close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	100,{
	mes "[Fisk]";
	mes "¿Así que quieres volver a Alberta, eh?";
	next;
	if(select("Sí, por favor.","He cambiado de opinión.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	86,{
	mes "[Paul]";
	mes "Buenos días~";
	mes "¿Te gustaría unirte al equipo de exploración de Sunken Ship?";
	next;
	mes "[Paul]";
	mes "¡Ah! Antes de continuar, tengo que avisarte de algo. Si no eres lo suficientemente fuerte, no deberías entrar.";
	next;
	mes "[Paul]";
	mes "Entonces, ¿quieres apuntarte?";
	mes "La cuota de entrada son sólo 200 zeny.";
	next;
	switch(select("¡Entrar!","Eh... no, gracias.")) {
		case 1:
			if (Zeny < 200) {
				mes "[Paul]";
				mes "Parece que no tienes suficiente dinero, colega. Vuelve cuando seas capaz de pagar la cuota.";
				close;
			} else {
				set Zeny,Zeny - 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[Paul]";
			mes "Bueno, como quieras...";
			mes "Estaré por aquí si cambias de opinión.";
			close;
	}
}

alberta,190,173,4	script	Phelix	85,{
	set .@weight,MaxWeight-Weight;
	mes "[Phelix]";
	if ((.@weight) < 10000) {
		mes "¡¡Espera un momento!!";
		mes "¡Has traído muchas cosas!";
		mes "¡No te puedo dar nada!";
		mes "Por favor, reduce la cantidad de items que cargas, y ven a verme.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "¿Qué demonios haces aquí?";
		mes "No hay nada gratis en este barco, si quieres algo, ¡¡trabaja para conseguirlo!!";
		next;
		mes "[Phelix]";
		mes "Mmm, si me traes 10 Jellopies te daré 1 Red Potion. ¿Qué te parece?";
		mes "Si lo ves demasiado difícil para tu flacucho cuerpo, 3 jellopies por 1 Carrot.";
		next;
		mes "[Phelix]";
		mes "Si te interesa mi oferta, tráeme lo que te pedí.";
		set @event_zelopy,1;
		close;
	} else {
		mes "Mmm.. ¿Quieres cambiar Jellopies por Red Potions ó Carrots? Vale.. ¿Qué te interesa?";
		next;
		switch(select("Red Potions por favor.","Mejor Carrots.")) {
		case 1:
			mes "[Phelix]";
			mes "Está bien... Déjame ver lo que traes...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 10) {
				mes "¡Eh! ¿Me estabas escuchando?. Te dije 10 jellopies por 1 Red Potion.. ¿Estás sord"+(Sex?"o":"a")+" o qué?";
				close;
			} else {
				set .@max,countitem(909)/10;
				mes "Mmmm, perfecto...";
				mes "¿Cuántas potions te interesán?";
				next;
				switch(select("Tantas como me puedas dar.","Decirle una cantidad.","Ninguna, me gustan mis Jellopies.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Red_Potion
					break;
				case 2:
					mes "[Phelix]";
					mes "No te puedo dar más de 100 de cada vez, así que no me fastidies, ¿VALE? Si no quieres nada, pon '0'.";
					mes "Ahora mismo, la cantidad máxima que puedes conseguir es de " + .@max + ", pero recuerda, 100 como máximo, ¿o acaso quieres que me rompa la espalda?";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount <= 0) {
						mes "Te lo agradezco, vuelve cuando quieras.";
						close;
					}
					if (.@amount > 100) {
						mes "Eh, ¿qué te he dicho? 100 como mucho, ¿o es que quieres matarme?";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Mmm, por lo que se ve no tienes suficientes. Consigue más Jellopies si quieres seguir haciendo negocios conmigo.";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Red_Potion
					break;
				case 3:
					mes "[Phelix]";
					mes "Vale, nos vemos la próxima vez.";
					close;
				}
				mes "[Phelix]";
				mes "¡Aquí tienes, como te prometí! No te lo tomes todo junto.";
				close;
			}
		case 2:
			mes "[Phelix]";
			mes "Muy bien, déjame ver lo que tienes...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 3) {
				mes "Mmm, pareces un poco estúpid"+(Sex?"o":"a")+", te dije 3 jellopies por 1 Carrot.. ¿lo coges?";
				close;
			} else {
				set .@max,countitem(909)/3;
				mes "No está mal nenaza...";
				mes "¿Cuántos quieres?";
				next;
				switch(select("Tantas como me puedas dar.","Decirle una cantidad.","Ninguna, me gustan mis Jellopies.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Carrot
					break;
				case 2:
					mes "[Phelix]";
					mes "No te puedo dar más de 100 de cada vez, así que no me fastidies, ¿VALE? Si no quieres nada, pon '0'.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount == 0) {
						mes "Muy bien, nos vemos la próxima vez.";
						close;
					}
					if (.@amount > 100) {
						mes "Eh, nenaza, te dije 100 como mucho, ¡no más! No me pienso romper la espalda para tí!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "No tienes suficiente. Vete a conseguir más items si quieres que te de algo.";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Carrot
					break;
				case 3:
					mes "[Phelix]";
					mes "Nos vemos.";
					close;
				}
				mes "[Phelix]";
				mes "¡Aquí tienes, como te prometí! No te empaches.";
				close;
			}
		}
	}
}
