//===== eAthena Script =======================================
//= Archer Skill Quest
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quest for skills: Arrow Crafting, Arrow Repel
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//============================================================

moc_ruins,118,99,5	script	Roberto#qsk_ac	88,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_MAKINGARROW") == 1) {
			mes "[Roberto]";
			mes "Ooh, ¡Eres de la misma ciudad que yo!";
			mes "¡Encantado de verte!";
			mes "¿Cómo estás?";
			mes "¡Ah! ¡Esa flecha!";
			mes "La hiciste tú, ¿no?";
			next;
			mes "[Roberto]";
			mes "¡Jaja...!";
			mes "¿Piensas que es mucho mejor?";
			mes "Jaja... de todas formas, estoy impresionado.";
			mes "Vuelve de vez en cuando";
			mes "con noticias de nuestro hogar.";
			mes "Entonces adiós.";
			close;
		}
		else if ((Basejob == Job_Archer && JobLevel >= 30) || BaseJob == Job_Hunter || BaseJob == Job_Dancer || BaseJob == Job_Bard) {
			mes "[Roberto]";
			mes "¡Ey!";
			mes " ";
			mes "Tu eres...";
			next;
			if (countitem(907) > 19 && countitem(921) > 6 && countitem(7012) > 40 && countitem(1019) > 12 && countitem(501) > 0) {
				mes "[Roberto]";
				mes "¡Las trajiste!";
				mes "Muchísimas gracias.";
				mes "Bueno, como prometí, te enseñare la skill.";
				next;
				delitem 907,20; //Resin
				delitem 921,7; //Mushroom_Spore
				delitem 7012,41; //Tough_Scalelike_Stem
				delitem 1019,13; //Wooden_Block
				delitem 501,1; //Red_Potion
				skill 147,1,0;
				mes "[Roberto]";
				mes "No hay necesidad de preocuparse por la cantidad de flechas que te quedan ahora.";
				mes "Oh, ¿y no habrás visto a";
				mes "a alguien llamado Jason en Payon?";
				mes "Ten cuidado. Es un";
				mes "hombre feroz.";
				next;
				mes "[Roberto]";
				mes "Solo tendrás que tener cuidado con Jason en Payon.";
				mes "Recuérdalo.";
				mes "Entonces adiós. Gracias por los regalos.";
				close;
			}
			else {
				mes "[Roberto]";
				mes "¿¡Un archer en Morroc!?";
				mes "¡Encantado de verte! ¡Conocer a un compañero";
				mes "arquero en un sitio como este! ¡*sniffsniff*!";
				mes "Yo vine solo a Morroc..";
				mes "pero era un recién llegado, y la presión... waaah~";
				mes "Estaba muy solo.";
				next;
				if (select("It must be hard. It's ok have faith.:Keep suffering.") == 1) {
					mes "[Roberto]";
					mes "Si. Gracias...";
					mes "Debes estar pasándolo mal";
					mes "en un lugar como este.";
					mes "¿No es difícil encontrar flechas?";
					mes "Por eso es que hice mis propias flechas.";
					next;
					select("Eh, ¡¿en serio?!");
					mes "[Roberto]";
					mes "¡Seh! Junto diversos objetos";
					mes "y hago flechas con ellos.";
					mes "Es una skill muy útil para ayudarme";
					mes "a sobrevivir solo en este duro mundo.";
					mes "Si quieres, puedo enseñártela.";
					next;
					select("Sería genial hacerlo.");
					mes "[Roberto]";
					mes "Pero... no lo puedo hacer gratis.";
					mes "Nada es gratis en este mundo.";
					mes "Mmm... ¿Qué te parece?";
					mes "Tráeme lo que te pida.";
					mes "Entonces te enseñare la skill.";
					next;
					mes "[Roberto]";
					mes "He estado muy solo desde que dejé mi ciudad natal.";
					mes "Querría algo que me aliviara la pena de estar lejos de mi ciudad,";
					mes "como cosas de allá.";
					mes "Tráeme 20 Resins de los árboles de la ";
					mes "selva de Payon, y 1 Red Potion";
					mes "que se vende en la tienda.";
					next;
					mes "[Roberto]";
					mes "También, 13 Trunk de los Willows que";
					mes "viven cerca del bosque de Payon,";
					mes "41 Tough Scalelike Stem, ";
					mes "y 7 Mushroom Spores.";
					mes "Si me traes todo esto,";
					next;
					mes "[Roberto]";
					mes "te enseñará la skill.";
					mes "Bueno... estaré esperando";
					mes "noticias de nuestra ciudad.";
					close;
				}
				mes "[Roberto]";
				if (Sex)
					mes "...hey chica.";
				else
					mes "...hey chico.";
				mes "...ten cuidado por la noche.";
				close;
			}
		}
		else {
			mes "[Roberto]";
			mes "Hmm... ¿Tienes";
			mes "algo que decir?";
			mes "Yo,";
			mes "nada.";
			mes "Diferencias en nivel";
			mes "terminan conversaciones.";
			close;
		}
	}
	else {
		mes "[?]";
		mes "Eh... ¿Primera vez que ves un archer o algo así?";
		mes "Solo sigue caminando a donde vas.";
		mes "Solo hablo con archers de alto nivel.";
		mes "¡No quieras que hable de otra cosa!";
		close;
	}
}

payon,103,63,3	script	Jason#qsk_ac	88,3,3,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_CHARGEARROW") == 1) {
			mes "[Jason]";
			mes "Ey, de nuevo nos vemos.";
			mes "Ehhhh que raro.";
			mes "Siempre que veo a alguien de nuevo";
			mes "empiezo a decir mucho.";
			mes "Ehhh... de todas formas encantado de volverte a ver.";
			mes "Ehhhh... no vuelvas ehh...";
			close;
		}
		else if ((Basejob == Job_Archer && JobLevel >= 35) || BaseJob == Job_Hunter || BaseJob == Job_Dancer || BaseJob == Job_Bard) {
			mes "[Jason]";
			mes "Joe... mi herida aun no cicatrizó.";
			mes "Bleh.. fui muy descuidado... ";
			mes "de volverme así.. emm...";
			mes "Pero aun ofendiéndome así,";
			mes "diciéndome tantos insultos...";
			next;
			mes "¿Qué debo hacer sobre Roberto?";
			mes "Mmmmm... ¡Ah!";
			mes "¿Tú? ¿Cuánto tiempo llevas ahí?";
			mes "Mmm... muy alto nivel.";
			mes "Alguien como tú podría estar";
			mes "preparado para aprender a usar Arrow Repel.";
			next;
			switch(select("What is that?:Teach me.")) {
			case 1:
				mes "[Jason]";
				mes "...estás bromeando, ¿no?";
				mes "Por favor, ¿aún no sabes usar";
				mes "a tu nivel Arrow Repel?";
				mes "Eres una persona extraña.";
				next;
				mes "[Jason]";
				mes "(Jason se queda en otro mundo.)";
				next;
				mes "[Jason]";
				mes "Em, bien. Te enseñaré";
				mes "lo que es Arrow Repel.";
				next;
				mes "[Jason]";
				mes "Arrow Repel es una skill que te permite";
				mes "apartar al oponente tan pronto ataques.";
				mes "Solo la puedes usar si la diriges exactamente";
				mes "al objetivo. Pero, a diferencia de la magia, ";
				mes "no se puede dejar de conjurar.";
				next;
				mes "[Jason]";
				mes "Es muy útil para un archer";
				mes "que es débil en distancias cortas.";
				mes "Si te interesa aprenderla,";
				mes "ven a mi de nuevo.";
				mes "Hay unos materiales necesarios.";
				next;
				mes "[Jason]";
				mes "Primero, porque tu tendrás que modificar un bow,";
				mes "trae un Crossbow que no uses.";
				mes "10 Tentacles, 10 Bill of Birds, ";
				mes "3 Yoyo Tails... Estos son muy elásticos.";
				mes "También, 2 Emeralds. Y por último, pero no menos importante...";
				mes "¡36 botellas de Banana Juice que me encantan!";
				next;
				mes "[Jason]";
				mes "......¡Ejem!";
				mes "Si me traes todo esto,";
				mes "Te enseñaré Arrow Repel.";
				mes "Entonces, hasta pronto.";
				mes "(Me voy a volver loco si no me traes el Banana Juice.)";
				close;
			case 2:
				if (countitem(721) > 1 && countitem(942) > 2 && countitem(962) > 9 && countitem(925) > 9 && countitem(532) > 35) {
					mes "[Jason]";
					mes "¡Ok! ¡Perfecto!";
					mes "Te enseñaré la nacionalmente";
					mes "conocida skill, ¡Arrow Repel!";
					next;
					delitem 721,2; //Azure_Jewel
					delitem 942,3; //Yoyo_Tail
					delitem 962,10; //Tentacle
					delitem 925,10; //Bill_Of_Birds
					delitem 532,36; //Banana_Juice
					skill 148,1,0;
					mes "[Jason]";
					mes "¡Oh, funciona mejor de lo que esperaba!";
					mes "¡No fue necesario modificar el bow!";
					mes "Puedes llevártelo.";
					mes "¡Y diviértete usando tu nueva";
					mes "skill tanto en campos como mazmorras!";
					mes "¡Je~Jeje~!";
					close;
				}
				else {
					mes "[Jason]";
					mes "Mmm... muy mal.";
					mes "Te estás olvidando algunas cosas.";
					mes "Te repito, necesitas 2 Emeralds,";
					mes "3 Yoyo Tails, 10 Tentacles, ";
					mes "10 Bill of Birds, y por último pero";
					mes "no menos importante, ¡36 botellas de Banana Juice!";
					next;
					mes "[Jason]";
					mes "¡Asegúrate de que tienes todo esto y regresa!";
					close;
				}
			}
		}
		else {
			mes "[Jason]";
			mes "Ooh... eres un archer.";
			mes "¡Si lo intentas un poco más";
			mes "tendrás una gran";
			mes "reputación como archer!";
			mes "¡Entrénate!";
			close;
		}
	}
	else {
		mes "[?]";
		mes "¿Qué necesita la vida de";
		mes "un solitario muchacho como yo?";
		close;
	}

OnTouch:
	mes "[???]";
	mes "Errrrrrr...";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= v1.0 Roberto message text is based off RO npc.  Jason message
//= text is custom from old version.
//= All items are from official quests though.[kobra_k88]
//= v1.0a Now using functions found in "Global_Functions.txt"
//= for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploits [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================
