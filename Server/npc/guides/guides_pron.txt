//===== eAthena Script ======================================= 
//= Prontera Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.6a
//===== Compatible With: ===================================== 
//= eAthena  SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Prontera
//===== Additional Comments: ================================= 
//= Fully working
//= v1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Removed Duplicates [Silent]
//= 1.6 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.6a Fixed some errors with the loop. [Samuray22]
//	-Thanks to $ephiroth.
//= 1.6b Fixed a Problem with Unnecesary RGB Values. [Samuray22]
//	-Thanks to Brainstorm.
//============================================================

prontera,153,195,4	script	Guide#prt::PrtGuide	105,{
	cutin "prt_soldier",2;
	mes "[Prontera Guide]";
	mes "Bienvenido a Prontera,";
	mes "la bella capital del reino de";
	mes "Rune-Midgard. Si";
	mes "tienes alguna pregunta o necesitas ayuda intentando encontrar algo en la ciudad, no dudes en preguntar.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guía de la Ciudad.:Eliminar Marcas del Mapa:Noticias:Cancelar")) {
		case 1:
			mes "[Prontera Guide]";
			mes "Por favor selecciona";
			mes "un lugar del";
			mes "siguiente menú";
			if (.@compass_check == 0) {
				mes "¿Te gustaría que";
				mes "marcara los lugares";
				mes "en tu Mini-Mapa?";
				next;
				if (select("Sí:No") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("Asociación de Espadachines:^0000FFSantuario^000000:Prontera Chivalry:Tienda de Armas:Tienda de Herramientas:Posada:Trading Post:Pub:Biblioteca:Job Agency:Castillo de Prontera:Ayuntamiento:^5F04B4Casa de los GMs^000000:Cancelar")) {
				case 1:
					mes "[Prontera Guide]";
					mes "La Asociación de Swordman";
					mes "que se encarga de los cambios de job";
					mes "a la clase swordman, ah";
					mes "sido movida a la ciudad de Izlude. Este edificio";
					mes "es solo un edificio vació ahora.";
					if (.@compass_check)
						viewpoint 1,237,41,4,0x00FF00;
					break;
				case 2:
					mes "[Prontera Guide]";
					mes "El santuario de prontera";
					mes "se encarga de los cambios de Job para";
					mes "las clases Acolyte y puede";
					mes "ser encontrado en la esquina";
					mes "noroeste de prontera";
					if (.@compass_check)
						viewpoint 1,236,316,5,0xFF0000;
					break;
				case 3:
					mes "[Prontera Guide]";
					mes "La Caballería de Prontera";
					mes "que se hace responsable por";
					mes "la seguridad de nuestra capital, está";
					mes "en la esquina noroeste de Prontera";
					if (.@compass_check)
						viewpoint 1,46,345,6,0x00FF00;
					break;
				case 4:
					mes "[Prontera Guide]";
					mes "La tienda de armas";
					mes "está localizada al noreste";
					mes "de la fuente central.";
					if (.@compass_check)
						viewpoint 1,175,220,7,0xFF00FF;
					break;
				case 5:
					mes "[Prontera Guide]";
					mes "La tienda de herramientas";
					mes "está localizada al noroeste";
					mes "de la fuente central";
					if (.@compass_check)
						viewpoint 1,134,221,8,0xFF00FF;
					break;
				case 6:
					mes "[Prontera Guide]";
					mes "Las Posadas en Prontera están";
					mes "localizadas ambas al este";
					mes "y al oeste de Prontera";
					mes "De la fuente central";
					if (.@compass_check) {
						viewpoint 1,204,191,9,0xFF00FF;
						viewpoint 1,107,192,10,0xFF00FF;
					}
					break;
				case 7:
					mes "[Prontera Guide]";
					mes "El puesto de cambios";
					mes "puede ser encontrado al sureste";
					mes "de la fuente central.";
					if (.@compass_check)
						viewpoint 1,179,184,11,0x00FF00;
					break;
				case 8:
					mes "[Prontera Guide]";
					mes "El Bar esta localizado";
					mes "al sureste de la fuente";
					mes "detrás de el puesto de cambios.";
					if (.@compass_check)
						viewpoint 1,208,154,12,0x00FF00;
					break;
				case 9:
					mes "[Prontera Guide]";
					mes "Si te diriges al norte de";
					mes "la fuente central, tu";
					mes "encontrarás una área vacía donde";
					mes "se puede acceder a ambas ramas ";
					mes " de la Biblioteca de Prontera si ";
					mes "te diriges al este o al oeste";
					if (.@compass_check) {
						viewpoint 1,120,267,13,0x00FF00;
						viewpoint 1,192,267,14,0x00FF00;
					}
					break;
				case 10:
					mes "[Prontera Guide]";
					mes "La agencia de Trabajos";
					mes "esta localizada al suroeste de la";
					mes "fuente central";
					if (.@compass_check)
						viewpoint 1,133,183,15,0x00FF00;
					break;
				case 11:
					mes "[Prontera Guide]";
					mes "el castillo de prontera esta";
					mes "localizado al norte";
					mes "sector de esta ciudad. Puedes";
					mes "ir a los campos que están al norte";
					mes "de Prontera si avanzas por";
					mes "la salida trasera del castillo.";
					if (.@compass_check)
						viewpoint 1,156,360,16,0x00FF00;
					break;
				case 12:
					mes "[Prontera Guide]";
					mes "El ayuntamiento";
					mes "está localizado en la";
					mes "esquina suroeste";
					mes "de nuestra ciudad de Prontera";
					if (.@compass_check)
						viewpoint 1,75,91,17,0x01FF01;
					break;
				case 13:
					mes "[Prontera Guide]";
					mes "La casa de los GMs se encuentra entremurallas,";
					mes "situada en la zona Sur-Este de la ciudad de Prontera.";
					if (.@compass_check)
						viewpoint 1,225,116,18,0x0000FF;
					break;
				case 14:
					mes "[Prontera Guide]";
					mes "Por favor pídeme que elimine";
					mes "las marcas de tu mapa si";
					mes "ya no deseas tener";
					mes "marcadas las locaciones";
					mes "en él.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,4,0x00FF00;
			viewpoint 2,237,41,5,0x0000FF;
			viewpoint 2,46,345,6,0x00FF00;
			viewpoint 2,175,220,7,0xFF0000;
			viewpoint 2,134,221,8,0xFF0000;
			viewpoint 2,204,191,9,0xFF0000;
			viewpoint 2,107,192,10,0xFF0000;
			viewpoint 2,179,184,11,0x00FF00;
			viewpoint 2,208,154,12,0x00FF00;
			viewpoint 2,120,267,13,0x00FF00;
			viewpoint 2,192,267,14,0x00FF00;
			viewpoint 2,133,183,15,0x00FF00;
			viewpoint 2,156,360,16,0x00FF00;
			viewpoint 2,75,91,17,0x00FF00;
			viewpoint 2,225,116,18,0x0000FF;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Prontera Guide]";
			mes "Avances en hechizos y";
			mes "tecnología nos han permitido";
			mes "a nosotros actualizar nuestro sistema";
			mes "de información, posibilitando la marcación";
			mes "de lugares en tu Mini-Map";
			mes "para una fácil navegación.";
			next;
			mes "[Prontera Guide]";
			mes "Tu Mini-Map está localizado";
			mes "en la esquina derecha de arriba";
			mes "de la pantalla. Si tu no puedes";
			mes "verla, presiona las teclas Ctrl + Tab";
			mes "o haz click en el boton \"Map\"  en la ventana de información básica.";
			next;
			mes "[Prontera Guide]";
			mes "En tu Mini-Map,";
			mes "haz click en los simbolos \"+\" y \"-\"";
			mes "para hacer zoom hacia dentro y";
			mes "hacia afuera en tu Mini-Map. Esperamos";
			mes "que disfrutes de su estadía";
			mes "aquí en la Ciudad de Prontera!...";
			break;
		case 4:
			mes "[Prontera Guide]";
			mes "¡Muy Bien aventurero!";
			mes "¡Te deseo seguridad en";
			mes "tus viajes a través";
			mes "de las tierras que visites!...";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

prontera,280,210,4	duplicate(PrtGuide)	East Gate-Guide	105

prontera,34,194,0	duplicate(PrtGuide)	West Gate-Guide	105

prontera,165,34,2	duplicate(PrtGuide)	South Gate-Guide	105

prontera,151,330,4	duplicate(PrtGuide)	North Gate-Guide	105
