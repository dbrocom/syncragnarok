//===== eAthena Script ======================================= 
//= Morroc Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.4a
//===== Compatible With: ===================================== 
//= eAthena  SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Morroc
//===== Additional Comments: ================================= 
//= Fully working.  Added a guide at every exit.
//= v1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Removed Duplicates [Silent]
//= 1.4 rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

morocc,153,286,6	script	Guide#moc::MocGuide	707,{
	cutin "moc_soldier",2;
	mes "[Morroc Guide]";
	mes "Bienvenido a Morroc,";
	mes "el pueblo fronterizo del";
	mes "Reino de Rune-Midgarts.";
	mes "Por favor preguntamen cualquier duda";
	mes "si estás teniendo problemas";
	mes "buscando cualquier cosa en el pueblo.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("City Guide:Remove Marks from Mini-Map:Notice:Cancel")) {
		case 1:
			mes "[Morroc Guide]";
			mes "Por favor selecciona";
			mes "una ubicación del";
			mes "siguiente menú.";
			if (.@compass_check == 0) {
				mes "¿Te gustaría que";
				mes "marcara las ubicaciones";
				mes "en tu Mini-Map?";
				next;
				if (select("Yes.:No.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Thief Guild^000000:Weapon Shop:Inn:Pub:Mercenary Guild:Forge:Cancel")) {
				case 1:
					mes "[Morroc Guide]";
					mes "La guild de Thief está";
					mes "a cargo de todos los Job";
					mes "Cambiar a la Clase Thief";
					mes "Por lo que he escuchado,";
					mes "puedes encontrarlos adentro";
					mes "de la pirámide cercana...";
					if (.@compass_check)
						viewpoint 1,24,297,2,0xFF0000;
					break;
				case 2:
					mes "[Morroc Guide]";
					mes "La tienda de Armas";
					mes "está hacia el sureste";
					mes "al final de Morroc.";
					if (.@compass_check)
						viewpoint 1,253,56,3,0xFF00FF;
					break;
				case 3:
					mes "[Morroc Guide]";
					mes "Existen residenciales";
					mes "donde puedes descansar";
					mes "al sureste y";
					mes "noreste del final de Morroc.";
					if (.@compass_check) {
						viewpoint 1,197,66,4,0xFF00FF;
						viewpoint 1,273,269,5,0xFF00FF;
					}
					break;
				case 4:
					mes "[Morroc Guide]";
					mes "Puedes encontrar el";
					mes "Pub en el noreste de Morroc.";
					if (.@compass_check)
						viewpoint 1,52,259,6,0xFF00FF;
					break;
				case 5:
					mes "[Morroc Guide]";
					mes "La guild";
					mes "de Mercenarios está ubicada";
					mes "al este de Morroc.";
					if (.@compass_check)
						viewpoint 1,284,171,7,0x00FF00;
					break;
				case 6:
					mes "[Morroc Guide]";
					mes "El herrero está";
					mes "ubicado justo";
					mes "al sureste del";
					mes "centro de Morroc.";
					if (.@compass_check)
						viewpoint 1,47,47,7,0xFF00FF;
					break;
				case 7:
					mes "[Morroc Guide]";
					mes "Por favor pídeme que \"Remueva";
					mes "las marcas de Mini-map\" si tú";
					mes "no deseas tener";
					mes "las ubicaciones marcadas";
					mes "en tu Mini-Map.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0x00FF00;
			viewpoint 2,237,41,3,0x0000FF;
			viewpoint 2,46,345,4,0x00FF00;
			viewpoint 2,175,220,5,0xFF0000;
			viewpoint 2,175,220,6,0xFF0000;
			viewpoint 2,175,220,7,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Morroc Guide]";
			mes "Avances en magia y";
			mes "tecnología nos han permitido";
			mes "actualizar nuestro sistema de información";
			mes "permitiéndonos marcar";
			mes "ubicaciones en tu Mini-Map";
			mes "para una mejor navegación.";
			next;
			mes "[Morroc Guide]";
			mes "Tu Mini-Map esta ubicado";
			mes "en el costado superior derecho";
			mes "de tu pantalla. Si no puedes";
			mes "verlo, presiona Ctrl + Tab";
			mes "o presiona el botón de Map en tu ventada de Información Básica (Ctrl + V)";
			next;
			mes "[Morroc Guide]";
			mes "En tu Mini-Map,";
			mes "cliquea sobre los símbolos \"+\" y \"-\"";
			mes "para aumentar o disminuir";
			mes "el zoom de tu Mini-Map. Nosotros";
			mes "esperamos que disfrutes tus viajes";
			mes "aquie en la ciudad de Morroc.";
			break;
		case 4:
			mes "[Morroc Guide]";
			mes "Entonces,";
			mes "intenta alejarte de";
			mes "los problemas";
			mes "allá afuera, aventurero.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "moc_soldier",255;
	end;
}

morocc,54,97,0	duplicate(MocGuide)	Guide#2moc	707
