//===== eAthena Script ========================================
//= Izlude Arena Room
//===== By: ==================================================
//= SinSloth
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Izlude Battle Arena Main Room
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Fixed a small reward typo (was giving potato.) [L0ne_W0lf]
//============================================================

arena_room,94,93,5	script	Vendigos	97,{

	mes "[Vendigos]";
	mes "Bienvenido al mundo de Arena, la batalla contra un tiempo límite.";
	mes "Mi nombre es Vendigos, estoy aqui para ayudarte.";
	next;
	mes "[Vendigos]";
	mes "Si tiene alguna pregunta, siéntase libre de preguntarme.";
L_Menu:
	next;
	switch( select( "Cómo retar","Acerca los Arena Points","Mis Arena Points","Cancelar" ) )
	{
		case 1:
			mes "[Vendigos]";
			mes "Hay dos tipos diferentes de arena mode^3131FFPlayer Mode^000000 y ^3131FFParty Mode^000000.";
			next;
			mes "[Vendigos]";
			mes "^3131FFPlayer Mode^000000 consiste en 4 diferentes escenarios basados en el nivel de peronaje de 50~80.";
			mes "Para una party de 5 miembros, pueden participar en ^3131FFParty Mode^000000.";
			next;
			mes "[Vendigos]";
			mes "Para ^3131FFcada Player Mode Stage^000000, los personajes que sean 20 niveles más alto que el nivel requerido en cada escenario no pueden entrar. Esto es para prevenir que personajes de nivel alto ocupen con anterioridad un escenario de nivel bajo.";
			next;
			mes "[Vendigos]";
			mes "Están los NPCs para Player Mode Stages en dirección las 12 en punto y en dirección contraria a la del reloj.";
			mes "Mientras estas esperando en un chat room, serás guiado automáticamente a un arena room por la orden de espera.";
			break;
		
		case 2:
			mes "[Vendigos]";
			mes "Permíteme explicarte sobre los ^3131FFArena Point^000000.";
			mes "Son puntos de recompensa para jugadores que han ^3131FFcompletado con éxito un arena stage^000000, un jugador puede tener un máximo de 30,000 puntos.";
			next;
			mes "[Vendigos]";
			mes "Aún si no completaste un escenario debido a que se acabo el tiempo u otros errores, se te dará una pequeña cantidad de arena points.";
			next;
			mes "[Vendigos]";
			mes "Respecto al uso de Arena Points,";
			mes "estamos proporcionando varios servicios como el servicio de fotos de recuerdo por un  NPC llamado Givu.";
			break;

		case 3:
			mes "[Vendigos]";
			mes "Dejeme revisar "+strcharinfo(0)+"tus arena points.";
			next;
			mes "[Vendigos]";
			mes ""+strcharinfo(0)+" tienes un total ^3131FF"+arena_point+"^000000 puntos.";
			next;
			mes "[Vendigos]";
			mes "Si desea saber como usar arena points, please refer to the 2nd menu ^3131FF'About Arena Points'^000000.";
			break;

		case 4:
			mes "[Vendigos]";
			mes "Ok, que pase un buen momento.";
			close;
	}
	next;
	mes "[Vendigos]";
	mes "Hay algo más en lo que pueda ayudarle?";
	goto L_Menu;
	
Onlinerec_50:
	mapannounce "arena_room",""+$arena_50topn$+" has renewed the top record in the Arena Time Force Battle level 50. Congratulations!",0;
	end;
	
Onlinerec_60:
	mapannounce "arena_room",""+$arena_60topn$+" has renewed the top record in the Arena Time Force Battle level 60. Congratulations!",0;
	end;
	
Onlinerec_70:
	mapannounce "arena_room",""+$arena_70topn$+" has renewed the top record in the Arena Time Force Battle level 70. Congratulations!",0;
	end;
Onlinerec_80:
	mapannounce "arena_room",""+$arena_80topn$+" has renewed the top record in the Arena Time Force Battle level 80. Congratulations!",0;
	end;
	
Onlinerec_pt:
	mapannounce "arena_room","Party "+$arena_pttopn$+" has renewed the top record in the Arena Time Force Battle. Congratulations!",0;
	end;

//OnCommand: "linerec_vs"
// var vs_winner = GetGlobalStr "040611_arn_vstopn"
// broadcastinmap "Party "+vs_winner+" has renewed the top record in the Arena Time Force Battle. Congratulations!"
// end;

}

arena_room,105,82,5	script	Arena Record Staff	57,{

	mes "[Owen Kheuv]";
	mes "Hola, mi nombre es Own Kheuv";
	mes "estoy a cargo de los registros de todos los arena stage.";
	next;
	mes "[Owen Kheuv]";
	mes "¿Te gustaría revisar el registro de los mejores jugadores de cada escenario?";
	mes "Si es así, elige del menú de abajo.";
	next;
	switch( select( "Escenario lvl 50","Escenario lvl 60","Escenario lvl 70","Escenariolvl 80","Escenario Party" ) )
	{
		case 1:
			mes "[Owen Kheuv]";
			mes "Actualmente el mejor jugador de la arena del escenario lvl 50 es ^3131FF "+$arena_50topn$+"^000000, el mejor es ^3131FF"+$top_50min+"^000000 minutos ^3131FF"+$top_50sec+"^000000 segundos.";
			close;

		case 2:
			mes "[Owen Kheuv]";
			mes "Actualmente el mejor jugador de la arena del escenario lvl 60 es ^3131FF"+$arena_60topn$+"^000000, el mejor es ^3131FF"+$top_60min+"^000000 minutos ^3131FF"+$top_60sec+"^000000 segundos.";
			close;

		case 3:
			mes "[Owen Kheuv]";
			mes "Actualmente el mejor jugador de la arena del escenario lvl 70 es ^3131FF"+$arena_70topn$+"^000000, el mejor es ^3131FF"+$top_70min+"^000000 minutos ^3131FF"+$top_70sec+"^000000 segundos.";
			close;

		case 4:
			mes "[Owen Kheuv]";
			mes "Actualmente el mejor jugador de la arena del escenario lvl 80 es ^3131FF"+$arena_80topn$+"^000000, el mejor es ^3131FF"+$top_80min+"^000000 minutos ^3131FF"+$top_80sec+"^000000 segundos.";
			close;

		case 5:
			mes "[Owen Kheuv]";
			mes "Actualmente la mejor party es ^3131FF"+$arena_pttopn$+"^000000, el mejor es ^3131FF"+$top_ptmin+"^000000 minutos ^3131FF"+$top_ptsec+"^000000 segundos.";
			close;
	}
}

arena_room,68,135,3	script	Helper Pat	726,{

	mes "[Pat]";
	mes "Bienvenido, bienvenido.";
	mes "Soy un ayudante del escenario de nivel 50 de la arena.";
	next;
	mes "[Pat]";
	mes "Este ^3131FFescenario de nivel 50 de la arena^000000";
	mes "es accesible a personajes desde ^FF0000nivel 50^000000 hasta ^FF0000nivel 69^000000.";
	next;
	mes "[Pat]";
	mes "Este límite de nivel es para prevenir que personajes de nivel alto abusen de escenarios de nivel bajo. Espero que entiendas.";
	mes "Además aceptamos un pago de entrada de, 1,000 zeny.";
	next;
	mes "[Pat]";
	mes "Permiteme darte a conocer las reglas de juego de la arena.";
	next;
	mes "[Pat]";
	mes "^3131FFEspera en un chat room a que llegue tu turno.^000000";
	mes "Cuando sea tu turno, automáticamente serás enviado a un mapa de la arena.";
	next;
	mes "[Pat]";
	mes "Al entrar inmediatamente se activará un contador para checar tu tiempo.";
	mes "Por favor haz lo que ^3131FFHeel and Toe^000000 te aconseja. ";
	next;
	mes "[Pat]";
	mes "Tienes unos ^3131FF5 minutos^000000 de batalla.";
	next;
	mes "[Pat]";
	mes "Una vez que completes todos los cuartos incluyendo un boss room, serás enviado otra vez a un ^3131FFending waiting room^000000.";
	mes "Sólo se permite estar en el waiting room ^3131FF1 minuto^000000, por favor apresurate a recibir los arena points y sal.";
	next;
	mes "[Pat]";
	mes "Si te quedas más de 1 minuto dentro del waiting room, serás sacado y no recibirás ningun arena points. Por favor recuerda eso.";
	next;
	mes "[Pat]";
	mes "Espero que te la pases bien.";
	close;
}

arena_room,41,93,5	script	Helper Ben	750,{

	mes "[Ben]";
	mes "Bienvenido, bienvenido.";
	mes "Soy un ayudante del escenario de nivel 60 de la arena.";
	next;
	mes "[Ben]";
	mes "Este ^3131FFescenario de nivel 60 de la arena^000000";
	mes "es accesible a personajes desde ^FF0000nivel 60^000000 hasta ^FF0000nivel 79^000000.";
	next;
	mes "[Ben]";
	mes "Este límite de nivel es para prevenir que personajes de nivel alto abusen de escenarios de nivel bajo. Espero que entiendas.";
	mes "Además aceptamos un pago de entrada de, 1,000 zeny.";
	next;
	mes "[Ben]";
	mes "Permiteme darte a conocer las reglas de juego de la arena.";
	next;
	mes "[Ben]";
	mes "^3131FFEspera en un chat room a que llegue tu turno.^000000";
	mes "Cuando sea tu turno, automáticamente serás enviado a un mapa de la arena.";
	next;
	mes "[Ben]";
	mes "Al entrar inmediatamente se activará un contador para checar tu tiempo.";
	mes "Por favor haz lo que ^3131FFMinilover^000000 te aconseja. ";
	next;
	mes "[Ben]";
	mes "Tienes unos ^3131FF6 minutos^000000 de batalla.";
	next;
	mes "[Ben]";
	mes "Una vez que completes todos los cuartos incluyendo un boss room, serás enviado otra vez a un ^3131FFending waiting room^000000.";
	mes "Sólo se permite estar en el waiting room ^3131FF1 minuto^000000, por favor apresurate a recibir los arena points y sal.";
	next;
	mes "[Ben]";
	mes "Si te quedas más de 1 minuto dentro del waiting room, serás sacado y no recibirás ningun arena points. Por favor recuerda eso.";
	next;
	mes "[Ben]";
	mes "Espero que te la pases bien.";
	close;
}

arena_room,53,49,5	script	Helper Vicious	730,{

	mes "[Vicious]";
	mes "Hey tu..";
	mes "Mi nombre es Vicious, soy un ayudante del escenadio de nivel 70 de la arena.";
	mes "(...No tengo ni idea de como diablos me metí en este asqueroso trabajo...mumble mumble...grumble grumble..)";
	next;
	mes "[Vicious]";
	mes "Este ^3131FFescenario de nivel 70 de la arena^000000";
	mes "es accesible a personajes desde ^FF0000nivel 70^000000 hasta ^FF0000nivel 89^000000.";
	next;
	mes "[Vicious]";
	mes "Este límite de nivel es para prevenir que personajes de nivel alto abusen de escenarios de nivel bajo. Espero que entiendas.";
	mes "Además aceptamos un pago de entrada de, 1,000 zeny.";
	next;
	mes "[Vicious]";
	mes "Permiteme darte a conocer las reglas de juego de la arena.";
	next;
	mes "[Vicious]";
	mes "^3131FFEspera en un chat room a que llegue tu turno.^000000";
	mes "Cuando sea tu turno, automáticamente serás enviado a un mapa de la arena.";
	next;
	mes "[Vicious]";
	mes "Al entrar inmediatamente se activará un contador para checar tu tiempo.";
	mes "Por favor haz lo que ^3131FFCadilac^000000 te aconseja. ";
	next;
	mes "[Vicious]";
	mes "Tienes unos ^3131FF7 minutos^000000 de batalla.";
	next;
	mes "[Vicious]";
	mes "Una vez que completes todos los cuartos incluyendo un boss room, serás enviado otra vez a un ^3131FFending waiting room^000000.";
	mes "Sólo se permite estar en el waiting room ^3131FF1 minuto^000000, por favor apresurate a recibir los arena points y sal.";
	next;
	mes "[Vicious]";
	mes "Si te quedas más de 1 minuto dentro del waiting room, serás sacado y no recibirás ningun arena points. Por favor recuerda eso.";
	next;
	mes "[Vicious]";
	mes "Okay, cuidate.";
	close;
}

arena_room,147,49,3	script	Helper Epin	727,{

	mes "[Epin]";
	mes "Adiós.";
	mes "Soy un ayudante del escenario de nivel 80 y mi nombre es Epin.";
	mes "Me gusta leer y tengo 19 años...huh...? ¿No quieres escucharme...?";
	next;
	emotion e_cry,0;
	mes "[Epin]";
	mes "Está bien...";
	next;
	mes "[Epin]";
	mes "Este ^3131FFescenario de nivel 80 de la arena^000000";
	mes "es accesible a personajes desde ^FF0000nivel 80^000000 hasta ^FF0000nivel 99^000000.";
	next;
	mes "[Epin]";
	mes "Este límite de nivel es para prevenir que personajes de nivel alto abusen de escenarios de nivel bajo. Espero que entiendas.";
	mes "Además aceptamos un pago de entrada de, 1,000 zeny.";
	next;
	mes "[Epin]";
	mes "Permiteme darte a conocer las reglas de juego de la arena.";
	next;
	mes "[Epin]";
	mes "^3131FFEspera en un chat room a que llegue tu turno.^000000";
	mes "Cuando sea tu turno, automáticamente serás enviado a un mapa de la arena.";
	next;
	mes "[Epin]";
	mes "Al entrar inmediatamente se activará un contador para checar tu tiempo.";
	mes "Por favor haz lo que ^3131FFActus^000000 te aconseja. ";
	next;
	mes "[Epin]";
	mes "Tienes unos ^3131FF8 minutos^000000 de batalla.";
	next;
	mes "[Epin]";
	mes "Una vez que completes todos los cuartos incluyendo un boss room, serás enviado otra vez a un ^3131FFending waiting room^000000.";
	mes "Sólo se permite estar en el waiting room ^3131FF1 minuto^000000, por favor apresurate a recibir los arena points y sal.";
	next;
	mes "[Epin]";
	mes "Si te quedas más de 1 minuto dentro del waiting room, serás sacado y no recibirás ningun arena points. Por favor recuerda eso.";
	next;
	mes "[Epin]";
	mes "Espero que te la pases bien.";
	close;
}

arena_room,158,93,5	script	Helper Lunic	828,{

	mes "[Lunic]";
	mes "Bienvenido al escenario de la party arena.";
	mes "Espero que escuches con atención mi presentación.";
	mes "ya que este escenario de la party arena es un poco diferente de los escenarios de player mode.";
	next;
	mes "[Lunic]";
	mes "^3131FFParty Arena Stage^000000";
	mes "es accesible a personajes desde ^FF0000nivel 10^000000 hasta ^FF0000nivel 99^000000.";
	next;
	mes "[Lunic]";
	mes "Además, la arena no comenzará hasta que los 5 miembros de la party entren al cuarto. Aceptamos un pago de entrada de 1,000 zeny.";
	next;
	mes "[Lunic]";
	mes "Permiteme darte a conocer las reglas de juego de la arena.";
	next;
	mes "[Lunic]";
	mes "^3131FFHaz una party con tus amigos y espera en un chat room^000000.";
	mes "Antes debes formar una party. Si no lo haces, todavía puedes jugar pero podrías tener un monton de inconvenientes.";
	next;
	mes "[Lunic]";
	mes "También recuerda que 5 jugadores serán enviados automáticamente al arena room en el orden en que ^FF0000entraron al chat room^000000 no en el orden de la ^FF0000party^000000. Por favor recuerda eso.";
	next;
	mes "[Lunic]";
	mes "Y por favor haz una party de ^3131FF5 jugadores^000000 antes de entrar al chat room.";
	next;
	mes "[Lunic]";
	mes "Cuando llegue tu turno, serás enviado a un pequeño mapa en donde hay un warp y un NPC de ayuda.";
	mes "En caso de que entres con miembros de una party diferente, usa el warp salir a otro mapa. Entonces regresarás a el waiting room.";
	next;
	mes "[Lunic]";
	mes "En caso de que entres con los miembros de tu party, por favor procede a hablar con el NPC ayuda en el mapa pequeño.";
	mes "El NPC de ayuda sólo habla con ^FF0000 1 jugador ^000000a la vez.";
	next;
	mes "[Lunic]";
	mes "El NPC guiará a ti y a los miembros de tu party al arena room real.";
	mes "Sin embargo, si cualquiera en la party ^3131FFno tiene suficiente dinero para pagar el pago de entrada, será enviado afuera. ^000000.";
	next;
	mes "[Lunic]";
	mes "Además sólo puedes estar dentro del mapa por ^FF0000 1 minuto^000000.";
	next;
	mes "[Lunic]";
	mes "Al entrar inmediatamente se activará un contador para checar tu tiempo de batalla.";
	mes "Por favor haz lo que ^3131FFSlipslowrun^000000 te aconseja. ";
	next;
	mes "[Lunic]";
	mes "Tendrás ^3131FF10 minutos^000000 para completar el escenario sin embargo no es tan fácil de hacer. Y te aconsejo usar el tiempo sabiamente.";
	next;
	mes "[Lunic]";
	mes "Una vez que completes todos los cuartos incluyendo un boss room, serás enviado otra vez a un ^3131FFending waiting room^000000.";
	mes "Sólo se permite estar en el waiting room ^3131FF1 minuto^000000, por favor apresurate a recibir los arena points y sal.";
	next;
	mes "[Lunic]";
	mes "Si te quedas más de 1 minuto dentro del waiting room, serás sacado y no recibirás ningun arena points. Por favor recuerda eso.";
	next;
	mes "[Lunic]";
	mes "Además, si una party hace un nuevo registro a tiempo para completar el escenario,";
	mes "^3131FFel party master^000000 puede grabar ^3131FFel nombre de la party^000000.";
	next;
	mes "[Lunic]";
	mes "En este caso, si una party tiene más de un líder o ninguno, es imposible escrbibir ^FF0000el registro de la mejor party ^000000.";
	mes "Por lo tanto, se suguiere fuertemente formar una party antes de entrar al mapa de la arena.";
	next;
	mes "[Lunic]";
	mes "Gracias por escuchar y";
	mes "espero que te la pases bien.";
	close;
}

arena_room,158,82,1	script	Helper Lonik	828,{

	emotion e_no1,0;
	mes "[Lonik]";
	mes "Tah dah! ¡Aquí estoy!";
	next;
	mes "[Lonik]";
	mes "Tienes curiosidad si hay alguien dentro or no, ¿verdad?";
	next;
	mes "[Lonik]";
	mes "Mi respuesta es..";
	mes ".............";
	if(getmapusers("force_1-2"))
	{
		emotion e_oh,0;
		mes "¿Si!";
		mes "Hay alguien dentro.";
		next;
		mes "[Lonik]";
		mes "¿Mejor esperas un poco más!";
	}
	else
	{
		emotion e_x,0;
		mes "¡No!";
		mes "Ve por el, ¡Buena Suerte!";
	}
	close;
}

sec_in02,72,180,3	script	Arena Manager#arena	802,{

	input .@arena;
	if(!.@arena)
	{
		mes "[Arena Manager]";
		mes "La orden ha sido cancelado";
		close;
	}
	else if(.@arena == 1357)
	{
		mes "[Arena Manager]";
		mes "Selecciona una opción.";
		next;
		switch( select( "Reiniciar la arena","Cambiar el Tiempo de Clasificación" ) )
		{
			case 1:
				switch( select( "nivel 50","nivel 60","nivel 70","nivel 80","Party Mode" ) ) 
				{
					case 1:
						set .@arena,50;
						break;
					case 2:
						set .@arena,60;
						break;
					case 3:
						set .@arena,70;
						break;
					case 4:
						set .@arena,80;
						break;
					case 5:
						mes "[Arena Manager]";
						mes "== Peligro ==";
						mes "Has elegido reiniciar el escenario de party arena.";
						mes "¿Deseas continuar?";
						next;
						switch( select( "Si","No" ) )
						{
							case 1:
								donpcevent "Ponox::Onstart";
								mes "[Arena Manager]";
								mes "El escenario de la arena ha sido reactivado exitosamente.";
								close;

							case 2:
								mes "[Arena Manager]";
								mes "La orden ha sido cancelada.";
								close;
						}
				}
				mes "[Arena Manager]";
				mes "== Peligro ==";
				mes "Has elegido reiniciar el nivel "+.@arena+" y el escenario de la arena.";
				mes "¿Deseas continuar?";
				next;
				switch( select( "Si","No" ) )
				{
					case 1:
						donpcevent "lvl "+.@arena+"s Waiting Room::Onstart";
						mes "[Arena Manager]";
						mes "El escenario de la arena ha sido reactivado exitosamente.";
						close;
					
					case 2:
						mes "[Arena Manager]";
						mes "La orden ha sido cancelada.";
						close;
				}
				
			case 2:
				mes "[Arena Manager]";
				mes "== Peligro ==";
				mes "^CE0000Has elegido cambiar el tiempo de clasificación. ¡Asegurate de que esta no sea una mala decisión a menos que algo serio haya pasado!^000000";
				next;
				mes "[Arena Manager]";
				mes "¿Estas seguro que quieres cambiar el tiempo de clasificación?";
				next;
				switch( select( "No","Si" ) )
				{
					case 1:
						mes "[Arena Manager]";
						mes "Phew~ :)";
						close;

					case 2:
						switch( select( "nivel 50","nivel 60","nivel 70","nivel 80","Party Mode" ) )
						{
							case 1:
								mes "[Arena Manager]";
								mes "Por favor pon 0 para cancelar.";
								mes "Si no, por favor escribe entre 400 letras.";
								next;
								input .@arenamin;
								set $top_50min,.@arenamin;
								input .@arenasec;
								set $top_50sec,.@arenasec;
								mes "[Arena Manager]";
								mes "El tiempo de juego actual de los participantes del nivel 50 ha sido cambiado a ^FF0000"+$top_50min+"^000000 minutos y ^FF0000"+$top_50sec+"^000000 segundos.";
								close;
							case 2:
								mes "[Arena Manager]";
								mes "Por favor pon 0 para cancelar.";
								mes "Si no, por favor escribe entre 400 letras.";
								next;
								input .@arenamin;
								set $top_60min,.@arenamin;
								input .@arenasec;
								set $top_60sec,.@arenasec;
								mes "[Arena Manager]";
								mes "El tiempo de juego actual de los participantes del nivel 60 ha sido cambiado a ^FF0000"+$top_60min+"^000000 minutos y ^FF0000"+$top_60sec+"^000000 segundos.";
								close;
							case 3:
								mes "[Arena Manager]";
								mes "Por favor pon 0 para cancelar.";
								mes "Si no, por favor escribe entre 400 letras.";
								next;
								input .@arenamin;
								set $top_70min,.@arenamin;
								input .@arenasec;
								set $top_70sec,.@arenasec;
								mes "[Arena Manager]";
								mes "El tiempo de juego actual de los participantes del nivel 70 ha sido cambiado a ^FF0000"+$top_70min+"^000000 minutos y ^FF0000"+$top_70sec+"^000000 segundos.";
								close;
							case 4:
								mes "[Arena Manager]";
								mes "Por favor pon 0 para cancelar.";
								mes "Si no, por favor escribe entre 400 letras.";
								next;
								input .@arenamin;
								set $top_80min,.@arenamin;
								input .@arenasec;
								set $top_80sec,.@arenasec;
								mes "[Arena Manager]";
								mes "El tiempo de juego actual de los participantes del nivel 80 ha sido cambiado a ^FF0000"+$top_80min+"^000000 minutos y ^FF0000"+$top_80sec+"^000000 segundos.";
								close;
							case 5:
								mes "[Arena Manager]";
								mes "Por favor pon 0 para cancelar.";
								mes "Si no, por favor escribe entre 400 letras.";
								next;
								input .@arenamin;
								set $top_ptmin,.@arenamin;
								input .@arenasec;
								set $top_ptsec,.@arenasec;
								mes "[Arena Manager]";
								mes "El tiempo de juego actual de los participantes en party ha sido cambiado a^FF0000"+$top_ptmin+"^000000 minutos y ^FF0000"+$top_ptsec+"^000000 segundos.";
								close;
						}
				}
		}
	}
	mes "El password es incorrecto.";
	close;
}

sec_in02,79,171,3	script	Reward Manager#arena	802,{

	input .@arena;
	if(!.@arena)
	{
		mes "[Reward Manager]";
		mes "La orden ha sido cancelada.";
		close;
	}
	else if(.@arena == 1357)
	{
		mes "[Reward Manager]";
		mes "Has elegido esconder el NPC teletransportador.";
		next;
		switch( select( "Cancelar","Si","Encender" ) )
		{
			case 1:
				mes "[Reward Manager]";
				mes "Has cancelado la orden";
				close;
		
			case 2:
				mes "[Reward Manager]";
				mes "NPC ha sido escondido";
				disablenpc "Teleporter#arena";
				close;
				
			case 3:
				mes "[Reward Manager]";
				mes "NPC ha sido habilitado";
				enablenpc "Teleporter#arena";
				close;
		}
	}
	mes "[Reward Manager]";
	mes "El password es incorrecto";
	close;
}

arena_room,105,93,5	script	Teleporter#arena	55,{

	mes "[Teleporter]";
	mes "¡Yo puedo llevarte a la arena de recompensa!";
	mes "¿Te gustaría ir ahi?";
	next;
	switch( select( "Si.","No." ) )
	{
		case 1:
			mes "[Teleporter]";
			mes "Permíteme guiarte.";
			close2;
			warp "prt_are_in",60,14;
			end;

		case 2:
			mes "[Teleporter]";
			mes "No hay problema, sientete libre de regresar cuando quieras.";
			close;
	}
}

prt_are_in,98,14,3	script	Givu#arena	728,{


	if(checkweight(526,5) != 1)
	{
		mes "- ¡Espera un momento! -";
		mes "- Actualmente estas llevando -";
		mes "- demasiados items contigo. -";
		mes "- Por favor aligera tu peso -";
		mes "- e intenta otra vez. -";
		close;
	}
	mes "[Givu]";
	mes "Hola. Bienvenido al mundo de la Arena.";
	mes "Mi nombre es Givu, yo estoy a cargo del programa de intercambio de arena points.";
	next;
	mes "[Givu]";
	mes "Puedes intercambiar tus arena points por varias cosas.";
	mes "Por favor elige un menú.";
	next;
	switch( select( "Cambiar por items Consumibles","Cambiar por puntos de EXP","Tomar una Foto de Recuerdo","Revisar la cantidad actual de Arena Points" ) )
	{
		case 1:
			mes "[Givu]";
			mes "¿Qué items consumibles deseas cambiar?";
			next;
			switch( select( "Honey","Royal Jelly","Mastela Fruit","Condensed White Potion","Anodyne","Yggdrasil Seed","Yggdrasilberry","Old Blue Box","Old Purple Box","Old Card Album" ) )
			{
				case 1: callfunc "Func_Are_Rew",518,5,20;
				case 2: callfunc "Func_Are_Rew",526,5,30;
				case 3: callfunc "Func_Are_Rew",522,5,30;
				case 4: callfunc "Func_Are_Rew",547,8,30;
				case 5: callfunc "Func_Are_Rew",605,3,20;
				case 6: callfunc "Func_Are_Rew",608,1,20;
				case 7: callfunc "Func_Are_Rew",607,1,40;
				case 8: callfunc "Func_Are_Rew",603,1,100;
				case 9: callfunc "Func_Are_Rew",617,1,300;
				case 10: callfunc "Func_Are_Rew",616,1,1000;
			}
			
		case 2:
			mes "[Givu]";
			mes "¿Te gustaría cambiar tus arenas points por experience points?";
			mes "Se requieren 40 arena points.";
			next;
			switch( select( "Cancelar","Si" ) )
			{
				case 1:
					mes "[Givu]";
					mes "Has cancelado tu petición.";
					close;
				
				case 2:
					if(arena_point < 40)
					{
						mes "[Givu]";
						mes "No tiene suficientes arena points.";
						mes "Por favor revisa la cantidad total de arena points que tienes.";
						close;
					}
					set arena_point,arena_point -40;
					if(BaseLevel < 70) getexp 3000,0;
					else if(BaseLevel < 80) getexp 9000,0;
					else if(BaseLevel < 90) getexp 10000,0;
					else if(BaseLevel < 100) getexp 30000,0;
					mes "[Givu]";
					mes "Has ganado puntos de experiencia. Gracias.";
					close;
			}

		case 3:
			mes "[Givu]";
			mes "Has elegido el servicio de fotos de recuerdo.";
			next;
			switch( select( "Acerca del Servicio de Fotos de recuerdo.ices","Tomar una foto" ) )
			{
				case 1:
					mes "[Givu]";
					mes "¿Ves las escaleras a mi derecha?";
					next;
					mes "[Givu]";
					mes "En las escaleras, te puedes tomar una screenshot con un NPC o con un monstruo.";
					next;
					mes "[Givu]";
					mes "Cuando elijas un NPC o un monstruo, este mostrará algunos emoticones ^FF0000por 1 minuto^000000.";
					next;
					mes "[Givu]";
					mes "¡No pierdas la oportunidad de tomarte una foto con tu NPC favorito!";
					close;

				case 2:
					if($arenapic)
					{
						mes "[Givu]";
						mes "El servicio de fotos de recuerdo está en proceso. Por favor espera.";
						close;
					}
					mes "[Givu]";
					mes "Por favor elige un NPC.";
					mes "El NPC aparecerá por 1 minute y este servicio requiere 10 arena points.";
					next;
					switch( select( "Baphomet","Dark Lord","Doppelganger","Eddga","Dracula","Samurai","Stormy Knight","Phreeoni","Girl","Valkyrie" ) )
					{
						case 1: set $@arenapic,1039; break;
						case 2: set $@arenapic,1272; break;
						case 3: set $@arenapic,1046; break;
						case 4: set $@arenapic,1115; break;
						case 5: set $@arenapic,1389; break;
						case 6: set $@arenapic,1492; break;
						case 7: set $@arenapic,1251; break;
						case 8: set $@arenapic,1159; break;
						case 9: set $@arenapic,6969; break;
						case 10: set $@arenapic,7777; break;
					}
					if(arena_point < 10)
					{
						mes "[Givu]";
						mes "No tiene suficientes arena points.";
						mes "Por favor revisa la cantidad total de arena points que tienes.";
						close;
					}
					mes "[Givu]";
					if($@arenapic == 6969) mes "¿Te gustaría tomarte una foto con chicas hermosas?";
					else if($@arenapic == 7777) mes "¿Te gustaría tomarte una foto con una Valkyrie?";
					else mes "Te gustaría tomarte una foto con una "+getmonsterinfo($@arenapic,0)+"?";
					next;
					switch( select( "Si","No" ) )
					{
						case 1:
							set arena_point,arena_point -10;
							set $arenapic,1;
							enablenpc "#arena_"+$@arenapic+"";
							movenpc "#arena_"+$@arenapic+"",96,28;
							if($@arenapic == 6969)
							{
								enablenpc "#arena_ss_2";
								enablenpc "#arena_ss_3";
								enablenpc "#arena_ss_4";
							}
							donpcevent "npctime#arena::OnStart";
							mes "[Givu]";
							mes "Gracias.";
							close;

						case 2:
							mes "[Givu]";
							mes "¿Te gustaría pensarlo un poco más?";
							close;
					}
			}
			
		case 4:
			mes "[Givu]";
			mes "Permíteme revisar "+strcharinfo(0)+" tus arena points.";
			next;
			mes "[Givu]";
			mes ""+strcharinfo(0)+" tienes un total ^3131FF"+arena_point+"^000000 puntos.";
			close;
	}

OnInit:
	set $arenapic,0;
	end;
}

prt_are_in,1,1,5	script	#arena_1039	1039,{
	end;
	
OnInit:
	disablenpc "#arena_6969";
	disablenpc "#arena_ss_2";
	disablenpc "#arena_ss_3";
	disablenpc "#arena_ss_4";
	disablenpc "#arena_1272";
	disablenpc "#arena_1046";
	disablenpc "#arena_1115";
	disablenpc "#arena_1389";
	disablenpc "#arena_1492";
	disablenpc "#arena_1251";
	disablenpc "#arena_1159";
	disablenpc "#arena_7777";
	disablenpc "#arena_1039";
	end;

OnHeart:
	emotion e_lv,0;
	end;
OnSci:
	emotion e_scissors,0;
	end;
OnBest:
	emotion e_no1,0;
	end;
OnOmg:
	emotion e_omg,0;
	end;
OnKik:
	emotion e_gg,0;
	end;
OnKis:
	emotion e_kis,0;
	end;
}

prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_6969	714
prt_are_in,94,26,5	duplicate(#arena_1039)	#arena_ss_2	714
prt_are_in,100,28,3	duplicate(#arena_1039)	#arena_ss_3	714
prt_are_in,102,26,3	duplicate(#arena_1039)	#arena_ss_4	714
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1272	1272
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1046	1046
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1115	1115
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1389	1389
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1492	1492
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1251	1251
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1159	1159
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_7777	811

prt_are_in,130,30,3	script	npctime#arena	139,{

OnStop:
	stopnpctimer;
	end;
	
OnStart:
	initnpctimer;
	end;
	
OnTimer2000:
	mapannounce "prt_are_in","Givu : You should keep time with popping emotion icons~",1,0xFFCE00;
	end;
	
OnTimer5000:
	mapannounce "prt_are_in","Emoticon : /lv",1,0xFFCE00;
	end;
	
OnTimer7000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer8000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer9000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer10000:
	mapannounce "prt_are_in"," ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnHeart";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnHeart";
		donpcevent "#arena_ss_3::OnHeart";
		donpcevent "#arena_ss_4::OnHeart";
	}
	end;
	
OnTimer15000:
	mapannounce "prt_are_in","Emoticon : /gawi",1,0xFFCE00;
	end;
	
OnTimer17000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer18000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer19000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer20000:
	mapannounce "prt_are_in"," ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnSci";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnSci";
		donpcevent "#arena_ss_3::OnSci";
		donpcevent "#arena_ss_4::OnSci";
	}
	end;
	
OnTimer25000:
	mapannounce "prt_are_in","Emoticon : /no1",1,0xFFCE00;
	end;
	
OnTimer27000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer28000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer29000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer30000:
	mapannounce "prt_are_in"," ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnBest";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnBest";
		donpcevent "#arena_ss_3::OnBest";
		donpcevent "#arena_ss_4::OnBest";
	}
	end;
	
OnTimer35000:
	mapannounce "prt_are_in","Emoticon : /huk",1,0xFFCE00;
	end;
	
OnTimer37000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer38000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer39000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer40000:
	mapannounce "prt_are_in"," ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnOmg";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnOmg";
		donpcevent "#arena_ss_3::OnOmg";
		donpcevent "#arena_ss_4::OnOmg";
	}
	end;
	
OnTimer45000:
	mapannounce "prt_are_in","Emoticon : /gg",1,0xFFCE00;
	end;
	
OnTimer47000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer48000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer49000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer50000:
	mapannounce "prt_are_in"," ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnKik";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnKik";
		donpcevent "#arena_ss_3::OnKik";
		donpcevent "#arena_ss_4::OnKik";
	}
	end;
	
OnTimer55000:
	mapannounce "prt_are_in","Emoticon : /kis",1,0xFFCE00;
	end;
	
OnTimer57000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;

OnTimer58000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer59000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer60000:
	mapannounce "prt_are_in"," ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnKis";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnKis";
		donpcevent "#arena_ss_3::OnKis";
		donpcevent "#arena_ss_4::OnKis";
	}
	end;
	
OnTimer62000:
	mapannounce "prt_are_in","Time is over. Thank you for using my services.",1,0xFFCE00;
	movenpc "#arena_"+$@arenapic+"",1,1;
	donpcevent "#arena_1039::OnInit";
	set $arenapic,0;
	stopnpctimer;
	end;
}

sec_in02,72,171,3	script	Picture Manager#arena	802,{

	input .@arena;
	if(!.@arena)
	{
		mes "[Picture Manager]";
		mes "La orden ha sido cancelada.";
		close;
	}
	else if(.@arena == 1357)
	{
		mes "[Picture Manager]";
		mes "¿Te gustaría poner la variable de Picture Reward en 0?";
		next;
		switch( select( "Cancelar","Si" ) )
		{
			case 1:
				mes "[Picture Manager]";
				mes "Has cancelado tu petición.";
				close;

			case 2:
				mes "[Picture Manager]";
				mes "Picture rewarding reajustado.";
				set $arenapic,0;
				close;
		}
	}
	mes "[Picture Manager]";
	mes "El password es incorrecto.";
	close;
}

sec_in02,76,176,5	script	Live Broadcast#arena	802,{

	mes "[Live Broadcast]";
	mes "Actuamente hay "+getmapusers("force_1-1")+" personas en el mapa de nivel 50.";
	mes "Actualmente hay "+getmapusers("force_2-1")+" personas en el mapa de nivel 60.";
	mes "Actualmente hay "+getmapusers("force_3-1")+" personas en el mapa de nivel 70.";
	mes "Actualmente hay "+getmapusers("force_4-1")+" personas en el mapa de nivel 80.";
	mes "Actualmente hay "+getmapusers("force_1-2")+" personas en el mapa de party";
	mes "Actualmente hay "+getmapusers("arena_room")+" personas en el waiting room.";
	close;
}

function	script	Func_Are_Rew	{
				
	mes "[Givu]";
	mes "Te gustaría cambiar tus arenas points por "+getitemname(getarg(0))+"?";
	mes "Puedes cambiar ^3131FF"+getarg(2)+" arena points por "+getarg(1)+" "+getitemname(getarg(0))+"^000000.";
	mes "Si deseas cancelar, por favor pon 0. Si no, por favor pon cuantas ^3131FFveces^000000 deseas gastar arena points.";
	next;
	input .@reward;
	if(!.@reward)
	{
		mes "[Givu]";
		mes "Has cancelado tu petición.";
		close;
	
	}
	else if(.@reward > 1500)
	{
		mes "[Givu]";
		mes "Has excedido la capacidad máxima.";
		close;
	}
	if(arena_point < .@reward * getarg(2))
	{
		mes "[Givu]";
		mes "No tienes suficientes arena points.";
		mes "Por favor revisa la cantidad total de arena porint que tienes.";
		close;
	}
	set arena_point,arena_point - (.@reward * getarg(2));
	getitem getarg(0),.@reward * getarg(1);
	mes "[Givu]";
	mes "Gracias, por favor vuelva pronto.";
	close;
}

arena_room,63,126,0	script	LevelCheck#50	139,5,5,{

OnTouch:
	if(BaseLevel < 50 || BaseLevel > 69)
	{
		mes "[Helper Pat]";
		mes "Lo siento pero ese lugar";
		mes "sólo está disponible para personas de";
		mes "nivel ^FF000050^000000 a ^FF000069^000000. Me temo que";
		mes "tienes que irte inmediatamente.";
		mes "Por favor procede a la arena";
		mes "apropiada. ¡Que te diviertas!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Helper Pat]";
		mes "¡Oh! ¡Eso es muy malo!";
		mes "Tienes el nivel requerido";
		mes "para probar esta arena pero";
		mes "cuesta 1000 zeny entrar.";
		mes "¡Por favor regresa cuando";
		mes "los tengas!";
		close2;
		warp "arena_room",100,75;
	}
	end;
}

arena_room,49,87,0	script	LevelCheck#60	139,5,5,{

OnTouch:
	if(BaseLevel < 60 || BaseLevel > 79)
	{
		mes "[Helper Ben]";
		mes "Lo siento pero ese lugar";
		mes "sólo está disponible para personas de";
		mes "nivel ^FF000060^000000 a ^FF000079^000000. Me temo que";
		mes "tienes que irte inmediatamente.";
		mes "Por favor procede a la arena";
		mes "apropiada. ¡Que te diviertas!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Helper Ben]";
		mes "¡Oh! ¡Eso es muy malo!";
		mes "Tienes el nivel requerido";
		mes "para probar esta arena pero";
		mes "cuesta 1000 zeny entrar.";
		mes "¡Por favor regresa cuando";
		mes "los tengas!";
		close2;
		warp "arena_room",100,75;
	}
	end;
}

arena_room,63,53,0	script	LevelCheck#70	139,5,5,{

OnTouch:
	if(BaseLevel < 70 || BaseLevel > 89)
	{
		mes "[Helper Vicious]";
		mes "Lo siento pero ese lugar";
		mes "sólo está disponible para personas de";
		mes "nivel ^FF000070^000000 a ^FF000089^000000. Me temo que";
		mes "tienes que irte inmediatamente.";
		mes "Por favor procede a la arena";
		mes "apropiada. ¡Que te diviertas!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Helper Vicious]";
		mes "¡Oh! ¡Eso es muy malo!";
		mes "Tienes el nivel requerido";
		mes "para probar esta arena pero";
		mes "cuesta 1000 zeny entrar.";
		mes "¡Por favor regresa cuando";
		mes "los tengas!";
		close2;
		warp "arena_room",100,75;
	}
	end;
}

arena_room,136,53,0	script	LevelCheck#80	139,5,5,{

OnTouch:
	if(BaseLevel < 80)
	{
		mes "[Helper Epin]";
		mes "Lo siento pero ese lugar";
		mes "sólo está disponible para personas de";
		mes "nivel ^FF000080^000000 a ^FF000099^000000. Me temo que";
		mes "tienes que irte inmediatamente.";
		mes "Por favor procede a la arena";
		mes "apropiada. ¡Que te diviertas!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Helper Epin]";
		mes "¡Oh! ¡Eso es muy malo!";
		mes "Tienes el nivel requerido";
		mes "para probar esta arena pero";
		mes "cuesta 1000 zeny entrar.";
		mes "¡Por favor regresa cuando";
		mes "los tengas!";
		close2;
		warp "arena_room",100,75;
	}
	end;

}
