//===== eAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

payon,173,141,4	script	Ripped Cabus#GymPass	899,{
	mes "[Ripped]";
	mes "Hey, hola. La gente no es";
	mes "tan físicamente activa como";
	mes "antes. Aunque te ganes la vida";
	mes "peleando, quizá tu cuerpo";
	mes "tenga áreas débiles o";
	mes "flácidas. ¿Me explico?";
	next;
	mes "[Ripped]";
	mes "Hey, entrena conmigo, y te";
	mes "aseguro que podrás";
	mes "levantar y cargar más";
	mes "cosas. Solo dame un";
	mes "^FF0000Gym Pass^000000 por cada entrenamiento,";
	mes "y estaremos listos para empezar.";
	next;
	mes "[Ripped]";
	mes "Pero no te emociones tanto:";
	mes "Sin importar que tanto";
	mes "entrenemos, podrías";
	mes "sobrepasarte. ¿Has escuchado de alguien";
	mes "que sea extremadamente musculoso?";
	mes "No, porque todos están muertos. ¿Lo ves?";
	next;
	mes "[Ripped]";
	mes "Yo creo que sería más seguro";
	mes "para ti que entrenes seriamente";
	mes "conmigo y así aumentes";
	mes "tu capacidad de carga hasta ^FF000010 veces^000000";
	mes "Entonces... ¿Estás listo para sudar?";
	next;
	switch(select("Yes:No:Um, my workouts wore off.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[Ripped]";
				mes "Oh, genial, veo que";
				mes "trajiste tu Gym Pass.";
				mes "Está bien, sólo has lo que yo hago,";
				mes "y trata de sentir el esfuerzo.";
				mes "¿Preparado? ¡Empecemos!";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[Ripped]";
				mes "Listo, con esto debes ser capaz";
				mes "de cargar más cosas.";
				mes "Veamos, todavía es posible incrementar";
				mes "tu capacidad para cargar cosas";
				mes "^FF0000" + .@remain_carry + "^000000 veces más si seguimos";
				mes "entrenando juntos como hasta ahora.";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,0;
				close;
			}
			else {
				mes "[Ripped]";
				mes "Hey, ¿qué fue lo que te dije?";
				mes "Tienes que traerme un";
				mes "^FF0000Gym Pass^000000 si quieres";
				mes "ejercitarte, y hacer mas fuertes tus";
				mes "músculos para más cargar cosas.";
				close;
			}
		}
		else {
			mes "[Ripped]";
			mes "No creo que podamos";
			mes "seguir ejercitando tus músculos";
			mes "para cargar aún más.";
			mes "¡Sería muy peligroso para tu cuerpo";
			mes "tan siquiera intentarlo! Recuerda,";
			mes "te había advertido sobre los límites.";
			close;
		}
	case 2:
		mes "[Ripped]";
		mes "Aww, que pena.";
		mes "Bueno, regresa si";
		mes "cambias de parecer. Dile";
		mes "a tus amigos sobre mí:";
		mes "si están flácidos, yo les ayudaré";
		mes "a ponerse en forma.";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[Ripped]";
			mes "¿Qué pasó?";
			mes "¿Dejaste que tus músculos de";
			mes "carga se estropearan? Por suerte";
			mes "para ti, hay una cosa llamada";
			mes "memoria muscular. No tomará";
			mes "mucho tiempo recuperarlos...";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[Ripped]";
			mes "¿Qué tal?";
			mes "Tus músculos de carga";
			mes "han regresado,";
			mes "¡Así de fácil! Trata de no";
			mes "descuidar tus músculos otra vez, ¿ok?";
			skill "ALL_INCCARRY",gympassmemory,0;
			close;
		}
		else {
			mes "[Ripped]";
			mes "Uhh...";
			mes "Nunca nos hemos ejercitado";
			mes "juntos.";
			mes "Estoy seguro de eso.";
			close;
		}
	}
}
