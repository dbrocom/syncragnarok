//===== eAthena Script ======================================= 
//= Uneasy Prontera Cemetery Quest (original script!)
//===== By: ================================================== 
//= Lupus
//===== Current Version: ===================================== 
//= 1.2a (Tested and fully working!)
//===== Compatible With: ===================================== 
//= eAthena Version 1.0
//===== Description: ========================================= 
//= A periodical quest of the Uneasy Cemetery (Kill undead / Prevent their appearance)
//= Every day, at the midnight Prontera receive a wave of Undeads.
//= They come from Uneasy Cemetery of Prontera. To protect the players
//= from the undeads terror you may either kill the enemy. Or supply Mother Mathana
//= with needed amount of Holy Water. Every citizen can take his part in the 
//= saving of Prontera city. After some days of quiet life... the Cemetery strikes back.
//===== Additional Comments: ================================= 
//= 1.1 More advanced ver. Added some bonus the the one who'd kill the last walking undead
//= 1.2 Added coords to the script to make label OmMobDead working
//= 1.2a Changed item names to item IDs. [Samuray22]
//============================================================ 


prontera,3,3,3	script	Uneasy_Check	-1,{
	end;

OnHour00:
	set $UNEASY_DL,$UNEASY_DL-1;
	set $UNEASY_BL,$UNEASY_BL+30; //add need of HW for 30 bottles per day
	if ($UNEASY_BL>666) set $UNEASY_BL,666; //keep needed bottles not <=666
	if ($UNEASY_DL < 0) goto L_Start_Undead;
//The Cemetery is OK yet.
	disablenpc "Mother Mathana";
	end;
OnInit:
	if ($UNEASY_DL >= 0) disablenpc "Mother Mathana";
	end;

OnHour06:
	killmonsterall "prontera";	//The Sun kills undead in the morning
	end;

OnHour01:
	if ($@UNEASY_MOB > 0) mapannounce "prontera","[Mother Mathana]: In the name of Odin, please finish these roaming undead leftovers!",0;
	end;

OnZombieDead:
	set $@UNEASY_MOB,$@UNEASY_MOB-1;
	if ($@UNEASY_MOB>0) end;
	set $UNEASY_DL,0;
	set $UNEASY_H$,strcharinfo(0);
	if (Sex==1) mapannounce "prontera","[Mother Mathana]: Brave "+$UNEASY_H$+" has just killed the last undead in Prontera!",0;
	if (Sex==0) mapannounce "prontera","[Mother Mathana]: Lady "+$UNEASY_H$+" has just killed the last undead in Prontera!",0;
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	end;

L_Start_Undead:
	killmonsterall "prontera";	//kills any left monsters
	enablenpc "Mother Mathana";
//call some monsters in the city
	set $@UNEASY_MOB, 65;
	areamonster "prontera",0,0,0,0,"Zombie",1015,30,"Uneasy_Check::OnZombieDead";
//in the Cemetery
	monster "prontera",268,349,"Zombie",1015,30,"Uneasy_Check::OnZombieDead";
	monster "prontera",269,350,"Ghoul",1036,5,"Uneasy_Check::OnZombieDead";
//announce
	mapannounce "prontera","[Mother Mathana]: The cememtery has become restless!  In the name of Odin, hurry to the Sanctuary! Save the city of Prontera!",0;
}

prontera,257,313,5	script	Mother Mathana	79,{
	mes "[Mother Mathana]";
	if ($UNEASY_DL <= 0) goto L_Undead_Walk;
	mes "Me temo que hay algo malo con nuestro viejo cementerio...";
	if ($UNEASY_H$==strcharinfo(0)) mes "Pero gracias a ti, "+$UNEASY_H$+"Podremos dormir " + $UNEASY_DL + " Noches!";
	if ($UNEASY_H$!=strcharinfo(0)) mes "Pero gracias a la ayuda de "+$UNEASY_H$+"tenemos " + $UNEASY_DL + " Noches tranquilas!";
	emotion 0;
	close;

L_Undead_Walk:
	if ($UNEASY_DL == 0) mes "ELLOS podrian volver mañana en la noche de nuevo!";
	if ($UNEASY_DL == 0 && $UNEASY_H$==strcharinfo(0)) mes "Gracias, "+$UNEASY_H$+"! Ahora nos las arreglaremos para descansar asta la siguiente media noche!!";
	if ($UNEASY_DL == 0 && $UNEASY_H$!=strcharinfo(0)) mes "Pero debido a la ayuda de "+$UNEASY_H$+"podremos descansar asta la siguiente media noche!";
	mes "Para calmar la inquietud de el cementerio, debemos vertir agua bendita (Holy Water) en todas esas tumbas, pero a nuestros hermanos y hermanas se les acabo";
	mes "Podrias darnos agua bendita? (Holy Water)";
	next;
	menu "Si. toma toda mi agua bendita!",-, "No, la necesito.<_<",M_NO, "No tengo =(.",M_DONT_HAVE;

	if ( countitem(523)<1 ) goto M_DONT_HAVE;
	set $UNEASY_BL,$UNEASY_BL-countitem(523);
	delitem 523,countitem(523);

	if ( $UNEASY_BL > 0 ) goto L_NEED_MORE;
//set quiet days!!! no more undead for this period!
	set $UNEASY_DL,5+((0-$UNEASY_BL)/30);
	set $UNEASY_H$,strcharinfo(0);
	mes "[Mother Mathana]";
	mes "Gracias, "+$UNEASY_H$+"Ahora tenemos suficiente agua bendita!";
	next;
	mes "[Mother Mathana]";
	mes "Al verter el cementerio son esa agua tendremos " + $UNEASY_DL + " noches seguras!!";
	next;
	killmonsterall "prontera";	//kills any left monsters
	mes "[Mother Mathana]";
	mes "mira, "+ $UNEASY_H$ +"? se han ido!!!";
	next;
	mes "[Mother Mathana]";
	mes "Nuestra iglesia te agradecera personalmente...";
	next;
	if (Sex==1) mapannounce "prontera","[Mother Mathana]: In the name of Odin we declare handsom "+$UNEASY_H$+" as a Prontera savior!",0;
	if (Sex==0) mapannounce "prontera","[Mother Mathana]: In the name of Odin we declare beautiful "+$UNEASY_H$+" as a Prontera savior!",0;
	mes "[Mother Mathana]";
	mes "En el nombre de Odin te bendecimos y descentemente presentamos un modesto regalo de la bodega de vino de Mareusi.";
	getitem 505,1; //Blue_Potion
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	close;

L_NEED_MORE:
	mes "[Mother Mathana]";
	mes "Gracias, bueno "+strcharinfo(0)+"pero aun necesitamos " + $UNEASY_BL + " mas botellas de Holy Water.";
	close;

M_NO:
	mes "[Mother Mathana]";
	mes "Me temo que el cementerio pronto estara fuera de control... porfavor traernos toda la Holy Water que puedas.";
	close;

M_DONT_HAVE:
	mes "[Mother Mathana]";
	mes "Omg! aun necesitamos " + $UNEASY_BL + " Mas botellas de Holy Water... Porque no vas y le pides a otra gente algunas Holy Water extras?";
	mes "El cementerio pronto estara Fuera de Control!!....";
	mes "Porfavor, en el nombre de Odin, Ayuda a la ciudad de Prontera.";
	close;
}
