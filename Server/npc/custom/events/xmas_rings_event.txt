//===== eAthena Script =======================================
//= Karachun Event (Xmas Rings Quest)
//===== By: ==================================================
//= Lupus (1.0)
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena Version; 4880+ (with 'setItemScript' command)
//===== Description: =========================================
//= Custom event: Karachun (from 8 Dec till 8 Jan), X-Mas, HNY
//===== Additional Comments: =================================
//= 1.0 Tested, fully working. 1.0a a few typos fix
//= 1.1 Readded Mdef +1 and  Luk +1 to Santa's Hat [Lupus]
//= 1.2 Fixed double quotes not being escaped [brianluau]
//============================================================


prontera,156,174,4	script	Snower	713,{
	mes "[Snower]";
	if(Q_XMSRINGS==2){
		mes "¿Oh... es verdad?¿ Tu ayudaste a mi hermanita Sneewy? Gracias.";
	}else if(Q_XMSRINGS==3){
		mes "Snoowy me envió un pajaro con un mensaje. Ella me dijo sobre tu ayuda...";
		mes "Te diré un secreto de los anillos mágicos.";
		emotion e_no1;
		next;
		mes "[Snower]";
		mes "Ellos tienen poderes mágicos solo del 8 de Diciembre hasta el 8 de Enero.";
		next;
		mes "[Snower]";
		mes "Por cierto, solo ponte Santa's Hat para descubrir su verdadero poder.";
		next;
		mes "[Snower]";
		mes "Si te pones ambos anillos su poder se duplicará.";
		mes "Ten un buen día!";
		close;
	}else{
		mes "¡Hola! ¡Dos de mis hermanas y yo estamos preparando regalos para todas las 3 fiestas!";
	}
	emotion e_sob;
	next;
	menu "¿Donde estan tus hermanas?",-, "¿Que fiestas?",M_PR,"¿Que regalos?",M_WRAP,"¿Puedo ayudarte?",M_QUEST;

	mes "[Snower]";
	mes "Mi hermana más joven Sneewy se fue a alguna tierra lejana para preparar presentes para sus pobres chicos...Yo no se donde. Deberías preguntar  a mi hermana mayor y sus pajaritos.";
	next;
	mes "[Snower]";
	mes "¿Mi hermana mayor? Su nombre es Snoowy, Ella navegó a alguna isla verde... Ella está hablando sobre algunos presentes para delfines o algo así...";
	close;

M_PR:
	mes "[Snower]";
	mes "¿Todas las tres fiestas? ¡Bueno, ellas vienen una despues de la otra!";
	mes "^000080The Christmas^000000, ^FF0000Karachun^000000 and ^008000New Year Eve^000000!";
	next;
	mes "[Snower]";
	mes "^FF0000Karachun^000000? Heh... This is an old pagans festival. They used to celebrate it before the appearance of HNY and XMas, you know.";
	next;
	goto M_QUEST;

M_WRAP:
	mes "[Snower]";
	mes "My hermana dice que todos los presentes deben verse de la misma forma. ¡De lo contrario todos los chicos lucharían por los más elaborados!";
	next;
	mes "[Snower]";
	mes "¿Te gustaría que te envuelva tus lujosas cajas de regalos con un papel gris?";
	next;
	menu "¡Envuélvelas, por favor!",-, "No, gracias.",M_QUEST;

	set @prizes, countitem(664)+countitem(665)+countitem(666)+countitem(667);
	if( @prizes == 0){
		mes "[Snower]";
		mes "No veo ningún presente en cajas lujosas contigo...";
		if(countitem(644))mes "Solo tienes cajas grises";
		close;
	}
	delitem 664,countitem(664);
	delitem 665,countitem(665);
	delitem 666,countitem(666);
	delitem 667,countitem(667);
	getitem 644,@prizes;

	mes "[Snower]";
	mes "*shuffle* Aquí tienes.";
	next;

M_QUEST:
	mes "[Snower]";
	mes "Espero que encuentres a mi hermana lo mas pronto posible... Ellas necesitan ayuda.";
	if(Q_XMSRINGS==0) set Q_XMSRINGS,1;
	close;

OnInit:
//Santa's Hat
	setitemscript 2236,"{ bonus bMdef,1; bonus bLuk,1; if(isequipped(2636,2637)){if(@xmr == gettime(2))end; set @xmr,gettime(2); misceffect 410; end;} if(isequipped(2636)){if(@xmr == gettime(2))end; set @xmr,gettime(2); misceffect 72;} if(isequipped(2637)){if(@xmr == gettime(2))end; set @xmr,gettime(2); misceffect 338;}}";
//Gold Xmas Ring
	setitemscript 2636,"{ bonus bLoseSPWhenUnequip,30; if(isequipped(2236)==0)end; if(getskilllv(\"AL_HEAL\")){skill \"TF_HIDING\",4+isequipped(2637);}else{skill \"AL_HEAL\",1+4*isequipped(2637);} }";
//Silver Xmas Ring
	setitemscript 2637,"{ bonus bDamageWhenUnequip,40; if(isequipped(2236)==0)end; if(getskilllv(\"AL_TELEPORT\")){skill \"MC_IDENTIFY\",1;}else{skill \"AL_TELEPORT\",1+isequipped(2636);} }";
	end;
}

louyang,224,249,4	script	Sneewy	717,{
	mes "[Sneewy]";
	if(Q_XMSRINGS==0){
		mes "¡Hola! Estoy esperando el pajarito con mensaje de mi hermana...";
		emotion e_sob;
		close;
	}
	if(Q_XMSRINGS==1){
		mes "Desearía tener 4 Gift Boxes grises mas para los chicos nativos...";
		emotion e_sob;
		next;
		mes "[Sneewy]";
		mes "Oh, hola. ¿Mi hermano te ha dicho todo?";
		next;
		menu "Aquí están tus 4 Gift Boxes",-, "I don't know any Snowhatevers...",M_END;

		mes "[Sneewy]";
		if(countitem(644)<4){
			mes "Dame 4 presentes para los chicos pobres... ¡Espera, no tienes 4 de ellos!";
			emotion e_sry;
			close;
		}
		set Q_XMSRINGS,2;
		delitem 644,4;
		getnameditem 2637,strcharinfo(0);
		emotion e_kis2;
		mes "Thank you! Here's my li'l something for you!";
		next;
		mes "[Sneewy]";
		mes "Por cierto, si te pones 2 anillos diferentes entonces tu leveo en las fiestas será mucho más fácil!";
		close;
	}
	emotion e_thx;
	mes "¡Gracias de nuevo! ¡Ningún chico  pasará las fiestas sin presentes!";
	close;

M_END:
	mes "[Sneewy]";
	mes "Lástima... Tengo un anillo mágico de Santa.... Lo cambiaría por 4 Gift Boxes...";
	emotion e_swt;
	close;
}

izlu2dun,133,160,2	script	Snoowy	714,{
	mes "[Snoowy]";
	if(Q_XMSRINGS==0){
		mes "Holaaaaaa! ¿Conoces a mi hermano y hermana? ¿Como es eso? ¡Mi hermano Snower es muy conocido en Prontera! Solo pregunta a cualquier chico allí.";
		emotion e_hmm;
		close;
	}
	if(Q_XMSRINGS==1){
		mes "Darn... How could poor children be more important than the marine folks.. Am I wrong?";
		next;
		mes "[Snoowy]";
		mes "Errr. Helloou there. My sister Sneewy's sent me a birdie message. The letter was wet and I couldn't read anything but some ending letters of her address...";
		next;
		mes "[Snoowy]";
		mes "Look, it's something like 'yang' in the very end... Hmm... She must be misspelled 'Al Doo Boryang'...";
		close;
	}
	if(Q_XMSRINGS==2){
		mes "You've helped my sis? It's just great!";
		next;
		mes "[Snoowy]";
		mes "But I too need 3 more gifts for my marine folk friends...";
		next;
		menu "Have these gifts!",-, "I need a li'l something in return",M_END;

		mes "[Snoowy]";
		if(countitem(644)<3){
			mes "Oops!! You haven't got 3 gray gift boxes on you.";
			emotion e_sry;
			close;
		}
		set Q_XMSRINGS,3;
		delitem 644,3;
		getnameditem 2636,strcharinfo(0);
		emotion e_kis;
		mes "Thaank yoouu! Have this magic ring!";
		close;
	}
	emotion e_thx;
	mes "Oh... the whole marine folk send you their thanks! They love presents so much!!!";
	if(rand(2))mes "Didn't you know that Santa's Hat reveals the real power of the rings?";
	close;

M_END:
	mes "[Snoowy]";
	mes "I'd exchange a GOLD magic ring for 3 gift boxes...";
	emotion e_swt;
	close;
}
