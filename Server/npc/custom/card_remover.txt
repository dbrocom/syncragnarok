//===== eAthena Script ======================================= 
//= Card Removal NPC
//===== By: ================================================== 
//= TyrNemesis^
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Removes cards from equipped items.
//===== Additional Comments: ================================= 
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//============================================================ 

prt_in,28,73,4	script	Wise Old Woman#eAcustom	78,{
	mes "[Wise Old Woman]";
	mes "Buenos días, joven. Tengo la habilidad de remover cartas de tu equipo. ¿Te agrada esta idea?";
	next;
	switch(select("Yes, it does.:What do you charge?:No thanks.")) {
		case 1:
			mes "[Wise Old Woman]";
			mes "Muy bien. ¿Qué item debo examinar?";
			next;

			setarray .@position$[1], "Head","Body","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
			set .@menu$,"";
			for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
			{
				if( getequipisequiped(.@i) )
					set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

				set .@menu$, .@menu$ + ":";
			}
			set .@part,select(.@menu$);
			if(!getequipisequiped(.@part)) {
				mes "[Wise Old Woman]";
				mes "Joven... No estas vistiendo nada de lo que yo pueda remover una carta.";
				close;
			}
			if(getequipcardcnt(.@part) == 0) {
				mes "[Wise Old Woman]";
				mes "Joven... No hay cartas en este item. Me temo que no puedo hace nada con él.";
				close;
			}
			set .@cardcount,getequipcardcnt(.@part);
			
			if (!checkweight(1202,(.@cardcount+1))) { 
				mes "^3355FFUn momento!";
				mes "No puedo ofrecerte ninguno de";
				mes "mis servicios porque";
				mes "traes mucho";
				mes "peso. Pon tus items en";
				mes "Kagra Storage y vuelve.";
				close;
			}
			mes "[Wise Old Woman]";
			mes "Este item tiene " + .@cardcount + " cartas en él. Para hacer mi magia, necesitaré " + (200000+(.@cardcount * 25000)) + " zeny, un ^0000FFStar Crumb^000000, y una ^0000FFYellow Gemstone^000000.";
			next;
			if(select("Very well. Do it.:Nevermind.") == 2) {
				mes "[Wise Old Woman]";
				mes "Muy bien. Regresa si necesitas mis servicios.";
				close;
			}
			if((zeny < (200000+(.@cardcount * 25000))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
				mes "[Wise Old Woman]";
				mes "No tienes los items que requiero para hacer mi magia, joven. Vuelve cuando los tengas.";
				close;
			}
			mes "[Wise Old Woman]";
			mes "Antes de comenzar, debo advertirte... puedo fallar. Si lo hago, podría destruir las cartas, el item o ambos. No hay reembolso. Dicho esto: ¿qué es más importante para ti: las cartas o el item?";
			next;
			switch(select("I changed my mind about this.:The item.:The cards.")) {
				case 1:
					mes "[Wise Old Woman]";
					mes "Muy bien. Vuelve inmediatamente si buscas mis servicios.";
					close;
				case 2:
					set .@failtype,1;
					break;
				case 3:
					set .@failtype,2;
			}
			mes "[Wise Old Woman]";
			mes "Muy bien. Comenzaré.";
			set zeny,zeny - (200000+(.@cardcount * 25000));
			delitem 1000,1;
			delitem 715,1;
			
			// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
			// First value = Total failure chance (item and cards destroyed)
			// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
			// Third value = Harmless failure chance (all that's lost is your investment)

			set .@failchance,rand(100);
		/*	
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Wise Old Woman]";
				mes "The process was a total failure. I am afraid the item and the cards were destroyed.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;					
					mes "[Wise Old Woman]";
					mes "While I have managed to remove the cards from the item, they were destroyed in the process. The item, however, is okay.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Wise Old Woman]";
					mes "Most unfortunate. I succeeded at removing the cards, but the item itself was destroyed in the process.";
					close;	
				}
			}
		*/

			if(.@failchance < 10) {
				next;
				failedremovecards .@part,3;				
				mes "[Wise Old Woman]";
				mes "No logré remover las cartas. Pero, afortunadamente, tanto el item como las cartas están en buenas condiciones.";
				close;	
			}
			next;
			successremovecards .@part;
			mes "[Wise Old Woman]";
			mes "El proceso fue un éxito. Aquí están tus cartas y tu item. Adiós.";
			close;			
		case 2:
			mes "[Wise Old Woman]";
			mes "Te costará la módica suma de 200000 zeny, más 25000 zeny por cada carta que remueva del item. Además, necesito un 'star crumb' y una 'yellow gemstone' para hacer mi magia.";
			close;
		case 3:
			mes "[Wise Old Woman]";
			mes "Muy bien. Vuelve inmediatamente si buscas mis servicios.";
			close;
	}
}
