//===== Athena Script =======================================
//= Donation NPC
//===== By ==================================================
//= Josh
//===== Version =============================================
//= 1.0	 - First release. Probably contains bugs/security
//=	   risks.
//= 1.1  - Added a check for whether the account exists when
//=	   adding a donator. Need to improve ordering when
//=	   viewing all donations.
//= 1.2  - Modified for public use. Added checkweight feature.
//= 2.0  - Many changes, especially ones I had always wanted
//=	   to add to this script. Includes reading items from
//=	   a separate SQL table and more database manipulation
//=	   options for GMs.
//= 2.1  - Made few changes including the add/remove items
//=	   feature.
//= 3.0  - All strings inputted by a user and user/char names
//=	   in SQL queries are now escaped. Each item has a
//=	   price rather than a quantity. This script can work
//=	   with decimals.
//= 3.1  - Added quotes to some queries, fixed a variable and
//=	   removed a comment.
//= 3.2  - Fixed a problem where eAthena would crash if a
//=	   query returned NULL.
//= 3.3  - Optimized query speeds by combining a few select 
//=	   queries into one. Requires Trunk 7975.
//= 3.4  - Added MySQL version check. If version < 5.0.8, all
//=	   queries with CAST are omitted. Use 5.0.8 and up
//=	   when possible. SQL errors may consequent if GM's
//=	   input is incorrect. Added logging of claims.
//=	   "log_npc" in log_athena.conf must be enabled. Logs
//=	   will appear in the "npclog" table. Claim menu now
//=	   only shows items that can be afforded.
//= 3.5  - Minor change to table.
//= 3.6  - Removed name column in donate_item_db. Added
//=	   support for item_db2 table.
//= 3.7  - Added Zeny support. $rate must be set for it to be
//=	   used. Removed truncate() in a query since eAthena
//=	   automatically truncates floats to ints.
//= 3.8  - Fixed problem with menus and null values.
//= 3.9  - Explicit reset of @aid.
//= 3.10 - Applied previous fix to other variables and forced
//=	   dialogue box closure every time database is
//=	   modified.
//= 3.11 - Explicit reset of another variable. Fixed typo
//=	 - with $rate. Added logmes for GM operations.
//===== Compatible With =====================================
//= eAthena SQL - any version with the new query_sql command
//=		  (Trunk 7975 and up).
//= MySQL - 5.0.8 and up highly recommended but not required.
//===== Description =========================================
//= A script that lets a player claim an item for donating.
//= Allows a GM to input each donation.
//===== Comments ============================================
//= This script uses SQL tables to store variables for the
//= amount donated by users and the items claimable.
//===== Installation ========================================
//= You must import donate.sql and donate_item_db.sql (and
//= item_db.sql and item_db2.sql, which comes with eAthena)
//= before using this script.
//===========================================================
//= Thanks to Vich for helping me with the SQL syntax.
//= Thanks to Lance for helping me with the the arrays and 
//= for implementing query_sql.
//= Thanks to Skotlex for implementing escape_sql.
//= Thanks to Toms for implementing the new multi-column 
//= query_sql command.
//===========================================================

prontera,145,179,5	script	Donation Girl	714,{

if (getgmlevel() >= 80) goto L_GM;

L_START:
mes "[Donation Girl]";
mes "Hola! Soy la chica de las Donaciones!";
mes "Si hiciste una donación";
mes "tienes derecho a una recompenza!";
next;
menu "Más información.",-,"Pedir recompenza.",L_CHECK,"Estadísticas.",L_STATS;
L_INFO:
mes "[Donation Girl]";
mes "Cada mes se paga mucho dinero para el funcionamiento de este servidor.";
next;
mes "[Donation Girl]";
mes "Tú puedes ayudar donando cualquier cantidad de dinero.";
next;
mes "[Donation Girl]";
mes "Para demostrarte nuestro aprecio te daremos una linda recompenza.";
next;
menu "Continuar.",L_START,"Cancelar.",-;

L_CHECK:
query_sql "SELECT `amount`,`claimed` FROM `donate` WHERE `account_id` = "+getcharid(3), @amount$, @claimed$;
query_sql "SELECT "+@amount$+" - "+@claimed$, @value$;
query_sql "SELECT '"+@value$+"' > 0", @enough;
if(!@enough) {
	mes "[Donation Girl]";
	mes "Disculpa, no puedes pedir tu recompenza-";
	mes "Si ya haz donado pero no te hemos dado tu recompensa,";
	mes "por favor danos tiempo para procesar tu donación.";
	close;
	}

L_CLAIM:
mes "[Donation Girl]";
mes "Grácias por donar!";
mes "Tú tienes $"+@value$+" Valor de crédito!";
mes "¿Qué quieres de recompenza?";
next;
menu "Items",L_CLAIMITEM,"Zeny",L_ZENY;

L_CLAIMITEM:
mes "[Donation Girl]";
mes "Muy bien. ¿Cuál item quieres?";
next;
query_sql "SELECT `id` FROM `donate_item_db` WHERE `price` <= "+@value$+" ORDER BY `id`",@name;
set @menu$, getitemname(@name[0]);
	for(set @i, 1; @i < getarraysize(@name); set @i, @i + 1){
		set @menu$, @menu$ + ":" + getitemname(@name[@i]);
	}

set @m, select(@menu$)-1;

query_sql "SELECT `price` FROM `donate_item_db` WHERE `id` = "+@name[@m], @price$;
query_sql "SELECT "+@value$+" / "+@price$, @max;

mes "[Donation Girl]";
mes getitemname(@name[@m])+"s costo $"+@price$+" cada.";
mes "Cuántos "+getitemname(@name[@m])+"Quieres tu recompenza?";
mes "Máximo: "+@max+".";
input @quantity;
mes "[Donation Girl]";
if(@quantity>@max) {
	mes "Disculpa, no puedes pedir tu recompenza. "+@quantity+" "+getitemname(@name[@m])+"s.";
	next;
	goto L_CLAIM;
	}
if(!@quantity) {
	mes "No puedes tener 0 como cantidad!";
	next;
	goto L_CLAIM;
	}
if(!checkweight(@name[@m],@quantity)) {
	mes "Perdón, pero no lo puedes llevar "+@quantity+" "+getitemname(@name[@m])+"s.";
	next;
	goto L_CLAIM;
	}
query_sql "SELECT "+@quantity+" * "+@price$, @total$;
mes "Estás seguro que quieres una recompenza "+@quantity+" "+getitemname(@name[@m])+"por $"+@total$+"?";
next;
menu "No.",L_CLAIM,"Si.",-;
query_sql "UPDATE `donate` SET `claimed` = `claimed` + "+@total$+" WHERE `account_id` = "+getcharid(3);
logmes "Claimed "+@quantity+" "+getitemname(@name[@m])+"s";
getitem @name[@m],@quantity;
mes "[Donation Girl]";
mes "Gracias por donar! Esperamos que disfrutes de tu recompenza!";
close;

L_ZENY:
mes "[Donation Girl]";
if(!$rate) {
	mes "Disculpa, de momento no recompenzamos con zeny.";
	mes "Por favor regresa y reclama un item.";
	next;
	goto L_CLAIM;
	}
query_sql "SELECT "+@value$+" * "+$rate, @maxzeny;
mes "Muy bien. Puedes pedir tanto como "+@maxzeny+"Z.";
mes "¿Cuánto Zeny quieres reclamar?";
input @zeny;
mes "[Donation Girl]";
if(@zeny>@maxzeny) {
	mes "Disculpa, pero no tienes suficientes para reclamar "+@zeny+"Z.";
	next;
	goto L_CLAIM;
	}
if(!@zeny) {
	mes "No puedes tener 0 como cantidad!";
	next;
	goto L_CLAIM;
	}
set @total, @zeny * $rate;
mes "Estás seguro que quieres reclamar "+@zeny+"Z por $"+@total+"?";
next;
menu "No.",L_CLAIM,"Si",-;
query_sql "UPDATE `donate` SET `claimed` = `claimed` + "+@total+" WHERE `account_id` = "+getcharid(3);
logmes "Claimed "+@zeny+" zenies";
set Zeny, Zeny + @zeny;
mes "[Donation Girl]";
mes "Grácias por donar! Esperamos que disfrutes tu recompenza!";
close;

L_STATS:
mes "[Donation Girl]";
query_sql "SELECT IFNULL((SELECT SUM(amount) FROM `donate`),0)", @total$; 
mes "Nuestro fondo está en un total de $"+@total$;
next;
menu "Más información.",L_INFO,"Pedir mi recompenza.",L_CHECK,"Estadísticas.",L_STATS;

L_GM:
mes "[GM Menu]";
mes "Hola GM!";
mes "¿Qué quieres hacer?";
next;
query_sql "SHOW VARIABLES LIKE 'version'", @version, @valule$;
query_sql "SELECT '"+@valule$+"' >= '5.0.8'", @version;
menu "Agregar/Remover una Donación.",L_GM2,"Agregar/Remover Items.",L_ITEM,"(Re)Tipo de cambio determinado.",L_RATE,"Probar el Script.",L_START;

L_GM2:
menu "Agregar una donación.",L_DONATE,"Remover una donación.",L_REMOVE,"Ver todas las donaciones.",L_VIEWALL,"Regresar al menú principal",L_GM;

L_ITEM:
menu "Agregar un item.",L_NEWITEM,"Remover un item.",L_DELITEM,"Ver todos los items.",L_ALLITEMS,"Regresar al menú principal.",L_GM;

L_NEWITEM:
mes "[GM Menu]";
mes "Por favor indica el nombre del item:";
input @itemname$;
set @iid, 0;
query_sql "SELECT `id` FROM `item_db` WHERE `name_english` = '"+escape_sql(@itemname$)+"' || `name_japanese` = '"+escape_sql(@itemname$)+"' UNION SELECT `id` FROM `item_db2` WHERE `name_english` = '"+escape_sql(@itemname$)+"' || `name_japanese` = '"+escape_sql(@itemname$)+"'", @iid;
if(!@iid) goto L_INONE;
query_sql "SELECT 1 FROM `donate_item_db` WHERE `id` = "+@iid, @check;
mes "[GM Menu]";
mes "Por favor indica el coste de cada uno: "+@itemname$+":";
input @cost$;
if(@version) query_sql "SELECT CAST('"+escape_sql(@cost$)+"' AS DECIMAL)", @cost$;
query_sql "SELECT '"+escape_sql(@cost$)+"' > 0", @valid;
if(!@valid) goto L_ZERO;
mes "[GM Menu]";
mes "Has especificado que los donadores pueden demandar "+@itemname$+"por $"+@cost$+" cada.";
mes "¿Quieres continuar?";
next;
menu "No.",L_ITEM,"Si.",-;
mes "[GM Menu]";
if(!@check){
	query_sql "INSERT INTO `donate_item_db` VALUES ("+@iid+",'"+@cost$+"')";
	logmes "Added "+@itemname$+"s to list of claimable items";
	mes "Item agregado sin problemas!!";
	} else {
	mes "Item "+@itemname$+" Ya existe en la base de datos.";
	mes "¿Quieres reemplazarlo?";
	next;
	menu "No.",L_ITEM,"Si.",-;
	query_sql "REPLACE INTO `donate_item_db` VALUES ("+@iid+",'"+@cost$+"')";
	logmes "Changed the price of "+@itemname$+"s";
	mes "[GM Menu]";
	mes "Item reemplazado sin problemas!!.";
	}
close;

L_INONE:
mes "[GM Menu]";
mes "Item "+@itemname$+" no existe.";
next;
goto L_ITEM;

L_DELITEM:
mes "[GM Menu]";
mes "Por favor indica el nombre del Item:";
input @itemname$;
set @iid, 0;
query_sql "SELECT `donate_item_db`.`id` FROM `donate_item_db` LEFT JOIN `item_db` ON `donate_item_db`.`id` = `item_db`.`id` LEFT JOIN `item_db2` ON `donate_item_db`.`id` = `item_db2`.`id` WHERE `item_db`.`name_english` = '"+escape_sql(@itemname$)+"' || `item_db`.`name_japanese` = '"+escape_sql(@itemname$)+"' || `item_db2`.`name_english` = '"+escape_sql(@itemname$)+"' || `item_db2`.`name_japanese` = '"+escape_sql(@itemname$)+"'", @iid;
if(!@iid) goto L_INONE;
next;
mes "[GM Menu]";
mes "Has especificado para borrar "+@itemname$+" de la Base de Datos.";
mes "¿Quieres continuar?";
next;
menu "No.",L_ITEM,"Si.",-;
query_sql "DELETE FROM `donate_item_db` WHERE `id` = "+@iid;
logmes "Deleted "+@itemname$+"s from list of claimable items";
mes "[GM Menu]";
mes "Item borrado sin problemas!!.";
close;

L_ALLITEMS:
mes "[GM Menu]";
query_sql "SELECT `id`,`price` FROM `donate_item_db` ORDER BY `id`", @items, @itemamount$;
for(set @i, 0; @i < getarraysize(@items); set @i, @i + 1){
		mes getitemname(@items[@i])+" - $"+@itemamount$[@i];
	}
next;
goto L_GM;

L_DONATE:
mes "[GM Menu]";
mes "Por favor indica el nombre del donador:";
input @donator$;
set @aid, 0;
query_sql "SELECT `account_id` FROM `login` WHERE `userid` = '"+escape_sql(@donator$)+"'", @aid;
if(!@aid) goto L_NONE;
set @donated$, "";
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid, @donated$;
query_sql "SELECT '"+@donated$+"' > 0", @donated;
switch(@donated) {
	case 0:
		mes @donator$+" no ha donado antes.";
		break;
	case 1:
		mes @donator$+" ha donado $"+@donated$+".";
		break;
	}
next;
mes "[GM Menu]";
mes "Por favor indica la cantidad donada por "+@donator$;
input @donating$;
if(@version) query_sql "SELECT CAST('"+escape_sql(@donating$)+"' AS DECIMAL)", @donating$;
query_sql "SELECT '"+escape_sql(@donating$)+"' > 0", @valid;
if(!@valid) goto L_ZERO;
mes "[GM Menu]";
mes "Has especificado que "+@donator$+" a donado $"+@donating$+".";
mes "¿Quieres continuar?.";
next;
menu "No.",L_GM,"Si.",-;
switch(@donated) {
	case 0:
		query_sql "INSERT INTO `donate` VALUES ("+@aid+", '"+@donating$+"', 0)";
		break;
	case 1:
		query_sql "UPDATE `donate` SET `amount` = `amount` + "+@donating$+" WHERE `account_id` = "+@aid;
		break;
	}
logmes "Credited "+@donator$+" with $"+@donating$;
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid, @newdonated$;
mes "[GM Menu]";
mes "Donación agregada sin problemas!!.";
mes @donator$+" a donado un total de $"+@newdonated$;
close;

L_ZERO:
mes "[GM Menu]";
mes "No puedes tener 0 como cantidad!.";
next;
goto L_GM;

L_NONE:
mes "[GM Menu]";
mes "Nombre de la cuenta "+@donator$+" no existe.";
next;
goto L_GM;

L_REMOVE:
mes "[GM Menu]";
mes "Por favor indica el nombre del Donador:";
input @donator$;
set @aid, 0;
query_sql "SELECT `account_id` FROM `login` WHERE `userid` = '"+escape_sql(@donator$)+"'", @aid;
if(!@aid) goto L_NONE;
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid, @donated$;
query_sql "SELECT '"+@donated$+"' > 0", @donated;
mes "[GM Menu]";
if(!@donated) {
	query_sql "DELETE FROM `donate` WHERE `account_id` = "+@aid;
	logmes "Deleted "+@donator$+" from donation database";
	mes @donator$+" no es un donador y se ha borrado de la Base de Datos de donaciones.";
	} else {
	mes @donator$+" ha donado $"+@donated$+".";
	next;
	switch(select("Deduct an amount from "+@donator$,"Remove "+@donator$+" from the donation database")){
		mes "[GM Menu]";
		case 1:
			mes "Por favor indica la cantidad: "+@donator$+" es para ser deducido por:";
			input @deduct$;
			if(@version) query_sql "SELECT CAST('"+escape_sql(@deduct$)+"' AS DECIMAL)", @deduct$;
			query_sql "SELECT '"+escape_sql(@deduct$)+"' > 0", @valid;
			if(!@valid) goto L_ZERO;
			mes "[GM Menu]";
			mes "Has especificado que "+@donator$+" es para ser deducido por $"+@deduct$+".";
			mes "¿Quieres continuar?";
			next;
			menu "No.",L_GM,"Si.",-;
			query_sql "UPDATE `donate` SET `amount` = `amount` - "+@deduct$+" WHERE `account_id` = "+@aid;
			query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid, @afterdeduct$;
			logmes "Deducted "+@deduct$+" from "+@donator$;
			mes "[GM Menu]";
			mes "Donación deducida sin problemas!!.";
			mes @donator$+" ha donado un total de $"+@afterdeduct$;
			break;
		case 2:
			mes "Has especificado removerlo "+@donator$+" de la Base de Datos de donaciones.";
			mes "¿Quieres continuar?";
			next;
			menu "No.",L_GM,"Si.",-;
			query_sql "DELETE FROM `donate` WHERE `account_id` = "+@aid;
			logmes "Deleted "+@donator$+" from donation database";
			mes "[GM Menu]";
			mes "Donador borrado sin problemas!!";
			break;
		}
	}
close;

L_VIEWALL:
mes "[GM Menu]";
query_sql "SELECT `account_id`,`amount` FROM `donate` ORDER BY `amount` DESC", @donatoraid, @donatedamount$;
for(set @i, 0; @i < getarraysize(@donatoraid); set @i, @i + 1){
	query_sql "SELECT `userid` FROM `login` WHERE `account_id` = "+@donatoraid[@i], @donateruserid$;
	for(set @j, 0; @j < getarraysize(@donateruserid$); set @j, @j + 1){
		mes @donateruserid$[@j]+" - "+@donatedamount$[@i];
	}
}
next;
goto L_GM;

L_RATE:
mes "[GM Menu]";
if($rate) mes "$1 está actualmente en el valor "+$rate+"Z.";
mes "¿Cuánto Zeny vale $1?";
input $rate;
mes "[GM Menu]";
mes "El valor de $1 ha cambiado con éxito a "+$rate+"Z.";
next;
goto L_GM;
}
