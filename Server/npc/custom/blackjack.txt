//===== eAthena Script ======================================= 
//= Black Jack
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//=
//===== Description: ========================================= 
//= Black Jack card game.  Gameplay based off standard casino
//= black jack rules.  Dealer must have at least 17 to stay and will
//= automatically stay at 17 and up.  Player must have at least
//= 13 to stay.  Aces counted as 11 or 1.  Option to "Double Down".
//= Currently does not allow for "insurance", or "splitting"
//= of pairs.
//===== Additional Comments: ================================= 
//=
//============================================================ 


cmd_in02,188,89,1	script	Black Jack Dealer	57,{

	mes "[Dealer]";
	mes "Hola! ¿Deseas jugar Black Jack?";
	M_Menu:
	next;
	menu "¿Cuáles son las reglas?",M_0, "Si quiero jugar.",L_Play, "No gracias.",M_End;

	M_0:
		mes "[Dealer]";
		mes "¿Qué deseas saber?";
		sM_Menu0:
		next;
		menu "Lo básico.",sM_0a, "Perder y ganar.",sM_0b, "\"Doblar apuesta\"",sM_0c,
			"Valor del As",sM_0d, "Nada",M_Menu;

		sM_0a:
			mes "[Dealer]";
			mes "Black Jack es un juego de cartas en el cual el objetivo es llegar a 21, o tan";
			mes "cerca como sea posible a 21 puntos, sin pasarse de esa cifra.";
			next;
			mes "[Dealer]";
			mes "Antes que el juego comience, el jugador debe hace una apuesta. Una vez hecha";
			mes "la apuesta, tanto al jugador como al croupier se le reparten dos cartas";
			mes "a cada uno. Dependiendo de las cartas que te salgan puedes elegir ^5533FF'Plantarse'^000000,";
			mes "o pudes elegir ^5533FF'Otra carta'^000000.";
			next;
			mes "[Dealer]";
			mes "Cuando escojes \"Plantarse\", le estás diciendo al croupier que no";
			mes "necesitas más cartas. Esto le permite al croupier pedir otra carta si él/ella";
			mes "así lo desea. Para plantarte, debes tener ^FF3355como mínimo 13 puntos^000000. El/La";
			mes "croupier puede plantarse solo cuando él/ella tiene^FF3355 17 puntos o más^000000.";
			next;
			mes "[Dealer]";
			mes "Cuando escojes \"Otra carta\", le estás diciendo al croupier que quieres";
			mes "otra carta. Como tienes más cartas puedes aumentar tu puntaje";
			mes "total. Siempre y cuando tengas ^FF3355lmenos de 21^000000 puntos puedes pedir";
			mes "una carta del mazo de cartas.";
			goto sM_Menu0;
		sM_0b:
			mes "[Dealer]";
			mes "Existen tres formas de ganar y perder en el Black Jack.";
			next;
			mes "[Dealer]";
			mes "1.) Al final de la ronda de Black Jack, si tienes más puntos";
			mes "que el croupier, tu ganas la mano. De otra manera, si tu tienes";
			mes "menos puntos que el croupier tu pierdes.";
			next;
			mes "[Dealer]";
			mes "2.) Si pides otra carta y esta hace que tu puntaje sea mayor a 21, tu";
			mes "automaticamente pierdes la mano. Esto es ^5533FF'Pasarse'^000000.  Si";
			mes "el croupier se pasa entonces tu ganas la mano.";
			next;
			mes "[Dealer]";
			mes "3.) Si tienes un puntaje total de 21 con las primeras 2 cartas, tu";
			mes "automaticamente ganas la mano. Esto se llama ^5533FF'Black Jack'^000000";
			mes "y sucede cuando obtienes un \"As\" y una carta con valo de \"10\". Si el/la";
			mes "croupier obtiene un \"Black Jack\" él/ella gana la mano automaticamente.";
			next;
			mes "[Dealer]";
			mes "4.) Aparte de perder o ganar, puedes empatar con el dealer. Si";
			mes "ambos, tú y el/la croupier, tienen el mismo puntaje total al finalizar";
			mes "la mano, esto se denomina Empate, sin ganador y sin perder ni ganar";
			mes "dinero.";
			next;
			mes "[Dealer]";
			mes "Esto es llamado  ^5533FF'Empate'^000000 con el croupier. Esto también";
			mes "aplica cuando tú y el croupier tienen Black Jack al mismo";
			mes "tiempo.";
			goto sM_Menu0;
		sM_0c:
			mes "[Dealer]";
			mes "La opción de \"Doblar\" te permite doblar tu apuesta en juego,";
			mes "pero con la desventaja de que solamente puedes pedir una";
			mes "carta adicional. Esta opción está habilitada unicamente al principio de";
			mes "cada mano.";
			next;
			mes "[Dealer]";
			mes "Un ejemplo de cuando Doblar es ventajoso, es cuando";
			mes "tus primeras dos cartas te dan un puntaje de 11. Ahí tienes";
			mes "una buena probabilidad de obtener 21 ó 20 con la siguiente carta que te den. Esta";
			mes "es una buena mano para Doblar tu apuesta.";
			goto sM_Menu0;
		sM_0d:
			mes "[Dealer]";
			mes "La carta del \"As\" es única en el juego del Black Jack a que";
			mes "puede tener 2 valores. Un As puede ser contado ya sea como 11 puntos";
			mes "o solamente 1 punto. Por ejemplo si tienes un As y un 4, eso";
			mes "te serían 15 ó 5 puntos.";
			next;
			mes "[Dealer]";
			mes "Si decides Plantarte, el As se cuenta automaticamente";
			mes "como 11 puntos para darte un total de 15 puntos.";
			next;
			mes "[Dealer]";
			mes "Si decidiste pedir otra carta y recibes un 9, el As se";
			mes "tomaría como 1 punto para date un total de 14";
			mes "puntos. Si el As fuese contado como 11 puntos, tu tendrías";
			mes "un puntaje total mayor a 21 y perderías.";
			next;
			mes "[Dealer]";
			mes "Es por la flexibilidad que se tiene con el \"As\" que la hace";
			mes "la carta más poderosa del juego.";
			goto sM_Menu0;
	M_End:
		mes "[Dealer]";
		mes "Sientete bienvenido cuando quieras volver.";
		close;

//================
L_Play:
	mes "[Dealer]";
	mes "Hagan sus apuestas...";
	next;
	menu "2z",M_1a, "10z",M_1b, "20z",M_1c, "100z",M_1d, "Demasiado dinero para mi...",M_End;

	M_1a:
		if(Zeny < 2) goto sL_NotEnuf;
		set @bet, 2;
		goto L_Cont0;
	M_1b:
		if(Zeny < 10) goto sL_NotEnuf;
		set @bet, 10;
		goto L_Cont0;
	M_1c:
		if(Zeny < 20) goto sL_NotEnuf;
		set @bet, 20;
		goto L_Cont0;
	M_1d:
		if(Zeny < 100) goto sL_NotEnuf;
		set @bet, 100;
		goto L_Cont0;

		sL_NotEnuf:
			mes "[Dealer]";
			mes "Lo siento pero no tienes suficiente zeny para hacer esa apuesta.";
			close;

	L_Cont0:
	mes "(repartiendo cartas)";
	next;
	deletearray $@card[0],13;
	set @dealerTurn, 0;
	set @numP, 0;
	set @numD, 0;
	set @pAce, 0;
	set @dAce, 0;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;

//==============
L_Start:
	callsub sF_GetTot;

	mes "- Aquí están las cartas del  ^FF5533CROUPIER^000000:";
	if (@numD==2) callsub sF_D2cards;
	if (@numD==3) callsub sF_D3cards;
	if (@numD==4) callsub sF_D4cards;
	if (@numD==5) callsub sF_D5cards;
	mes "    El CROUPIER tiene: ^FF5533"+@dealTot+"^000000";
	mes " ";
	mes "- Aquí están ^5533FFTUS^000000 cartas:";
	if (@numP==2) callsub sF_P2cards;
	if (@numP==3) callsub sF_P3cards;
	if (@numP==4) callsub sF_P4cards;
	if (@numP==5) callsub sF_P5cards;
	if(@pAce != 1 || @playTot == 21) mes "    TÚ tienes: ^5533FF" +@playTot+ "^000000";
	if(@pAce == 1 && @playTot != 21) mes "    Tú tienes: ^5533FF" +@playTot+ "^000000, ó ^5533FF" +(@playTot-10)+ "^000000";
	next;
	if(@playTot==21 && @dealTot==21) goto sL_Push;
	if(@numP==2 && @playTot == 21) goto sL_Win;
	if(@numD==2 && @dealTot == 21) goto sL_Lose;
	if(@playTot > 21) goto sL_Lose;
	if(@dealTot > 21) goto sL_Win;
	if(@numP==2 && @dealerTurn==0) menu "Otra carta",M_Hit, "Me planto",M_Stay, "Doblo",M_Double;
	if(@dealerTurn == 0) menu "Otra carta",M_Hit, "Me planto",M_Stay;

	M_Stay:
		mes "[Dealer]";
		if(@playTot < 13) goto sL_PlayToLow;
		if(@dealTot > 16) mes "El Croupier se planta.";
		if(@dealTot > 16 || @numD == 5) goto L_Check;
		mes "El Croupier va a pedir Otra Carta.";
		next;
		callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
		set @dealerTurn, 1;
		goto L_Start;

		sL_PlayToLow:
			mes "Lo siento pero no tienes puntaje suficiente para plantarte. Debes pedir otra carta.";
			next;
			goto M_Hit;

	M_Hit:
		if(@numP == 5) goto M_Stay;
		callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
		goto L_Start;
	M_Double:
		mes "[Dealer]";
		mes "El jugador ha decidido Doblar. Tu apuesta va a ser";
		mes "doblada, y te será concedida 1 carta más.";
		next;
		set @dealerTurn, 1;
		set @bet, @bet*2;
		callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
		goto L_Start;

//=============
L_Check:
	next;
	if(@playTot < @dealTot) goto sL_Lose;
	if(@playTot == @dealTot) goto sL_Push;

	sL_Win:
		mes "[Dealer]";
		mes "Felicidades! Has ganado!";
		next;
		set Zeny, Zeny + @bet;
		goto L_Play;
	sL_Lose:
		mes "[Dealer]";
		mes "Lo siento pero has perdido.";
		set Zeny, Zeny - @bet;
		next;
		goto L_Play;
	sL_Push:
		mes "[Dealer]";
		mes "Es un empate. Has empatado con el Croupier.";
		next;
		goto L_Play;


//==================================
// Sub function for dealing/pulling the cards
sF_GetCards:
	set @rnd, rand(1,13);
	if($@card[@rnd] == 4) goto sF_GetCards;
	set $@card[@rnd], $@card[@rnd] + 1;
	set getarg(1), @rnd;
	if(getarg(1) > 10) set getarg(1), 10;
	if(getarg(1) == 1 && getarg(3) < 1) set getarg(1), 11;
	if(getarg(1) == 11) set getarg(3), 1;
	set getarg(2), " " + getarg(1) + " ";
	if(@rnd == 10) set getarg(2), getarg(1);
	if(@rnd == 1) set getarg(2), " A ";
	if(@rnd == 11) set getarg(2), " J ";
	if(@rnd == 12) set getarg(2), " Q ";
	if(@rnd == 13) set getarg(2), " K ";
	set getarg(0), getarg(0) + 1;
	return;

//==================================
// Sub function for getting the total score for each hand
sF_GetTot:
	set @i, 0;
	set @dealTot, 0;
	set @playTot, 0;

	GetDealTot:
		set @dealTot, @dealTot + @dealCard[@i];
		set @i, @i + 1;
		if(@i < @numD) goto GetDealTot;
		if(@dAce == 1 && @dealTot > 21) set @dealTot, @dealTot - 10;
		set @i, 0;
	GetPlayTot:
		set @playTot, @playTot + @playCard[@i];
		set @i, @i + 1;
		if(@i < @numP) goto GetPlayTot;
		if(@pAce == 1 && @playTot > 21) set @pAce, 2;
		if(@pAce > 0 && @playTot > 21) set @playTot, @playTot - 10;
	return;

//=======================================
// Sub function for displaying the Cards
sF_D2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_D3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" |   | "+@dealCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_P3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" |   | "+@playCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_D4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@dealCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_P4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" |";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@playCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_D5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[3]+" |   | "+@dealCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[3]+" |   | "+@playCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
}
